+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
+    Program name   :  a1104131.ps
+    Description    :  Create XREF file of Blue Cross MEM IDs for coordination
+                      with Value Options.
+    Author         :  appnc
+    Date created   :  04/14/2011
+    Project Number :  A1104131
+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
+                                REVISIONS
+------------------------------------------------------------------------------
+   apiii  mm/dd/ccyy  A####### - TYP - Comments
+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

. ----------------------------
. Required Program Includes
. ----------------------------

              INC       COMMON.CB            ...Common work area
              INC       X_COMMON.CB          ...OGB Common Work Area
              INC       VALID.RL             ...Valid codes
              INC       X_OGBFILE.CB         ...OGB Custom File Handler
              
. ----------------------------
. Program Specific Includes
. ----------------------------

              INC       MEM.RL
              INC       MEME.RL

. ----------------------------
. File Declarations
. ----------------------------

IN_FILE       FILE      VAR=8,TEXT
IN_NAME       INIT      "/w/a1104131.inp"

IN_LIST       LIST
IN_MEMID      DIM  008
              LISTEND


OUT_FILE      FILE      VAR=18,TEXT
OUT_NAME      INIT      "/w/a1104131.txt"               

OUT_LIST       LIST
OUT_MEMID      DIM  008
OUT_FILLER     DIM  002
OUT_ACCU       DIM  008
               LISTEND
              
. ----------------------------
. Program Specific Variables & Working Storage
. ----------------------------

TIMESTAMP     DIM       14
CURRDATE      DIM       8
WRITE_COUNT   FORM      8
ERROR_COUNT   FORM      8
TRAP_DATA     DIM       500
TODAY         DIM       8
CAL_CLOCK     DIM       21
ORIG_RETURN   FORM      5
CURR_RETURN   FORM      5
FIRST_READ    INIT      "Y"
XTRACT_DATE   INIT      "20100701"
WK_BLUE       INIT      "BLUE"
DISP4         DIM       4
DISP3         DIM       3



PARAM_LIST    LIST
PARAM_FROM    DIM       8
PARAM_THRU    DIM       8 
              LISTEND

. ----------------------------
. Begin Program
. ----------------------------

. ----------------------------
. Start here if selected from menu   
. ----------------------------

         CALL      RUN_NOW

         CHAIN     C_NEXT

. ----------------------------
. Start here if called by the Scheduler    
. ----------------------------

         INC       SCHED_NP.SR              Scheduler routines (no printer)

RUN_NOW

. ...Check return stack
         RETCOUNT  ORIG_RETURN

. ----------------------------
. Fill top of screen
. ----------------------------

. ...Screen Display
         MOVE      "MENU"                TO  C_NEXT
         MOVE      "BLUE CROSS XREF" TO  C_OPER
         MOVE      "a1104131"            TO  C_PROG
         MOVE      BLANKS                TO  C_LABEL
         MOVE      BLANKS                TO  C_FILE
         MOVE      BLANKS                TO  C_KEYS
         CLOCK     CALENDAR            INTO  CAL_CLOCK

         CALL X_BLANK_SCREEN

. ----------------------------
. Set Applicable Traps
. ----------------------------
         
. ...Set Applicable Traps
         TRAPSAVE  TRAP_DATA
         TRAPCLR   ALL
         TRAP      QUIT   NORESET  IF F9
	
. ----------------------------
. Get scheduler information
. ----------------------------

         CALL     SCHED_PICK

         IF (RUN_MODE = "B" OR RUN_MODE = "N")
            CALL      MAIN_PROCESS
         ENDIF
         RETURN

. ----------------------------
. Main Process
. ----------------------------

MAIN_PROCESS
         DEBUG
         CALL      INITIALIZE
         CALL      REFORMAT_MEME         
         CALL      OPEN_FILES
         CALL      DO_WORK
         CALL      CLOSE_FILES
         CALL      SORT_OUTPUT
         CALL      FTP_OUTPUT
         
         
. ...Normal End-Of-Job
         IF (RUN_MODE = "N")
             DIALOG       HORZ="25",VERT="17":
                          BGCOL=C_ERRB,FGCOL=C_ERRF:
                          LNCOL=C_ERRL,SHADE="Y":
                          TEXT="  PROGRAM COMPLETED NORMALLY  ":
                          TEXT="------------------------------":
                          TEXT="  Press any key to continue   ":
                          FIELD=WCHAR 
         ENDIF

. ...Return Control Back to Chain - End of Program
         TRAPREST  TRAP_DATA
         MOVE      SCH_RTSUCC  TO  SCH@RTCODE
         RETURN

. ----------------------------
. Initialize
. ----------------------------

INITIALIZE

         CLOCK     TIMESTAMP   INTO  TIMESTAMP
         CLOCK     TIMESTAMP   INTO  CURRDATE
         CLOCK     TIMESTAMP     TO  TODAY

. ...Display Counters
         MOVE "Input Recs Read  : " TO X_CNTDESC1
         MOVE "Recs Written     : " TO X_CNTDESC2
         MOVE "                 : " TO X_CNTDESC3
         MOVE "                 : " TO X_CNTDESC4
         MOVE "                 : " TO X_CNTDESC5
         MOVE "                 : " TO X_CNTDESC6
         
         CALL X_DISPLAY_COUNTS
         
         RETURN
         
. ----------------------------
. Open Files
. ----------------------------

OPEN_FILES

         MOVE IN_NAME TO C_FILE
         CALL OPEN_OGBFILE USING IN_FILE,IN_NAME
         
         MOVE OUT_NAME TO C_FILE
         CALL PREP_OGBFILE USING OUT_FILE,OUT_NAME

         RETURN

. ----------------------------
. Get File Name
. ----------------------------

REFORMAT_MEME

         MESSAGE HORZ="22",VERT="20":
                 BGCOL=C_DLGB,FGCOL=C_DLGF:
                 LNCOL=C_DLGL,SHADE="y": 
                 TEXT="REFORMATTING MEME ... PLEASE WAIT";
    
         CLEAR SCRATCH
         PACK  SCRATCH WITH "meme.dbf /w/a1104131.inp": 
                            " -p96-103ge#"",XTRACT_DATE,"#"":  
                            " -p112-115eq#"",WK_BLUE,"#" -t -j":
                            " 1-8"
                            
         REFORMAT SCRATCH
         IF OVER
             ERROR HORZ="15",VERT="19":
                   BGCOL=C_ERRB,FGCOL=C_ERRF:
                   LNCOL=C_ERRL,SHADE="Y":
                   TEXT="REFORMAT FAILED(BLUE)"
             CHAIN "MENU"
         ENDIF  
         
         CLEAR SCRATCH
         PACK  SCRATCH WITH "dsort /w/a1104131.inp /w/a1104131.inp 1-8 -t -u"
         
         ROLLOUT SCRATCH
         IF OVER                                            
             ERROR     HORZ="15",VERT="19":                 
                       BGCOL=C_ERRB,FGCOL=C_ERRF:           
                       LNCOL=C_ERRL,SHADE="Y":              
                       TEXT="SORT FAILED - A1104131.INP"    
             CHAIN     "menu"                               
         ENDIF         
         
         DISPLAY        *P1:20,*BGCOLOR=*CYAN,*EL:
                        *P1:21,*BGCOLOR=*CYAN,*EL:
                        *P1:22,*BGCOLOR=*CYAN,*EL:
                        *P1:23,*BGCOLOR=*CYAN,*EL           

         RETURN

. ----------------------------
. Process Input
. ----------------------------

DO_WORK

          LOOP
              CLEAR OUT_LIST
              MOVE OGB_SEQ1 TO OGB_SEQ
              CALL READ_OGBFILE USING IN_FILE,IN_NAME,IN_LIST
              IF (RESULT != 1)
                  CALL X_DISPLAY_COUNTS
                  BREAK
              ENDIF
              
              ADD 1 TO X_CNT1
              UNPACK X_CNT1 INTO DISP4,DISP3
              IF (DISP3 = "000")
                  CALL X_DISPLAY_COUNTS
              ENDIF
              
              PACK MEM_FK11 WITH IN_MEMID,BLANKS
              CALL FILE_READ USING MEM_FD:              ...FILE DECLARATION LIST
                                   MEM_FN:              ...FILE NAME LIST
                                   MEM_FC:              ...TYPE OF INDEX
.                                                       ...(I)NDEX or (A)IMDEX
                                   MEM_FV:              ...FILE KEY VARLISTS
                                   MEM_FK:              ...FILE KEY SIZES
                                   MEM_LIST:            ...FILE RECORD (FIELD LIST)
                                   MEM_REC:             ...FILE RECORD (PACKED)
                                   MEM_OLD:             ...FILE SAVED WHEN READ
                                   MEM_QTY:             ...NUMBER OF FIlES
                                   1                    ...FILE NUMBER FOR THIS READ
                                   
              IF (RESULT != 1)
                   CONTINUE
              ENDIF
              
              IF (SQUEEZE MEM_ACCU = "")
                   CONTINUE
                 ELSE
                   PACK MEME_FK11 WITH MEM_ACCU,BLANKS
                   CALL READ_FOR_XREF
              ENDIF
              
          REPEAT    

          RETURN

. ----------------------------
. Read for XREF in Blue Cross
. ----------------------------

READ_FOR_XREF

     CALL FILE_READ USING MEME_FD:             ...FILE DECLARATION LIST
                          MEME_FN:             ...FILE NAME LIST
                          MEME_FC:             ...TYPE OF INDEX
.                                              ...(I)NDEX or (A)IMDEX
                          MEME_FV:             ...FILE KEY VARLIST
                          MEME_FK:             ...FILE KEY SIZES
                          MEME_LIST:           ...FILE RECORD (FIELD LIST)
                          MEME_REC:            ...FILE RECORD (PACKED)
                          MEME_OLD:            ...FILE SAVED WHEN READ
                          MEME_QTY:            ...NUMBER OF FILES
                          1                    ...FILE NUMBER FOR THIS READ
      LOOP
          CALL FILE_NEXT USING MEME_FD:             ...FILE DECLARATION LIST
                               MEME_FN:             ...FILE NAME LIST
                               MEME_FC:             ...TYPE OF INDEX
.                                                   ...(I)NDEX or (A)IMDEX
                               MEME_FV:             ...FILE KEY VARLIST
                               MEME_FK:             ...FILE KEY SIZES
                               MEME_LIST:           ...FILE RECORD (FIELD LIST)
                               MEME_REC:            ...FILE RECORD (PACKED)
                               MEME_OLD:            ...FILE SAVED WHEN READ
                               MEME_QTY:            ...NUMBER OF FILES
                               1                    ...FILE NUMBER FOR THIS READ
              IF ((RESULT <> 1) OR (MEM_ACCU <> MEME_ID1))
                 BREAK    
              ENDIF
              
              IF (MEME_EFF < "20100701") 
                  CONTINUE
              ENDIF
              
              IF (SQUEEZE MEME_NET != "BLUE")
                  CONTINUE
              ENDIF
              
              IF ((SQUEEZE MEME_LEV2 = "95") AND (SQUEEZE MEME_MODE = ""))
                  CONTINUE
              ENDIF
              IF ((SQUEEZE MEME_LEV2 = "90") AND (SQUEEZE MEME_MODE != ""))
                  CONTINUE
              ENDIF 
              
              ADD 1 TO X_CNT2
              PACK OUT_MEMID WITH MEM_ID1
              PACK OUT_ACCU  WITH MEM_ACCU
              MOVE OUT_NAME TO C_FILE
              CALL WRITE_OGBREC USING OUT_FILE,OUT_NAME,OUT_LIST 
              
          REPEAT    

          RETURN

. ----------------------------
. Close Files
. ----------------------------

CLOSE_FILES

         MOVE IN_NAME TO C_FILE
         CALL CLOSE_OGBFILE USING IN_FILE,IN_NAME
         
         MOVE OUT_NAME TO C_FILE
         CALL CLOSE_OGBFILE USING OUT_FILE,OUT_NAME

         RETURN

. ----------------------------
. Sort Output
. ----------------------------

SORT_OUTPUT

         CLEAR SCRATCH
         PACK  SCRATCH WITH "dsort /w/a1104131.txt /w/a1104131.srt 1-18 -t -u"
         
         ROLLOUT SCRATCH
         IF OVER                                            
             ERROR     HORZ="15",VERT="19":                 
                       BGCOL=C_ERRB,FGCOL=C_ERRF:           
                       LNCOL=C_ERRL,SHADE="Y":              
                       TEXT="SORT FAILED - A1104131.TXT"    
             CHAIN     "menu"                               
         ENDIF          

         RETURN

. ----------------------------
. FTP Output
. ----------------------------

FTP_OUTPUT

         CLEAR SCRATCH
         PACK  SCRATCH WITH "a1104131.ftp"
         ROLLOUT SCRATCH
         IF OVER                                            
             ERROR     HORZ="15",VERT="19":                 
                       BGCOL=C_ERRB,FGCOL=C_ERRF:           
                       LNCOL=C_ERRL,SHADE="Y":              
                       TEXT="FTP FAILED - A1104131.FTP"    
             CHAIN     "menu"                               
         ENDIF                   

         RETURN

. ----------------------------
. Operator pressed F9
. ----------------------------

QUIT
         IF (C_RMODE = "I")
            error     horz="23",vert="19":
                      bgcol=C_ERRB,fgcol=C_ERRF:
                      lncol=C_ERRL,shade="Y":
                      text=" Process Aborted by Operator ":
                      text="-----------------------------"
            messageerase
         ENDIF

         CLEAR     C_NOHIST

         TRAPREST  TRAP_DATA

         IF (RUN_MODE = "B")
            LOOP
               RETCOUNT  CURR_RETURN
               WHILE     (CURR_RETURN > ORIG_RETURN)
               NORETURN
            REPEAT
            MOVE      SCH_RTABRT  TO  SCH@RTCODE
            RETURN
         ENDIF
         CHAIN     "MENU"

. ----------------------------
. Program Specific Includes
. ----------------------------

              INC       X_OGBFILE.IO
              INC       UTIL.SR      
              INC       VALID.IO
              INC       X_STANDARD.SR
              INC       FILE.IO

. +++++++++++++++++++++++++++++++++++++
. /End of A1104131.PS
. +++++++++++++++++++++++++++++++++++++
