++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
+ NAME:
+   a2305311.ps
+
+ DESCRIPTION:
+   Vantage Plan Ending (MHHP only) - default to BCBS Magnolia Local
+   Plus for 1/1/24
+
+ CREATED BY:
+   aphas - 2023-05-31
++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

.# =================================================================== #
.# Variable Includes
.# =================================================================== #
    INC VALID.RL
    INC UTIL_COMMON.CB
    INC UTIL_MEMMEM.CB
    INC CLI.RL
.# =================================================================== #
.# Scheduler Parameters
.# =================================================================== #

PARAM_LIST          LIST WITH NAMES
PARAM_TEST          CHAR    4
                    LISTEND

.# =================================================================== #
.# Equates for code clarity
.# =================================================================== #

V_FLAG              EQU     1   ...0 or 1
V_DATE              EQU     8   ...YYYYMMDD
MSG_HEIGHT          EQU     24
REFRESH_RATE        EQU     100

.# =================================================================== #
.# Configuration
.# =================================================================== #

PROG_ID             INIT    "TRMMHP"
EMAIL_TO_NOERROR    INIT    "$ITAPPS"
EMAIL_TO_ERROR      INIT    "$ITAPPS"
NEW_TERM_DATE       INIT    "20231231"
NEW_EFF_DATE        INIT    "20240101"
NEW_NETWORK         INIT    "BLUE"
NEW_PRODUCT         INIT    "MAGNO"
NEW_PLAN            INIT    "PLUS"

.# =================================================================== #
.# Debugging Configuration
.# =================================================================== #

ENABLE_TEST         NUM     V_FLAG
TEST_CONFIG         LIST
TEST_EMAIL_TO       INIT    "$CURRENT_USER_EMAIL"
TEST_THIS_ID        INIT    "        "
                    LISTEND

.# =================================================================== #
.# File Declarations
.# =================================================================== #

WORK_FD             FILE    FIXED=X_MEME_SIZE,TEXT
WORK_FILE           INIT    "/w/tmp/a2305311.id.wrk"

UPDATE_FD           FILE    FIXED=X_INM_SIZE,TEXT
UPDATE_FILE         INIT    "a2305311.upd"

REPORT_FD           FILE    VAR=200,TEXT
REPORT_FILE         INIT    "/w/tmp/a2305311.csv"

.# =================================================================== #
.# File lists
.# =================================================================== #

WORK_LIST           LIST
WORK_ID             CHAR    8
                    LISTEND

REPORT_LIST         LIST
REPORT_REC          CHAR    200
                    LISTEND

REPORT_HEADER       VARLIST "MEME_ID,":
                            "LOG MESSAGE,":
                            "LAST NAME,":
                            "FIRST NAME,":
                            "RELATION,":
                            "MEME EFF,":
                            "MEME TRM,":
                            "NETWORK,":
                            "PRODUCT,":
                            "PLAN"

.# =================================================================== #
.# Program Variables
.# =================================================================== #

CURRDATE            CHAR    V_DATE
START_DATETIME      CHAR    24
END_DATETIME        CHAR    24
UPDATE_DONE         NUM     1
ORIG_EFF            CHAR    8
ORIG_TRM            CHAR    8
ORIG_TLET           CHAR    1
ORIG_TRUN           CHAR    8
VOID_DATE           CHAR    8
UPDATE_EFF          CHAR    8
UPDATE_NET          CHAR    5
UPDATE_PRD          CHAR    5
UPDATE_PLAN         CHAR    5


.# =================================================================== #
.# Begin Program
.# =================================================================== #
.Manual and Autosys
    CALL RUN_NOW
    CHAIN C_NEXT    ...go to next program (usually the Menu)

.Impact Scheduler Hook
    INC SCHED_NP.SR     ...for jobs that don't use the print system


.#######################################################################
RUN_NOW ROUTINE
+ Configure runtime settings

.Save stack size at program start
    RETCOUNT ORIG_RETURN

.Set applicable traps
    TRAPSAVE TRAP_DATA
    TRAPCLR ALL
    TRAP QUIT NORESET IF F9

.Get run date
    CLOCK TIMESTAMP INTO CURRDATE

    MOVE "a2305311" TO C_PROG        ...Program name
    MOVE "VANTA MHHP TO MLP" TO C_OPER  ...Program description
    MOVE "MENU" TO C_NEXT
    MOVE "RUN_NOW" TO C_LABEL
    MOVE BLANKS TO C_FILE
    MOVE BLANKS TO C_KEYS

.Check if the program was called from the command line
    IF (C_CMDPAR)
        CALL CMD_START USING C_PROG
    ELSE
        CALL X_BLANK_SCREEN
    ENDIF

.Configure test options based on file mode. Default is Production
    CALL UTIL_SET_TESTMODE USING PARAM_TEST:  ...scheduler param
                                 ENABLE_TEST:  ...test toggle
                                 TEST_CONFIG  ...test settings list

.Set up run mode or build SCH record
    CALL SCHED_PICK

    SWITCH RUN_MODE
        CASE "N" | "B"
            CALL MAIN_PROCESS
        DEFAULT
    ENDSWITCH

    CALL NORMAL_EOJ

    RETURN
    ENDROUTINE


.#######################################################################
MAIN_PROCESS LROUTINE
    debug
    CALL INITIALIZE
    CALL MEME_REFORMAT
    CALL CONVERT_PLAN
    CALL CLEAN_UP

    RETURN
    ENDROUTINE


.#######################################################################
INITIALIZE LROUTINE
+ Setup runtime variables
    CLOCK TIMESTAMP TO CURRDATE
    CLOCK CALENDAR TO START_DATETIME

.Get the files and EDI locations from valid 101
    CALL UTIL_GET_SYS_DIRS_COMMON

    CALL UTIL_INIT_COUNTERS USING "MEME Recs Proc'd":
                                  "Updates":
                                  "":
                                  "":
                                  "":
                                  ""

    RETURN
    ENDROUTINE


.#######################################################################
MEME_REFORMAT LROUTINE
+ Sort MEME records for VANTA MHHP plan

    CALL UTIL_XMESSAGE USING "Building work files...",MSG_HEIGHT

.Find current life cov with term
    PACK SCRATCH USING MEME_FNS," ",WORK_FILE," -t":
                        " -p0112-0116eq#"VANTA#"":
                        " -p0117-0121eq#"MHHP#"":
                        " -p0104-0111gt#"20231231#""

    IF (CHOP TEST_THIS_ID > "")
        PACK SCRATCH WITH SCRATCH," -p1-8eq",TEST_THIS_ID
    ENDIF

    CALL UTIL_DO_REFORMAT USING SCRATCH,"Finding MEME Data"

.Find open ended life cov
    PACK DATE_CCYYMMDD WITH BLANKS
    PACK SCRATCH USING MEME_FNS," ",WORK_FILE," -t -a":
                        " -p0112-0116eq#"VANTA#"":
                        " -p0117-0121eq#"MHHP#"":
                        " -p0104-0111eq#"",DATE_CCYYMMDD,"#""

    IF (CHOP TEST_THIS_ID > "")
        PACK SCRATCH WITH SCRATCH," -p1-8eq",TEST_THIS_ID
    ENDIF

    CALL UTIL_DO_REFORMAT USING SCRATCH,"Finding MEME Data"

.Sort file
    PACK SCRATCH WITH WORK_FILE, " ", WORK_FILE," 1-12 -t"
    CALL UTIL_DO_SORT USING SCRATCH, "Sorting work file"

    MESSAGEERASE

    RETURN
    ENDROUTINE


.#######################################################################
CONVERT_PLAN LROUTINE
+ Open and Prepare Files for Program
    debug
    CALL OPEN_OGBFILE USING WORK_FD,WORK_FILE
    CALL PREP_OGBFILE USING UPDATE_FD,UPDATE_FILE

    CALL PREP_OGBFILE USING REPORT_FD, REPORT_FILE
    CALL WRITE_OGBREC_SEQ USING REPORT_FD, REPORT_FILE, REPORT_HEADER

    LOOP
        CALL READ_OGBFILE_SEQ USING WORK_FD, WORK_FILE, MEME_LIST
        WHILE (RESULT = 1)

        CALL UTIL_MEM_FIND USING MEME_ID1
        CONTINUE IF NOT EQUAL

.skip voids
        IF (CHOP MEME_TRM > "")
            CONTINUE IF (CHOP MEME_TRM < MEME_EFF)
        ENDIF

        CALL UTIL_XCOUNTER USING X_CNT1,REFRESH_RATE

        CALL UPDATE_PLAN
    REPEAT

    CALL CLOSE_OGBFILE USING UPDATE_FD,UPDATE_FILE
    CALL CLOSE_OGBFILE USING WORK_FD,WORK_FILE
    CALL UTIL_DELETE_FILE USING WORK_FILE

    RETURN
    ENDROUTINE


.#######################################################################
LOG_UPDATES LROUTINE MSG@
+ Log coverage update
MSG@        CHAR    @
...

    SET UPDATE_DONE
    PACK REPORT_REC WITH    (CHOP MEME_ID1),PIPE:
                            (CHOP MSG@),PIPE:
                            (CHOP MEM_LNAME),PIPE:
                            (CHOP MEM_FNAME),PIPE:
                            (CHOP MEM_REL),PIPE:
                            (CHOP MEME_EFF),PIPE:
                            (CHOP MEME_TRM),PIPE:
                            (CHOP MEME_NET),PIPE:
                            (CHOP MEME_PRD),PIPE:
                            (CHOP MEME_PLAN)

    REPLACE ",;" IN REPORT_REC
    REPLACE "|," IN REPORT_REC

    CALL WRITE_OGBREC_SEQ USING REPORT_FD,REPORT_FILE,REPORT_LIST

    RETURN
    ENDROUTINE


.#######################################################################
UPDATE_PLAN LROUTINE
+ Term Vantage and create new Blue Local Plus

.TERM OLD
    FILL " " IN ORIG_TRM:
                ORIG_TLET:
                ORIG_TRUN

    IF (CHOP MEME_TLET > "")
        PACK ORIG_TLET WITH MEME_TLET
    ENDIF
    IF (CHOP MEME_TRUN > "")
        PACK ORIG_TRUN WITH MEME_TRUN
    ENDIF

.For future plans, void and replace
    IF (MEME_EFF > NEW_TERM_DATE)
        debug

        PACK ORIG_TRM WITH MEME_TRM
        PACK ORIG_EFF WITH MEME_EFF

        CALL UTIL_CALC_DAY_BEFORE USING MEME_EFF,MEME_TRM
        PACK MEME_TLET WITH "V",BLANKS

        CALL WRITE_UPDATE
        CALL LOG_UPDATES USING "VOID FUTURE & REPLACE"
        CALL CREATE_NEW USING ORIG_EFF

.For active plans as of transistion date, split the plan
    ELSE

        PACK ORIG_TRM WITH MEME_TRM

        PACK MEME_TRM WITH NEW_TERM_DATE
        FILL "~" IN MEME_TLET:
                    MEME_TRUN
        CALL WRITE_UPDATE
        CALL LOG_UPDATES USING "OLD PLAN"
        CALL CREATE_NEW USING NEW_EFF_DATE
    ENDIF

    RETURN
    ENDROUTINE


.#######################################################################
CREATE_NEW LROUTINE EFF@
+Create new Blue Cross record
EFF@        CHAR    @
    FILL " " IN MEME_ID2

    PACK MEME_EFF WITH EFF@,BLANKS
    PACK MEME_TRM WITH ORIG_TRM,BLANKS
    PACK MEME_TLET WITH ORIG_TLET,BLANKS
    PACK MEME_TRUN WITH ORIG_TRUN,BLANKS

    IF (CHOP MEME_TRM > "" & MEME_TRM < MEME_EFF)
        debug .how?
    ENDIF

    PACK MEME_NET WITH NEW_NETWORK,BLANKS
    PACK MEME_PRD WITH NEW_PRODUCT,BLANKS
    PACK MEME_PLAN WITH NEW_PLAN,BLANKS

    CALL LOG_UPDATES USING "NEW PLAN"

    CALL WRITE_UPDATE

    RETURN
    ENDROUTINE


.#######################################################################
WRITE_UPDATE LROUTINE
+ Write update record
HOLD_MEM    CHAR    X_MEM_SIZE
...
    PACK HOLD_MEM WITH MEM_LIST
    FILL " " IN MEM_LIST
    PACK MEM_ID1 WITH MEME_ID1,BLANKS

    FILL " " IN INM_LIST
    PACKLEN INM_REC WITH MEM_LIST,MEME_LIST
    UNPACK INM_REC INTO INM_LIST

    CALL UTIL_FORMAT_INME USING PROG_ID

    CALL WRITE_OGBREC_SEQ USING UPDATE_FD,UPDATE_FILE,INM_LIST
    CALL UTIL_XCOUNTER USING X_CNT2,REFRESH_RATE

    UNPACK HOLD_MEM INTO MEM_LIST

    RETURN
    ENDROUTINE


.#######################################################################
CLEAN_UP LROUTINE
+ Cleanup work files and handle output files
    CLOCK CALENDAR INTO END_DATETIME

    CALL UTIL_TRIM_TRAILING_SPACE USING REPORT_FILE,REPORT_FILE
    CALL UTIL_PENDING_BACKUP USING "/w/tmp/",REPORT_FILE,""
    CALL UTIL_PENDING_BACKUP USING UTIL_FILES_DIR,UPDATE_FILE,""
    CALL SEND_EMAIL
    CALL UTIL_DELETE_FILE USING REPORT_FILE

    RETURN
    ENDROUTINE


.#######################################################################
SEND_EMAIL LROUTINE
+ Email the report
REPORT@         CHAR    @
RECIPIENTS      CHAR    200
SUBJECT         CHAR    100
BODY            CHAR    1000
BODY_BASE       CHAR    1000
...

    PACK SCRATCH WITH "Vantage MHHP Default to BCBS Local Plus for 2024"
    IF (UPDATE_DONE)
        PACK SCRATCH WITH SCRATCH," *UPDATES DONE*"
    ELSE
        PACK SCRATCH WITH SCRATCH," *NO UPDATES REQUIRED*"
    ENDIF

    CALL UTIL_SET_SUBJECT USING SCRATCH,ENABLE_TEST,SUBJECT

    PACK BODY_BASE WITH "":
        "Checking for members with Vantage MHHP to default to ":
        "BLUE-MAGNO-PLUS and update."

    CALL UTIL_PACK_BODY_AND_COUNTERS USING BODY,BODY_BASE

    IF (UPDATE_DONE)
        CALL UTIL_SET_RECIPIENTS USING RECIPIENTS,EMAIL_TO_ERROR,TEST_EMAIL_TO
        CALL UTIL_EMAIL_NEOJ USING  C_PROG:
                                    RECIPIENTS:
                                    SUBJECT:
                                    BODY:
                                    START_DATETIME:
                                    END_DATETIME:
                                    REPORT_FILE
    ELSE
        CALL UTIL_SET_RECIPIENTS USING RECIPIENTS,EMAIL_TO_NOERROR,TEST_EMAIL_TO
        CALL UTIL_EMAIL_NEOJ USING  C_PROG:
                                    RECIPIENTS:
                                    SUBJECT:
                                    BODY:
                                    START_DATETIME:
                                    END_DATETIME
    ENDIF

    RETURN
    ENDROUTINE

.# =================================================================== #
.# Subroutines Includes
.# =================================================================== #
    INC UTIL_COMMON.SR
    INC UTIL_TABLE_IO.SR
    INC UTIL_TEST.SR
    INC UTIL_SYSTEM.SR
    INC UTIL_DATA.SR
    INC UTIL_EMAIL.SR
    INC UTIL_FORMAT_UPDATES.SR
    INC UTIL_DATES.SR
    INC UTIL_MEMBERS.SR     .MEM* io routines
    INC UTIL_BILLING.SR     .BIL* io routines
    INC UTIL_CLIENTS.SR     .CLI* io routines

.# =================================================================== #
.# End of Program
.# =================================================================== #
