++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
+ NAME:
+   a2309051.ps
+
+ DESCRIPTION:
+   Clear all the Non-Payment data from MEM records
+
+ CREATED BY:
+   aphas - 2023-09-05
++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

.# =================================================================== #
.# Variable Includes
.# =================================================================== #
    INC VALID.RL
    INC UTIL_COMMON.CB
    INC UTIL_MEMMEM.CB

.# =================================================================== #
.# Scheduler Parameters
.# =================================================================== #

PARAM_LIST          LIST WITH NAMES
PARAM_TEST          CHAR    4
                    LISTEND

.# =================================================================== #
.# Equates for code clarity
.# =================================================================== #

V_FLAG              EQU     1   ...0 or 1
V_DATE              EQU     8   ...YYYYMMDD
MSG_HEIGHT          EQU     24
REFRESH_RATE        EQU     1000

.# =================================================================== #
.# Configuration
.# =================================================================== #

PROG_ID             INIT    "NONPAY"
EMAIL_TO_NOERROR    INIT    "$ITAPPS"
EMAIL_TO_ERROR      INIT    "$ITAPPS"

.# =================================================================== #
.# Debugging Configuration
.# =================================================================== #

ENABLE_TEST         NUM     V_FLAG
TEST_CONFIG         LIST
TEST_EMAIL_TO       INIT    "$CURRENT_USER_EMAIL"
                    LISTEND

.# =================================================================== #
.# File Declarations
.# =================================================================== #

REPORT_FD           FILE    VAR=200,TEXT
REPORT_FILE         INIT    "/w/a2309051.csv"

WORK_FD             FILE    FIXED=X_MEM_SIZE,TEXT
WORK_FILE           INIT    "/w/tmp/a2309051.id.wrk"

UPDATE_FD           FILE    FIXED=X_INM_SIZE,TEXT
UPDATE_FILE         INIT    "a2309051.upd"

.# =================================================================== #
.# File lists
.# =================================================================== #

REPORT_LIST         LIST
REPORT_REC          CHAR    200
                    LISTEND

WORK_LIST           LIST
WORK_ID             CHAR    8
                    LISTEND

REPORT_HEADER       VARLIST "MEM_ID,":
                            "FIRST NAME,":
                            "LAST NAME,":
                            "UPDATE STATUS"

.# =================================================================== #
.# Program Variables
.# =================================================================== #

CURRDATE            CHAR    V_DATE
START_DATETIME      CHAR    24
END_DATETIME        CHAR    24
FOUND_DATA          NUM     1

.# =================================================================== #
.# Begin Program
.# =================================================================== #
.Manual and Autosys
    CALL RUN_NOW
    CHAIN C_NEXT    ...go to next program (usually the Menu)

.Impact Scheduler Hook
    INC SCHED_NP.SR     ...for jobs that don't use the print system
.    INC SCHED_WP.SR  ...for jobs requiring the print system


.#######################################################################
RUN_NOW ROUTINE
+ Configure runtime settings

.Save stack size at program start
    RETCOUNT ORIG_RETURN

.Set applicable traps
    TRAPSAVE TRAP_DATA
    TRAPCLR ALL
    TRAP QUIT NORESET IF F9

.Get run date
    CLOCK TIMESTAMP INTO CURRDATE

    MOVE "a230905" TO C_PROG        ...Program name
    MOVE "CLEAR NON-PAYMENT DATA" TO C_OPER  ...Program description
    MOVE "MENU" TO C_NEXT
    MOVE "RUN_NOW" TO C_LABEL
    MOVE BLANKS TO C_FILE
    MOVE BLANKS TO C_KEYS

.Check if the program was called from the command line
    IF (C_CMDPAR)
        CALL CMD_START USING C_PROG
    ELSE
        CALL X_BLANK_SCREEN
    ENDIF

.Configure test options based on file mode. Default is Production
    CALL UTIL_SET_TESTMODE USING PARAM_TEST:  ...scheduler param
                                 ENABLE_TEST:  ...test toggle
                                 TEST_CONFIG  ...test settings list


.Set up run mode or build SCH record
    CALL SCHED_PICK

    SWITCH RUN_MODE
        CASE "N" | "B"
            CALL MAIN_PROCESS
        DEFAULT
    ENDSWITCH

    CALL NORMAL_EOJ

    RETURN
    ENDROUTINE


.#######################################################################
MAIN_PROCESS LROUTINE
+ Core logic
    debug
    CALL INITIALIZE
    CALL PREP_FILES

    CALL CLEAN_UP

    RETURN
    ENDROUTINE


.#######################################################################
INITIALIZE LROUTINE
+ Setup runtime variables
    CLOCK TIMESTAMP TO CURRDATE
    CLOCK CALENDAR TO START_DATETIME

.Get the files and EDI locations from valid 101
    CALL UTIL_GET_SYS_DIRS_COMMON

    CALL UTIL_INIT_COUNTERS USING "MEM reads":
                                  "Log Updates":
                                  "Updates Made":
                                  "":
                                  "":
                                  ""

    CALL SET_RUN_CONFIG

    RETURN
    ENDROUTINE


.#######################################################################
SET_RUN_CONFIG LROUTINE
+ Configure any runtime settings here

    RETURN
    ENDROUTINE
.#######################################################################
MEM_REFORMAT LROUTINE
+ Sort MEME records for non-payment data

    CALL UTIL_XMESSAGE USING "Building work files...",MSG_HEIGHT

.Find members with non-payment data
    PACK SCRATCH USING MEM_FNS," ",WORK_FILE," -t":
                        " -p0393-0393ne#"" " #"":

    CALL UTIL_DO_REFORMAT USING SCRATCH,"Finding MEM Data"

.Sort file
    PACK SCRATCH WITH WORK_FILE, " ", WORK_FILE," 1-12 -t"
    CALL UTIL_DO_SORT USING SCRATCH, "Sorting work file"

    MESSAGEERASE

    RETURN
    ENDROUTINE
.#######################################################################
PREP_FILES LROUTINE
+Sort MEM records

    CALL MEM_REFORMAT

    CALL PREP_OGBFILE USING REPORT_FD, REPORT_FILE
    CALL WRITE_OGBREC_SEQ USING REPORT_FD, REPORT_FILE, REPORT_HEADER

    CALL OPEN_OGBFILE USING WORK_FD,WORK_FILE
    CALL PREP_OGBFILE USING UPDATE_FD,UPDATE_FILE

    LOOP
        CALL READ_OGBFILE_SEQ USING WORK_FD,WORK_FILE,WORK_LIST
        BREAK IF (RESULT !=1)

        CALL UTIL_MEM_FIND_REAL USING WORK_ID
        CONTINUE IF NOT EQUAL

        CALL UTIL_XCOUNTER USING X_CNT1,REFRESH_RATE

        debug
        CALL LOG_UPDATE USING "Clear Non-payment","Updated"
        CALL CLEAR_NONPAY
    REPEAT

    CALL CLOSE_OGBFILE USING REPORT_FD,REPORT_FILE
    CALL CLOSE_OGBFILE USING WORK_FD,WORK_FILE
    CALL CLOSE_OGBFILE USING UPDATE_FD,UPDATE_FILE

    CALL UTIL_PENDING_BACKUP USING "/w/tmp/",WORK_FILE,""
    CALL UTIL_DELETE_FILE USING WORK_FILE

    RETURN
    ENDROUTINE


.#######################################################################
LOG_UPDATE LROUTINE  MSG@,UPDATE_STATUS@
+ Log an error
MSG@            CHAR    @
UPDATE_STATUS@  CHAR    @
    debug
    SET FOUND_DATA
    PACK REPORT_REC WITH (CHOP MEM_ID1),COMMA:
                        (CHOP MEM_FNAME),COMMA:
                        (CHOP MEM_LNAME),COMMA:
                        (CHOP MSG@),COMMA:
                        (CHOP UPDATE_STATUS@)

    CALL WRITE_OGBREC_SEQ USING REPORT_FD,REPORT_FILE,REPORT_LIST
    CALL UTIL_XCOUNTER USING X_CNT2,REFRESH_RATE

    CLEARADR MSG@,UPDATE_STATUS@

    RETURN
    ENDROUTINE


.#######################################################################
CLEAR_NONPAY LROUTINE
+ Clear Non-Payment field

    FILL "~" IN MEM_INFO
    CALL WRITE_UPDATE

    RETURN
    ENDROUTINE


.#######################################################################
WRITE_UPDATE LROUTINE
+ Write update record
    PACK MEM_REC WITH MEM_LIST
    FILL " " IN INM_LIST
    PACK INM_REC WITH MEM_LIST
    UNPACK INM_REC INTO INM_LIST

    PACK INM_CHGBY WITH PROG_ID,BLANKS
    FILL " " IN INM_CHGDT

    CALL WRITE_OGBREC_SEQ USING UPDATE_FD,UPDATE_FILE,INM_LIST
    CALL UTIL_XCOUNTER USING X_CNT3,REFRESH_RATE

    RETURN
    ENDROUTINE
.#######################################################################
CLEAN_UP LROUTINE
+ Cleanup work files and handle output files
    CLOCK CALENDAR INTO END_DATETIME
    CALL UTIL_TRIM_TRAILING_SPACE USING REPORT_FILE,REPORT_FILE
    CALL UTIL_PENDING_BACKUP USING "/w/",REPORT_FILE,""
    CALL UTIL_PENDING_BACKUP USING UTIL_FILES_DIR,UPDATE_FILE,""
    CALL SEND_EMAIL
    CALL UTIL_DELETE_FILE USING REPORT_FILE

    RETURN
    ENDROUTINE

.#######################################################################
SEND_EMAIL LROUTINE
+ Email the report
REPORT@         CHAR    @
RECIPIENTS      CHAR    200
SUBJECT         CHAR    100
BODY            CHAR    1000
BODY_BASE       CHAR    1000
    debug
    PACK SCRATCH WITH "Clear Non-payment field"
    IF (FOUND_DATA)
        PACK SCRATCH WITH SCRATCH," *MEMBERS UPDATED*"
    ELSE
        PACK SCRATCH WITH SCRATCH," *NO MEMBERS TO UPDATE*"
    ENDIF

    CALL UTIL_SET_SUBJECT USING SCRATCH,ENABLE_TEST,SUBJECT

    PACK BODY_BASE WITH "":
        "Finding Members with Non-payment data to Update . "

    CALL UTIL_PACK_BODY_AND_COUNTERS USING BODY,BODY_BASE

    IF (FOUND_DATA)
        CALL UTIL_SET_RECIPIENTS USING RECIPIENTS,EMAIL_TO_ERROR,TEST_EMAIL_TO
        CALL UTIL_EMAIL_NEOJ USING  C_PROG:
                                    RECIPIENTS:
                                    SUBJECT:
                                    BODY:
                                    START_DATETIME:
                                    END_DATETIME:
                                    REPORT_FILE
    ELSE
        CALL UTIL_SET_RECIPIENTS USING RECIPIENTS,EMAIL_TO_NOERROR,TEST_EMAIL_TO
        CALL UTIL_EMAIL_NEOJ USING  C_PROG:
                                    RECIPIENTS:
                                    SUBJECT:
                                    BODY:
                                    START_DATETIME:
                                    END_DATETIME
    ENDIF

    RETURN
    ENDROUTINE

.# =================================================================== #
.# Subroutines Includes
.# =================================================================== #
    INC UTIL_COMMON.SR
    INC UTIL_TABLE_IO.SR
    INC UTIL_TEST.SR
    INC UTIL_SYSTEM.SR
    INC UTIL_DATA.SR
    INC UTIL_EMAIL.SR
    INC UTIL_MEMBERS.SR     .MEM* io routines
    INC UTIL_BILLING.SR     .BIL* io routines
    INC UTIL_CLIENTS.SR     .CLI* io routines

.# =================================================================== #
.# End of Program
.# =================================================================== #
