++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
+ NAME:
+   a2308161.ps
+
+ DESCRIPTION:
+   Create new ENBA CLIPs based on BASI Life clips
+
+ CREATED BY:
+   apjwh - 2023-08-16
++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

.# =================================================================== #
.# Variable Includes
.# =================================================================== #
    INC VALID.RL
    INC CLIP.RL
    INC CLI.RL
    INC UTIL_COMMON.CB

.# =================================================================== #
.# Scheduler Parameters
.# =================================================================== #

PARAM_LIST          LIST WITH NAMES
PARAM_TEST          CHAR    4
                    LISTEND

.# =================================================================== #
.# Equates for code clarity
.# =================================================================== #

V_FLAG              EQU     1   ...0 or 1
V_DATE              EQU     8   ...YYYYMMDD
MSG_HEIGHT          EQU     24
REFRESH_RATE        EQU     1000

.# =================================================================== #
.# Configuration
.# =================================================================== #

PROG_ID             INIT    "NEWLEB"
EMAIL_TO_NOERROR    INIT    "$ITAPPS"
EMAIL_TO_ERROR      INIT    "$ITAPPS"

NEW_CLIP_EFF        INIT    "20240101"
NEW_PLAN            INIT    "ENBA"

CFG_DO_CLIP_WRITE   NUM     "1" 0.skip, 1.write

.# =================================================================== #
.# Debugging Configuration
.# =================================================================== #

ENABLE_TEST         NUM     V_FLAG
TEST_CONFIG         LIST
TEST_EMAIL_TO       INIT    "$CURRENT_USER_EMAIL"
                    LISTEND

.# =================================================================== #
.# File Declarations
.# =================================================================== #

WORK_FD             FILE    FIXED=X_CLIP_SIZE,TEXT
WORK_FILE           INIT    "/w/tmp/a2308161.id.wrk"

REPORT_FD           FILE    VAR=200,TEXT
REPORT_FILE         INIT    "/w/tmp/a2308161.csv"

.# =================================================================== #
.# File lists
.# =================================================================== #

WORK_LIST           LIST
WORK_ID             CHAR    8
                    LISTEND

REPORT_LIST         LIST
REPORT_REC          CHAR    200
                    LISTEND

REPORT_HEADER       VARLIST "LOG TYPE,":
                            "LOG MESSAGE,":
                            "CLI ID,":
                            "CLIP ID2,":
                            "NETWORK,":
                            "PRODUCT,":
                            "PLAN,":
                            "EFFECTIVE,":
                            "CURRENT TERM,":
                            "RATE"

.# =================================================================== #
.# Program Variables
.# =================================================================== #

CURRDATE            CHAR    V_DATE
START_DATETIME      CHAR    24
END_DATETIME        CHAR    24
FOUND_INSTANCE      NUM     1
CURRENT_CLI         CHAR    8

.# =================================================================== #
.# Begin Program
.# =================================================================== #
.Manual and Autosys
    CALL RUN_NOW
    CHAIN C_NEXT    ...go to next program (usually the Menu)

.Impact Scheduler Hook
    INC SCHED_NP.SR     ...for jobs that don't use the print system


.#######################################################################
RUN_NOW ROUTINE
+ Configure runtime settings

.Save stack size at program start
    RETCOUNT ORIG_RETURN

.Set applicable traps
    TRAPSAVE TRAP_DATA
    TRAPCLR ALL
    TRAP QUIT NORESET IF F9

.Get run date
    CLOCK TIMESTAMP INTO CURRDATE

    MOVE "a2308161" TO C_PROG        ...Program name
    MOVE "NEW ENBA CLIP" TO C_OPER  ...Program description
    MOVE "MENU" TO C_NEXT
    MOVE "RUN_NOW" TO C_LABEL
    MOVE BLANKS TO C_FILE
    MOVE BLANKS TO C_KEYS

.Check if the program was called from the command line
    IF (C_CMDPAR)
        CALL CMD_START USING C_PROG
    ELSE
        CALL X_BLANK_SCREEN
    ENDIF

.Configure test options based on file mode. Default is Production
    CALL UTIL_SET_TESTMODE USING PARAM_TEST:  ...scheduler param
                                 ENABLE_TEST:  ...test toggle
                                 TEST_CONFIG  ...test settings list



.Set up run mode or build SCH record
    CALL SCHED_PICK

    SWITCH RUN_MODE
        CASE "N" | "B"
            CALL MAIN_PROCESS
        DEFAULT
    ENDSWITCH

    CALL NORMAL_EOJ

    RETURN
    ENDROUTINE


.#######################################################################
MAIN_PROCESS LROUTINE
+ Core logic
    debug
    CALL INITIALIZE
    CALL CLIP_REFORMAT
    CALL BUILD_NEW_CLIPS

    CALL CLEAN_UP

    RETURN
    ENDROUTINE


.#######################################################################
INITIALIZE LROUTINE
+ Setup runtime variables
    CLOCK TIMESTAMP TO CURRDATE
    CLOCK CALENDAR TO START_DATETIME

.Get the files and EDI locations from valid 101
    CALL UTIL_GET_SYS_DIRS_COMMON

    CALL UTIL_INIT_COUNTERS USING "Base CLIPs Proc'd":
                                  "New Created":
                                  "Skipped":
                                  "":
                                  "":
                                  ""

    CALL SET_RUN_CONFIG

    RETURN
    ENDROUTINE

.#######################################################################
CLIP_REFORMAT LROUTINE
+ Sort CLIP records for VANTA

    CALL UTIL_XMESSAGE USING "Building work file...",MSG_HEIGHT

.Find current vantage clips with term
    PACK SCRATCH USING CLIP_FNS," ",WORK_FILE," -t":
                        " -p0013-0017eq#"LIFE #"":
                        " -p0027-0030eq#"BASI#"":
                        " -p0033-0040le#"",NEW_CLIP_EFF,"#"":
                        " -p0041-0048gt#"20231231#""

    CALL UTIL_DO_REFORMAT USING SCRATCH,"Finding future termed CLIP's"

.Find open-ended vantage clips
    PACK DATE_CCYYMMDD WITH BLANKS
    PACK SCRATCH USING CLIP_FNS," ",WORK_FILE," -t -a":
                        " -p0013-0017eq#"LIFE #"":
                        " -p0027-0030eq#"BASI#"":
                        " -p0033-0040le#"",NEW_CLIP_EFF,"#"":
                        " -p0041-0048eq#"",DATE_CCYYMMDD,"#""

    CALL UTIL_DO_REFORMAT USING SCRATCH,"Finding active CLIP's"

.Sort file
    PACK SCRATCH WITH WORK_FILE, " ", WORK_FILE," 1-12 -t"
    CALL UTIL_DO_SORT USING SCRATCH, "Sorting work file"

    MESSAGEERASE

    RETURN
    ENDROUTINE


.#######################################################################
BUILD_NEW_CLIPS LROUTINE
    CLEAR CURRENT_CLI

    CALL OPEN_OGBFILE USING WORK_FD,WORK_FILE

    CALL PREP_OGBFILE USING REPORT_FD, REPORT_FILE
    CALL WRITE_OGBREC_SEQ USING REPORT_FD, REPORT_FILE, REPORT_HEADER

    debug
    LOOP
        CALL READ_OGBFILE_SEQ USING WORK_FD, WORK_FILE, CLIP_LIST
        WHILE (RESULT = 1)

        CLEAR CLI_LIST,CLI_REC
        PACK CLIP_REC WITH CLIP_LIST

        IF (CHOP CURRENT_CLI = "" | CHOP CLIP_ID1 != CURRENT_CLI)
            PACK CURRENT_CLI WITH CLIP_ID1

            CALL UTIL_CLI_FIND USING CLIP_ID1
            CONTINUE IF (NOT EQUAL)

            PACK CLI_REC WITH CLI_LIST,BLANKS
        ENDIF

.skip invalid clips to copy
        IF (CHOP CLIP_TRM > "")
            CONTINUE IF (CLIP_TRM < CLIP_EFF)
            CONTINUE IF (CLIP_TRM < NEW_CLIP_EFF)  .just in case
        ENDIF
        CONTINUE IF (CLIP_EFF > NEW_CLIP_EFF)

        CALL UTIL_XCOUNTER USING X_CNT1,REFRESH_RATE

.Format new CLIP
        PACK CLIP_PLAN WITH NEW_PLAN,BLANKS
        PACK CLIP_EFF WITH NEW_CLIP_EFF,BLANKS

.See if this new CLIP has already been created
        CALL CHECK_FOR_MATCH
        IF EQUAL
            debug .untested

            CALL UTIL_XCOUNTER USING X_CNT3,REFRESH_RATE
            CALL WRITE_LOG USING "SKIP","Matching CLIP already created"
            CONTINUE
        ENDIF

        CALL CREATE_CLIP
    REPEAT

    CALL CLOSE_OGBFILE USING WORK_FD,WORK_FILE
    CALL UTIL_DELETE_FILE USING WORK_FILE

    RETURN
    ENDROUTINE


.#######################################################################
CHECK_FOR_MATCH LROUTINE
+ See if a CLIP in the current CLI was already created
MATCH_LIST      VARLIST CLIP_EFF,CLIP_RT,CLIP_NET,CLIP_PRD,CLIP_PLAN
HOLD_CLIP       CHAR    X_CLIP_SIZE
MATCHED         NUM     1
BASE_MATCH      CHAR    100
TEST_MATCH      CHAR    100
...
    PACK HOLD_CLIP WITH CLIP_LIST
    PACK BASE_MATCH WITH MATCH_LIST

    CLEAR   CLIP_LIST:
            MATCHED

    LOOP
        IF (CHOP CLIP_ID2 = "")
            CALL UTIL_CLIP_FIND_BY_PLAN USING CURRENT_CLI,"LIFE","LIFE",NEW_PLAN
        ELSE
            CALL UTIL_CLIP_NEXT_BY_PLAN
        ENDIF
        WHILE EQUAL

        PACK TEST_MATCH WITH MATCH_LIST
        IF (CHOP BASE_MATCH = CHOP TEST_MATCH)
            SET MATCHED
            BREAK
        ENDIF
    REPEAT

    UNPACK HOLD_CLIP INTO CLIP_LIST

    IF (MATCHED)
        SETFLAG EQUAL
    ELSE
        SETFLAG NOT EQUAL
    ENDIF

    RETURN
    ENDROUTINE


.#######################################################################
CREATE_CLIP LROUTINE
    CALL UTIL_XCOUNTER USING X_CNT2,REFRESH_RATE

    FILL " " IN CLIP_ID2
    PACK CLIP_CHGBY WITH PROG_ID,BLANKS
    PACK CLIP_CHGDT WITH CURRDATE
    PACK CLIP_NEWBY WITH CLIP_CHGBY
    PACK CLIP_NEWDT WITH CLIP_NEWDT

    IF NOT (CFG_DO_CLIP_WRITE)
        CALL WRITE_LOG USING "SAMPLE CREATE","New ENBA CLIP would be created"
        RETURN
    ENDIF

    debug

    CALL FILE_WRITE USING   CLIP_FD:        ...File declaration
                            CLIP_FN:        ...File name list
                            CLIP_FC:        ...(I)ndex or (A)imdex
                            CLIP_FV:        ...File key varlists
                            CLIP_FK:        ...File key sizes
                            CLIP_LIST:      ...File record (field list)
                            CLIP_REC:       ...File record (packed)
                            CLIP_OLD:       ...File saved when read
                            CLIP_QTY:       ...Number of files
                            1               ...File number for this read

    CALL WRITE_LOG USING "CREATE","New ENBA CLIP created"
    RETURN
    ENDROUTINE


.#######################################################################
WRITE_LOG LROUTINE TYPE@,MSG@
TYPE@       CHAR    @
MSG@        CHAR    @
...
    SET FOUND_INSTANCE
    PACK REPORT_REC WITH    (CHOP TYPE@),COMMA:
                            (CHOP MSG@),COMMA:
                            (CHOP CLIP_ID1),COMMA:
                            (CHOP CLIP_ID2),COMMA:
                            (CHOP CLIP_NET),COMMA:
                            (CHOP CLIP_PRD),COMMA:
                            (CHOP CLIP_PLAN),COMMA:
                            (CHOP CLIP_EFF),COMMA:
                            (CHOP CLIP_TRM),COMMA:
                            (CHOP CLIP_RT)

    CALL WRITE_OGBREC_SEQ USING REPORT_FD,REPORT_FILE,REPORT_LIST

    RETURN
    ENDROUTINE


.#######################################################################
SET_RUN_CONFIG LROUTINE
+ Configure any runtime settings here

.TODO: delete if not needed

    RETURN
    ENDROUTINE


.#######################################################################
CLEAN_UP LROUTINE
+ Cleanup work files and handle output files
    CLOCK CALENDAR INTO END_DATETIME

    CALL UTIL_TRIM_TRAILING_SPACE USING REPORT_FILE,REPORT_FILE
    CALL UTIL_PENDING_BACKUP USING "/w/tmp/",REPORT_FILE,""
    CALL SEND_EMAIL
    CALL UTIL_DELETE_FILE USING REPORT_FILE

    RETURN
    ENDROUTINE


.#######################################################################
SEND_EMAIL LROUTINE
+ Email the report
REPORT@         CHAR    @
RECIPIENTS      CHAR    200
SUBJECT         CHAR    100
BODY            CHAR    1000
BODY_BASE       CHAR    1000
...

    PACK SCRATCH WITH "ADHOC: LIFE-LIFE-ENBA CLIPs Created"
    IF (FOUND_INSTANCE)
        PACK SCRATCH WITH SCRATCH," *UPDATED*"
    ELSE
        PACK SCRATCH WITH SCRATCH," *NO UPDATE REQUIRED*"
    ENDIF

    CALL UTIL_SET_SUBJECT USING SCRATCH,ENABLE_TEST,SUBJECT

    PACK BODY_BASE WITH "Creating CLIP records for the new Enhanced Basic Life product."

    CALL UTIL_PACK_BODY_AND_COUNTERS USING BODY,BODY_BASE

    IF (FOUND_INSTANCE)
        CALL UTIL_SET_RECIPIENTS USING RECIPIENTS,EMAIL_TO_ERROR,TEST_EMAIL_TO
        CALL UTIL_EMAIL_NEOJ USING  C_PROG:
                                    RECIPIENTS:
                                    SUBJECT:
                                    BODY:
                                    START_DATETIME:
                                    END_DATETIME:
                                    REPORT_FILE
    ELSE
        CALL UTIL_SET_RECIPIENTS USING RECIPIENTS,EMAIL_TO_NOERROR,TEST_EMAIL_TO
        CALL UTIL_EMAIL_NEOJ USING  C_PROG:
                                    RECIPIENTS:
                                    SUBJECT:
                                    BODY:
                                    START_DATETIME:
                                    END_DATETIME
    ENDIF

    RETURN
    ENDROUTINE


.# =================================================================== #
.# Subroutines Includes
.# =================================================================== #
    INC UTIL_COMMON.SR
    INC UTIL_TABLE_IO.SR
    INC UTIL_TEST.SR
    INC UTIL_SYSTEM.SR
    INC UTIL_DATA.SR
    INC UTIL_EMAIL.SR
    INC UTIL_DATES.SR
    INC UTIL_MEMBERS.SR     .MEM* io routines
    INC UTIL_BILLING.SR     .BIL* io routines
    INC UTIL_CLIENTS.SR     .CLI* io routines

.# =================================================================== #
.# End of Program
.# =================================================================== #
