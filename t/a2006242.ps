++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
+ NAME:
+   a2006242.ps
+
+ DESCRIPTION:
+   Fills out spans of MSP reporting for members erroneously changed
+   due to the upload of a 2015 MSP file instead of the 2020 file
+
+ CREATED BY:
+   apjwh - 2020-06-26
+
+ LAST MODIFIED BY:
+   apjwh - 2020-06-26
++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

.# =================================================================== #
.# Variable Includes
.# =================================================================== #
    INC UTIL_COMMON.CB  ... OGB Common variables for UTIL routine
    INC UTIL_MEMMEM.CB
    INC MSPA.RL
    INC VALID.RL

.# =================================================================== #
.# Scheduler Parameters
.# =================================================================== #

PARAM_LIST              LIST WITH NAMES
PARAM_TEST              CHAR    4
                        LISTEND

.# =================================================================== #
.# Equates for code clarity
.# =================================================================== #

V_FLAG                  EQU     1   ...0 or 1
V_DATE                  EQU     8   ...YYYYMMDD
MSG_HEIGHT              EQU     24
REFRESH_RATE            EQU     1000

.# =================================================================== #
.# Configuration
.# =================================================================== #

.TODO: add configuration settings here

.# =================================================================== #
.# Debugging Configuration
.# =================================================================== #

ENABLE_TEST             NUM     V_FLAG
TEST_CONFIG             LIST
.TODO: add test settings
                        LISTEND

.# =================================================================== #
.# File Declarations
.# =================================================================== #

BADMSP_FD               FILE    VAR=MSPX_REC_SIZE,TEXT
BADMSP_FILE             INIT    "/w/a2006242_input.txt"

.# =================================================================== #
.# File lists
.# =================================================================== #

OUTPUT_FD               FILE    VAR=MSPX_REC_SIZE,TEXT
OUTPUT_FILE             INIT    "/w/a2006242_output.txt"

.# =================================================================== #
.# Program Variables
.# =================================================================== #

CURRDATE                CHAR    V_DATE
START_DATETIME          CHAR    24
END_DATETIME            CHAR    24

RUNDATE                 CHAR    V_DATE

HEALTH_MAX              EQU     100
HEALTH_RECS             CHAR    X_MEME_SIZE[HEALTH_MAX]
HEALTH_COUNT            NUM     3

ACTIVE_MAX              EQU     100
ACTIVE_SPANS            CHAR    16[ACTIVE_MAX]
ACTIVE_SPAN_COUNT       NUM     3

ACTIVE_SPAN_LIST        LIST
SPAN_EFF                CHAR    V_DATE
SPAN_TRM                CHAR    V_DATE
SPAN_LAST_ID            CHAR    8
SPAN_LAST_ID2           CHAR    4
SPAN_LAST_COV           CHAR    2
SPAN_NET                CHAR    5
SPAN_PRD                CHAR    5
SPAN_PLAN               CHAR    4
                        LISTEND

.# =================================================================== #
.# Begin Program
.# =================================================================== #
.Manual and Autosys
    CALL RUN_NOW
    CHAIN C_NEXT    ...go to next program (usually the Menu)

.Impact Scheduler Hook
    INC SCHED_NP.SR     ...for jobs that don't use the print system


.#######################################################################
RUN_NOW ROUTINE
+ Configure runtime settings

.Save stack size at program start
    RETCOUNT ORIG_RETURN

.Set applicable traps
    TRAPSAVE TRAP_DATA
    TRAPCLR ALL
    TRAP QUIT NORESET IF F9

.Get run date
    CLOCK TIMESTAMP INTO CURRDATE

    MOVE "A2006242" TO C_PROG        ...Program name
    MOVE "Fix MSP History" TO C_OPER  ...Program description
    MOVE "MENU" TO C_NEXT
    MOVE "RUN_NOW" TO C_LABEL
    MOVE BLANKS TO C_FILE
    MOVE BLANKS TO C_KEYS

.Check if the program was called from the command line
    IF (C_CMDPAR)
        CALL CMD_START USING C_PROG
    ELSE
        CALL X_BLANK_SCREEN
    ENDIF

.Configure test options based on file mode. Default is Production
    CALL UTIL_SET_TESTMODE USING PARAM_TEST:  ...scheduler param
                                 ENABLE_TEST:  ...test toggle
                                 TEST_CONFIG  ...test settings list

.Set up run mode or build SCH record
    CALL SCHED_PICK

    SWITCH RUN_MODE
        CASE "N" | "B"
            CALL MAIN_PROCESS
        DEFAULT
    ENDSWITCH

    CALL NORMAL_EOJ

    RETURN
    ENDROUTINE


.#######################################################################
MAIN_PROCESS LROUTINE
+ Core logic
    debug
    CALL INITIALIZE
    CALL BUILD_MSP_HISTORY
    CALL CLEAN_UP

    RETURN
    ENDROUTINE


.#######################################################################
INITIALIZE LROUTINE
+ Setup runtime variables
    CLOCK TIMESTAMP TO CURRDATE
    CLOCK CALENDAR TO START_DATETIME

    MOVE "Members Processed     " TO X_CNTDESC1
    MOVE "                      " TO X_CNTDESC2
    MOVE "                      " TO X_CNTDESC3
    MOVE "                      " TO X_CNTDESC4
    MOVE "                      " TO X_CNTDESC5
    MOVE "                      " TO X_CNTDESC6

    CALL X_ZERO_COUNTS
    IF (NOT C_CMDPAR)
        CALL X_DISPLAY_COUNTS
    ENDIF

    PACK RUNDATE WITH CURRDATE

    LOADMOD "UTIL_ASORT"

    RETURN
    ENDROUTINE


.#######################################################################
BUILD_MSP_HISTORY LROUTINE
+ Build MSP records for affected members from the 2015 data through
+ current
REC         NUM     7
...

    CALL OPEN_OGBFILE USING BADMSP_FD,BADMSP_FILE
    CALL PREP_OGBFILE USING OUTPUT_FD,OUTPUT_FILE

    MOVE OGB_SEQ1 TO OGB_SEQ
    LOOP
        CALL READ_OGBFILE_SEQ USING BADMSP_FD,BADMSP_FILE,MSPX_LIST
        WHILE (RESULT = 1)

        CONTINUE IF (CHOP MSPX_MEM = "") header or trailer

        PACK BASE_MSP WITH MSPX_LIST

        CALL UTIL_XCOUNTER USING X_CNT1,REFRESH_RATE

        CALL UTIL_MEM_FIND USING MSPX_MEM
        CONTINUE IF NOT EQUAL

        CALL GET_ALL_HEALTH_PLANS   .gets the raw data we care about
        CALL BUILD_ACTIVE_SPANS     .builds the model we need for this program

.Determine per span what sort of changes are necessary
        UNPACK BASE_MSP INTO MSPX_LIST
        PACK BASE_MSP_EFF WITH MSPX_EFF

        FOR REC FROM 1 TO ACTIVE_SPAN_COUNT
            UNPACK ACTIVE_SPANS[REC] INTO ACTIVE_SPAN_LIST

            IF (SPAN_TRM < BASE_MSP_EFF)
                debug
                CONTINUE .let's assume the record we are fixing is the floor

            ELSE IF (SPAN_EFF <= BASE_MSP_EFF)
                debug
                UNPACK BASE_MSP INTO MSPX_LIST

                IF (SPAN_TRM = MSPX_TRM)
                    debug .no change detected - skip trying to fix this member
                    BREAK
                ENDIF

                PACK MSPX_TRM WITH SPAN_TRM
                CALL SET_DCN USING SPAN_LAST_ID
                CALL WRITE_MSP USING "UPDATE"

            ELSE
                debug

.TODO: set the DCN. Id + incrementing number per record

                CALL CMS_BUILD_NEW_MSP USING ACTIVE_SPANS[REC]
                CALL WRITE_MSP USING "ADD"

            ENDIF
        REPEAT
    REPEAT

    CALL CLOSE_OGBFILE USING BADMSP_FD,BADMSP_FILE
    CALL CLOSE_OGBFILE USING OUTPUT_FD,OUTPUT_FILE

    RETURN
    ENDROUTINE


.#######################################################################
GET_ALL_HEALTH_PLANS LROUTINE
+ Get all health plans and sort into ascending date order

.We report on the plasn that OGB coveres claims for:
.   PPO:
.       OGB-PPO: 1/1/70 - 12/31/2012
.   EPO:
.       OGB: 1/1/70 - 6/30/05
.       United -> <done>
.   HMO:
.       Humana -> BCBS
.   all BCBS plans
.       HSA
.       HRA
.       Local/Plus
MEM_MAX             EQU     100
MEM_COUNT           NUM     7
LAST_ID             CHAR    8
PROCESSED_MEM_IDS   CHAR    1000
...
    CLEAR PROCESSED_MEM_IDS,LAST_ID
    PACK CURRENT_MEM WITH MEM_LIST

    FOR MEM_COUNT FROM 1 TO MEM_MAX
        CALL ADD_HEALTH_PLANS USING MEM_ID1
        BREAK IF (CHOP MEM_ACCU = "")

        PACK PROCESSED_MEM_IDS WITH PROCESSED_MEM_IDS,"|",MEM_ID1
        SCAN MEM_ACCU IN PROCESSED_MEM_IDS
        BREAK IF EQUAL

        debug

        CALL UTIL_MEM_FIND USING MEM_ACCU
        WHILE EQUAL
    REPEAT

.Sort our health records into date order
    IF (HEALTH_COUNT > 1)
        ASORT ATYPE="REC":
              ARRAY=HEALTH_RECS:
              AMAX=HEALTH_COUNT:
              SRTFLD1="96-103",SRTORD1="A":
              SRTFLD2="104-111",SRTORD1="D"
    ENDIF

    UNPACK CURRENT_MEM INTO MEM_LIST

    RETURN
    ENDROUTINE


.#######################################################################
ADD_HEALTH_PLANS LROUTINE ID@
+ Get all health plans for the given Id
ID@         CHAR    @
...
    CLEAR MEME_LIST
    LOOP
        debug
        IF (CHOP MEME_ID2 = "")
            CALL UTIL_MEME_FIND USING ID@
        ELSE
            CALL UTIL_MEME_NEXT
        ENDIF
        WHILE EQUAL

        IF (CHOP MEME_TRM = "")
            PACK MEME_TRM WITH LAST_DATE
        ENDIF

.This line is problematic. For the purposes of this process, we will assume
.the effective date sent on the original file should stand. However, the MSP
.spec indicate that the effective date should start at the beginning as an
.active person
        CONTINUE IF (CHOP MEME_TRM < MSPX_EFF)

.Skip the usual suspects of not-really-health lines
        CONTINUE IF (MEME_EFF > MEME_TRM)
        CONTINUE IF (SQUEEZE MEME_LEV2 = "95" & SQUEEZE MEME_MODE = "")
        CONTINUE IF (SQUEEZE MEME_LEV2 = "90" & SQUEEZE MEME_MODE != "")

.Skip COBRA
        CONTINUE IF (CHOP MEME_LEV1 = "5000")

.Skip extra billing records and other records we don't care about
        CALL IS_HEALTH_CLIP USING MEME_NET,MEME_PRD,MEME_PLAN
        CONTINUE OVER

        IF (CHOP MEME_NET = "DHH")
            debug .shouldn't hit this
            CONTINUE
        ENDIF

        CALL UTIL_SAFE_PACK_ARRAY_LIST USING MEME_LIST,HEALTH_RECS,HEALTH_COUNT,HEALTH_MAX
    REPEAT

    RETURN
    ENDROUTINE


.#######################################################################
BUILD_ACTIVE_SPANS LROUTINE
+ Build continuous health records as an Active Covered Individual into
+ spans of coverage. The effective date of the first span should be the
+ start of coverage under the GHP.
+
+ In this case it is floored to what was sent previously
REC         NUM     1
...

.The MSP process' effective date is the start of a period of being an
.Active Covered Individual - it does not change to reflect the start of
.fields such as coverage level, RxBin, etc.
    CLEAR ACTIVE_SPAN_LIST

    FOR REC FROM 1 TO HEALTH_COUNT
        UNPACK HEALTH_RECS[REC] INTO MEME_LIST

        CALL IS_ACTIVE_GHP
        CONTINUE IF NOT EQUAL

        IF (CHOP SPAN_EFF = "")
            CALL PACK_SPAN_FIELDS USING MEME_EFF
            CONTINUE
        ENDIF

        CALL DATE_CALC USING SPAN_TRM,"ADD","DAYS",1,DATE_CCYYMMDD
        IF (MEME_EFF > DATE_CCYYMMDD)
            CALL UTIL_SAFE_PACK_ARRAY_LIST USING ACTIVE_SPAN_LIST,ACTIVE_SPANS,ACTIVE_SPAN_COUNT,ACTIVE_MAX

            CALL PACK_SPAN_FIELDS USING MEME_EFF
        ELSE
            IF (MEME_TRM > SPAN_TRM)
                CALL PACK_SPAN_FIELDS USING ""
            ENDIF
        ENDIF
    REPEAT

    IF (CHOP SPAN_EFF > "")
        CALL UTIL_SAFE_PACK_ARRAY_LIST USING ACTIVE_SPAN_LIST,ACTIVE_SPANS,ACTIVE_SPAN_COUNT,ACTIVE_MAX
        CLEAR ACTIVE_SPAN_LIST
    ENDIF

    RETURN
    ENDROUTINE


.#######################################################################
PACK_SPAN_FIELDS LROUTINE EFF@
+ Fill the span fields with MEME data
EFF@        CHAR    @
...
    IF (CHOP EFF@ > "")
        PACK SPAN_EFF WITH EFF@,BLANKS
    ENDIF
    PACK SPAN_TRM WITH MEME_TRM,BLANKS

    PACK SPAN_LAST_ID WITH MEME_ID1,BLANKS
    PACK SPAN_LAST_ID2 WITH MEME_ID2,BLANKS
    PACK SPAN_LAST_COV WITH MEME_DEPC,BLANKS
    PACK SPAN_NET WITH MEME_NET,BLANKS
    PACK SPAN_PRD WITH MEME_PRD,BLANKS
    PACK SPAN_PLAN WITH MEME_PLAN

    RETURN
    ENDROUTINE


.#######################################################################
IS_ACTIVE_GHP LROUTINE
+ Determine if this MEME records would qualify the member as an Active
+ Covered Individual

    CMATCH "9" IN MEME_LEV2
    IF (EQUAL & MEME_LEV2 != "92")
        SETFLAG NOT EQUAL
        RETURN
    ENDIF

    CMATCH "R" IN MEME_LEV1
    IF EQUAL
        SETFLAG NOT EQUAL
        RETURN
    ENDIF

.TODO: anthing else?

.TODO: This is how the current MSP process handles all of our plans. Is this
.correct? I feel like this should be responsive to dates, thus accounting for
.effective dates older than when BCBS became the HMO/PPO/etc contract holder
    IF (CHOP MEME_NET != "BLUE")
        SETFLAG NOT EQUAL
        RETURN
    ENDIF

    SETFLAG EQUAL

    RETURN
    ENDROUTINE


.#######################################################################
WRITE_MSP LROUTINE CHGTYPE@
+ Write the current MSP record with the provided transaction type to
+ output
CHGTYPE@        CHAR    @
...
    CALL WRITE_MSP_OUTPUT USING CHGTYPE@,OUTPUT_FD,OUTPUT_FILE

.TODO: log this

    RETURN
    ENDROUTINE


.#######################################################################
CLEAN_UP LROUTINE
+ Cleanup work files and handle output files
    CLOCK CALENDAR INTO END_DATETIME

.TODO: final clean up

    RETURN
    ENDROUTINE


.---------------------------------------------------------------------------
.TODO: add the below code to the routine include
.---------------------------------------------------------------------------

.#######################################################################
CMS_BUILD_NEW_MSP LROUTINE SPAN_DATA@
+ Flesh out the MSP data fields. Transaction set at write time.
SPAN_DATA@          CHAR    @
BENEFICIARY_MEM     CHAR    X_MEM_SIZE
PRIMARY_MEM         CHAR    X_MEM_SIZE
...
    FILL " " IN MSPX_LIST

    CLEAR BENEFICIARY_MEM,PRIMARY_MEM
    UNPACK SPAN_DATA@ INTO ACTIVE_SPAN_LIST

    CALL FIND_MEDICARE_ID USING SPAN_LAST_ID,MSPX_HICN

    CALL UTIL_MEM_FIND USING SPAN_LAST_ID
    IF NOT EQUAL
        debug
        CALL UTIL_ERRORMSG USING "Failed MEM read","CMS_BUILD_NEW_MSP"
    ENDIF
    PACK BENEFICIARY_MEM WITH MEM_LIST

.Set beneficiary demographics
    PACK MSPX_LNAM WITH MEM_LNAME,BLANKS
    PACK MSPX_FINI WITH MEM_FNAME,BLANKS
    PACK MSPX_DOB WITH MEM_DOB,BLANKS
    PACK MSPX_SSN WITH MEM_PSSN,BLANKS
    SWITCH MEM_SEX
    CASE "M"
        PACK MSPX_SEXC WITH "1"
    CASE "F"
        PACK MSPX_SEXC WITH "2"
    DEFAULT
        PACK MSPX_SEXC WITH "0"
    ENDSWITCH

    SWITCH MEM_REL
        CASE "01" | "02" | "03"
            PACK MSP_RELC WITH MEM_REL,BLANKS
        DEFAULT
            PACK MSP_RECL WITH "04",BLANKS
    ENDSWITCH

    PACK MSPX_IPOL WITH MEM_ESSN,BLANKS

.Set primary plan member details
    PACK MSPX_EMPS WITH MEM_ESSN
    IF (CHOP MEM_REL = "01")
        PACK MSPX_EMPF WITH MEM_FNAME,BLANKS
        PACK MSPX_EMPL WITH MEM_LNAME,BLANKS
    ELSE
        CLEAR MEM_AIM
        PACK MEM_AIM1 WITH "01X",MEM_ESSN,BLANKS
        PACK MEM_AIM3 WITH "03X01",BLANKS
        CALL UTIL_MEM_AIMFIND_REAL
        IF EQUAL
            PACK PRIMARY_MEM WITH MEM_LIST
            PACK MSPX_EMPF WITH MEM_FNAME,BLANKS
            PACK MSPX_EMPL WITH MEM_LNAME,BLANKS
        ENDIF
    ENDIF

.Note - this could be a 2 if the member has Medicare as primary during this
.period...
    PACK MSPX_STAT WITH "1",BLANKS

.Set unique record Id
    CALL SET_DCN USING SPAN_LAST_ID

.Set plan details
    PACK MSPX_COVT WITH "W",BLANKS
.TODO: should the HRA be sent with a "R"?

    PACK MSPX_EFF WITH SPAN_EFF
    PACK MSPX_TRM WITH SPAN_TRM
    IF (CHOP MSPX_TRM = CHOP LAST_DATE)
        FILL "0" IN MSPX_TRM
    ENDIF

    PACK MSPX_SIZE WITH "2",BLANKS
    PACK MSPX_GPOL WITH BLANKS

    IF (CHOP SPAN_LAST_COV = "" & CHOP PRIMARY_MEM > "")
        UNPACK PRIMARY_MEM INTO MEM_LIST
        CALL FIND_DEPC USING MEM_ID1,SPAN_TRM,SPAN_LAST_COV
    ENDIF
    SWITCH SPAN_LAST_COV
        CASE "EE"
            PACK MSPX_EMPC WITH "1"
        CASE "EC"
            PACK MSPX_EMPC WITH "3"
        CASE "ES" | "FM"
            PACK MSPX_EMPC WITH "2"
        DEFAULT
            FILL " " IN MSPX_EMPC
    ENDSWITCH

.Set PBM data
    PACK MSPX_RXID WITH MEM_ID1,BLANKS

    PACK MSPX_NPLN WITH BLANKS
    PACK MSPX_TFRE WITH BLANKS
    PACK MSPX_PERS WITH BLANKS

.Pharmacy Benefit BIN Number
    CALL SET_PBM_DATA USING SPAN_NET,SPAN_PRD

    CALL SET_TINS USING SPAN_NET

    RETURN
    ENDROUTINE


.#######################################################################
SET_PBM_DATA LROUTINE NET@,PRD@,PLAN@
+ Set PBM data per plan
NET@        CHAR    @
PRD@        CHAR    @
PLAN@       CHAR    @
...
    PACK NET WITH (CHOP NET@)
    PACK PRD WITH (CHOP PRD@)
    PACK PLAN WITH (CHOP PLAN@)

.TODO: read this from a table
    SWITCH NET
        CASE "BLUE"
            SWITCH PRD
                CASE "MAGNO" | "PELCN"
                    SWITCH PLAN
                        CASE "HSA"
                            PACK MSPX_PCN WITH EXP_SCP_PCN
                            PACK MSPX_BIN WITH EXP_SCP_BIN
                        DEFAULT
                            PACK MSPX_PCN WITH MED_IMP_PCN
                            PACK MSPX_BIN WITH MED_IMP_BIN
                    ENDSWITCH
                DEFAULT
                    debug

            ENDSWITCH
        DEFAULT
            debug
            PACK MSPX_PCN WITH BLANKS
            PACK MSPX_BIN WITH BLANKS
    ENDSWITCH

    SWITCH NET@

    SWITCH W_COVCODE
    CASE "FARMCO"
    PACK MSPX_RXGR WITH FARMCO_GHPN
    PACK MSPX_PCN  WITH OGB_PCN
    PACK MSPX_BIN  WITH OGB_BIN
    CASE "HUMASO"
    PACK MSPX_RXGR WITH HUMHMO_GHPN
    PACK MSPX_PCN  WITH HUM_PCN
    PACK MSPX_BIN  WITH HUM_BIN
    CASE "OGBPPO"
    PACK MSPX_RXGR WITH OGBPPO_GHPN
    PACK MSPX_PCN  WITH OGB_PCN
    PACK MSPX_BIN  WITH OGB_BIN
    CASE "UNIEPO"
    PACK MSPX_RXGR WITH UNIEPO_GHPN
    PACK MSPX_PCN  WITH OGB_PCN
    PACK MSPX_BIN  WITH OGB_BIN
    CASE "VANHMO"
    PACK MSPX_RXGR WITH VANHMO_GHPN
    PACK MSPX_PCN  WITH VAN_PCN
    PACK MSPX_BIN  WITH VAN_BIN
    CASE "BLUASO" OR "BLUHMO"
    PACK MSPX_PCN  WITH BLU_PCN
    PACK MSPX_BIN  WITH BLU_BIN
    CASE "BLUMAG" OR "BLUPEL"
    IF (WS_PLAN = "HSA ")
    PACK MSPX_PCN  WITH EXP_SCP_PCN
    PACK MSPX_BIN  WITH EXP_SCP_BIN
    ELSE
    PACK MSPX_PCN  WITH MED_IMP_PCN
    PACK MSPX_BIN  WITH MED_IMP_BIN
    ENDIF
    DEFAULT

    ENDSWITCH

    RETURN
    ENDROUTINE


.#######################################################################
FIND_MEDICARE_ID LROUTINE ID@,MEDID@
+ Get the most recent Medicare Id.
ID@         CHAR    @
MEDID@      CHAR    @
...
    FILL " " IN MEDID@
    CLEAR MEMO_LIST,CURR_EFF,CURR_TRM

    LOOP
        IF (MEMO_ID2 = "")
            CALL UTIL_MEMO_FIND USING ID@,""
        ELSE
            CALL UTIL_MEMO_NEXT
        ENDIF
        WHILE EQUAL

        SWITCH MEMO_TYPE
            CASE "MEDA" | "MEDB"
            DEFAULT
                CONTINUE
        ENDSWITCH
        CONTINUE IF (CHOP MEMO_POL = "")

        IF (CHOP MEMO_EFF > "" & MEMO_EFF > CURR_EFF)
            PACK MEDID@ WITH MEMO_POL
            PACK CURR_EFF WITH MEMO_EFF
            PACK CURR_TRM WITH MEMO_TRM
        ELSE IF (CHOP MEMO_EFF1 > "")
            PACK MEDID@ WITH MEMO_POL
            PACK CURR_EFF WITH MEMO_EFF1
            PACK CURR_TRM WITH MEMO_TRM1
        ENDIF
        BREAK IF (CURR_TRM = "" & CHOP MEDID@ > "")
    REPEAT

    RETURN
    ENDROUTINE


.#######################################################################
SET_TINS LROUTINE NET@
+ Find the Tax Id fields
NET@        CHAR    @
...
.Set employer TIN to OGB
    PACK MSPX_ETIN WITH OGB_TIN,BLANKS

.Set carrier TIN
    CLEAR ANA_AIM1
    PACK ANA_AIM1 WITH "01X",NET@,BLANKS
    CALL FILE_READ USING ANA_FD:      ...File Declaration List
                         ANA_FN:      ...File Name List
                         ANA_FC:      ...(I)ndex Or (A)imdex
                         ANA_FV:      ...File Key Varlists
                         ANA_AIM:     ...File Key Sizes
                         ANA_LIST:    ...File Record (Field List)
                         ANA_REC:     ...File Record (Packed)
                         ANA_OLD:     ...File Saved When Read
                         ANA_QTY:     ...Number Of Files
                         2            ...File Number For This Read
    IF (RESULT = 1)
        PACK MSPX_ITIN WITH ANA_TAX,BLANKS
    ELSE
        PACK MSPX_ITIN WITH OGB_TIN,BLANKS
    ENDIF

    RETURN
    ENDROUTINE


.#######################################################################
SET_DCN LROUTINE ID@
+ Generate a unique id for this record for this file
ID@                 CHAR    @
LAST_ID             CHAR    8
INTERNAL_COUNT      NUM     7
...
    CLEAR MSPX_DCN

    IF (CHOP ID@ != CHOP LAST_ID)
        CLEAR INTERNAL_COUNT
    ENDIF

    ADD 1 TO INTERNAL_COUNT

    PACK MSPX_MEM WITH ID@,BLANKS
    PACK MSPX_JJJ WITH (CHAR INTERNAL_COUNT)
    REPLACE " 0" IN MSPX_JJJ

    PACK LAST_ID WITH ID@,BLANKS

    RETURN
    ENDROUTINE


.#######################################################################
WRITE_MSP_OUTPUT LROUTINE CHGTYPE@,OUT_FD@,OUT_FILE@
+ Flesh out the MSP data fields
CHGTYPE@        CHAR    @
OUT_FD@         FILE    @
OUT_FILE@       CHAR    @
...
    SWITCH CHGTYPE@
        CASE "ADD"
            PACK MSPX_TRXT WITH "0"
        CASE "DELETE"
            PACK MSPX_TRXT WITH "1"
        CASE "UPDATE"
            PACK MSPX_TRXT WITH "2"
        DEFAULT
            debug
            CALL UTIL_ERRORMSG USING ("Invalid CMS change type ["+CHGTYPE@+"]"),"WRITE_MSP"
    ENDSWITCH

    CALL WRITE_OGBREC_SEQ USING OUT_FD@,OUT_FILE@,MSPX_LIST

    RETURN
    ENDROUTINE


.# =================================================================== #
.# Subroutines Includes
.# =================================================================== #
    INC VALID.IO
    INC UTIL_COMMON.SR
    INC UTIL_TEST.SR
    INC UTIL_SYSTEM.SR
    INC UTIL_MEM.SR
    INC UTIL_MEME.SR
    INC UTIL_DATA.SR
    INC FILTER_NON_HEALTH.SR

.# =================================================================== #
.# End of Program
.# =================================================================== #
