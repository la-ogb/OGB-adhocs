++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
+ NAME:
+   a2105171.ps
+
+ DESCRIPTION:
+   Queue members from enrollment lines with mid-month effective dates
+
+ CREATED BY:
+   apjwh - 2021-05-17
++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

.# =================================================================== #
.# Variable Includes
.# =================================================================== #
    INC VALID.RL
    INC UTIL_COMMON.CB
    INC MEM.RL
    INC MEMI.RL

.# =================================================================== #
.# Scheduler Parameters
.# =================================================================== #

PARAM_LIST          LIST WITH NAMES
PARAM_TEST          CHAR    4
                    LISTEND

.# =================================================================== #
.# Equates for code clarity
.# =================================================================== #

V_FLAG              EQU     1   ...0 or 1
V_DATE              EQU     8   ...YYYYMMDD
MSG_HEIGHT          EQU     24
REFRESH_RATE        EQU     25

.# =================================================================== #
.# Configuration
.# =================================================================== #

CFG_EMAILTO         INIT    "${ITAPPS}"

.# =================================================================== #
.# Debugging Configuration
.# =================================================================== #

ENABLE_TEST         NUM     V_FLAG
TEST_CONFIG         LIST
TEST_SKIP_DEL       NUM     "0"
TEST_EMAILTO        INIT    "${CURRENT_USER_EMAIL}"
                    LISTEND

.# =================================================================== #
.# File Declarations
.# =================================================================== #

.none

.# =================================================================== #
.# File lists
.# =================================================================== #

.none

.# =================================================================== #
.# Program Variables
.# =================================================================== #

CURRDATE            CHAR    V_DATE
START_DATETIME      CHAR    24
END_DATETIME        CHAR    24

.# =================================================================== #
.# Begin Program
.# =================================================================== #
.Manual and Autosys
    CALL RUN_NOW
    CHAIN C_NEXT    ...go to next program (usually the Menu)

.Impact Scheduler Hook
    INC SCHED_NP.SR     ...for jobs that don't use the print system


.#######################################################################
RUN_NOW ROUTINE
+ Configure runtime settings

.Save stack size at program start
    RETCOUNT ORIG_RETURN

.Set applicable traps
    TRAPSAVE TRAP_DATA
    TRAPCLR ALL
    TRAP QUIT NORESET IF F9

.Get run date
    CLOCK TIMESTAMP INTO CURRDATE

    MOVE "A2105171" TO C_PROG        ...Program name
    MOVE "MD0001 MEMI CLEAN" TO C_OPER  ...Program description
    MOVE "MENU" TO C_NEXT
    MOVE "RUN_NOW" TO C_LABEL
    MOVE BLANKS TO C_FILE
    MOVE BLANKS TO C_KEYS

.Check if the program was called from the command line
    IF (C_CMDPAR)
        CALL CMD_START USING C_PROG
    ELSE
        CALL X_BLANK_SCREEN
    ENDIF

.Configure test options based on file mode. Default is Production
    CALL UTIL_SET_TESTMODE USING PARAM_TEST:  ...scheduler param
                                 ENABLE_TEST:  ...test toggle
                                 TEST_CONFIG  ...test settings list

.Set up run mode or build SCH record
    CALL SCHED_PICK

    SWITCH RUN_MODE
        CASE "N" | "B"
            CALL MAIN_PROCESS
        DEFAULT
    ENDSWITCH

    CALL NORMAL_EOJ

    RETURN
    ENDROUTINE


.#######################################################################
MAIN_PROCESS LROUTINE
+ Core logic
    debug
    CALL INITIALIZE
    CALL DELETE_BAD_MEMI
    CALL CLEAN_UP

    RETURN
    ENDROUTINE


.#######################################################################
INITIALIZE LROUTINE
+ Setup runtime variables
    CLOCK TIMESTAMP TO CURRDATE
    CLOCK CALENDAR TO START_DATETIME

.Get the files and EDI locations from valid 101
    CALL UTIL_GET_SYS_DIRS_COMMON

    CALL UTIL_INIT_COUNTERS USING "MEMI Recs Reviewed":
                                  "Deletes Done":
                                  "Blank Cat/Alt Ids":
                                  "MD0001 Errors":
                                  "":
                                  ""

    CALL SET_RUN_CONFIG

    RETURN
    ENDROUTINE


.#######################################################################
SET_RUN_CONFIG LROUTINE
+ Configure any runtime settings here
    debug

    RETURN
    ENDROUTINE


.#######################################################################
DELETE_BAD_MEMI LROUTINE
+ Delete the badly formatted MEMI records
    debug
    FILL "0" IN MEMI_KEY
    CALL MEMI_READ
    LOOP
        CALL MEMI_NEXT
        WHILE (RESULT = 1)

        debug
        CALL UTIL_XCOUNTER USING X_CNT1,REFRESH_RATE

        IF (CHOP MEMI_ALT = "" | CHOP MEMI_CAT = "")
            debug
            CALL UTIL_XCOUNTER USING X_CNT3,REFRESH_RATE
            CALL DO_MEMI_DEL
        ELSE IF (CHOP MEMI_CAT = "MD0")
            debug
            MATCH "001" IN MEMI_ALT
            IF EQUAL
                CALL UTIL_XCOUNTER USING X_CNT4,REFRESH_RATE
                CALL DO_MEMI_DEL
            ENDIF
        ENDIF
    REPEAT

    RETURN
    ENDROUTINE


.#######################################################################
DO_MEMI_DEL LROUTINE

    debug
    CALL UTIL_XCOUNTER USING X_CNT2,REFRESH_RATE
    IF NOT (TEST_SKIP_DEL)
        CALL MEMI_DEL
    ENDIF
    RETURN
    ENDROUTINE


.#######################################################################
CLEAN_UP LROUTINE
+ Cleanup work files and handle output files
    CLOCK CALENDAR INTO END_DATETIME
    debug

    CALL SEND_EMAIL

    RETURN
    ENDROUTINE


.#######################################################################
SEND_EMAIL LROUTINE
RECIPIENTS  CHAR    200
SUBJECT     CHAR    200
BODY        CHAR    1000
...
    CALL UTIL_SET_RECIPIENTS USING RECIPIENTS,CFG_EMAILTO,TEST_EMAILTO
    CALL UTIL_SET_SUBJECT USING (C_PROG+": Delete Badly Formatted MEMI from MD0001"):
                                ENABLE_TEST:
                                SUBJECT
    PACK SCRATCH WITH "":
        "\n\tProcess to delete badly formatted MEMI records created by MD0001."
    IF (TEST_SKIP_DEL)
        PACK SCRATCH WITH SCRATCH,"\n":
            "\n\tCurrent test settings skip the delete."
    ENDIF
    CALL UTIL_PACK_BODY_AND_COUNTERS WITH BODY,SCRATCH

    CALL UTIL_EMAIL_NEOJ USING C_PROG:
                               RECIPIENTS:
                               SUBJECT:
                               BODY:
                               START_DATETIME:
                               END_DATETIME

    RETURN
    ENDROUTINE


.# =================================================================== #
.# Subroutines Includes
.# =================================================================== #
    INC VALID.IO
    INC UTIL_COMMON.SR
    INC UTIL_VALID.SR
    INC UTIL_TEST.SR
    INC UTIL_SYSTEM.SR
    INC UTIL_DATA.SR
    INC UTIL_EMAIL.SR
    INC UTIL_MEMBERS.SR
    INC UTIL_BILLING.SR
.    INC MEMI.IO

.# =================================================================== #
.# End of Program
.# =================================================================== #
