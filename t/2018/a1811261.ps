./* ====================================================================
. *   Program Name : a1811261.ps
. *   Description : Default LSU OPT2 plans to OPT1 for 1/1/19
. *
. *   Git Repo: https://git.la.gov/OGB/OGB-adhocs
. * ...........................Revisions................................
. * apjwh - 11/26/2018 - DEV - Initial Version
. * ....................................................................
. */

.# =================================================================== #
.# Variable Includes
.# =================================================================== #

    INC UTIL_COMMON.CB  ...OGB Common variables for UTIL routine
    INC VALID.RL  ...Valid codes
    INC MEM.RL  ...Demographics fields
    INC MEME.RL  ...Enrollment fields
    INC D_MEMMEM.CB  ...MEM Update Layout

.# =================================================================== #
.# Scheduler Parameters
.# =================================================================== #

PARAM_LIST          LIST WITH NAMES
PARAM_ISTEST        CHAR    1   ...Enable test mode
                    LISTEND

.# =================================================================== #
.# Equates for code clarity
.# =================================================================== #

FLAG                EQU     1
DATE                EQU     8

.# =================================================================== #
.# File Declarations
.# =================================================================== #

WORK_FILE           FILE    VAR=X_MEME_SIZE,TEXT
WORK_NAME           INIT    "/w/a1811261.tmp"

UPDATE_FILE         FILE    VAR=X_INM_SIZE,TEXT
UPDATE_NAME         INIT    "a1811261.inm"

.# =================================================================== #
.# Debugging Configuration
.# =================================================================== #

ENABLE_TEST         NUM     FLAG
TEST_CONFIG         LIST
TEST_SKIP_REFORMAT  NUM     FLAG
                    LISTEND

.# =================================================================== #
.# Program Variables
.# =================================================================== #

PROG_ID             INIT    "TRMOP2"

CURRDATE            CHAR    DATE
FIRST_OF_YEAR       CHAR    DATE
END_OF_YEAR         CHAR    DATE

.// --------------------------------------------------------------------

.# =================================================================== #
.# Begin Program
.# =================================================================== #

...Manual and Autosys
    CALL RUN_NOW
    CHAIN C_NEXT    ...go to next program (usually the Menu)

...Impact Scheduler Hook
    INC SCHED_NP.SR     ...for jobs that don't use the print system
.//    INC SCHED_WP.SR  ...for jobs requiring the print system

.#######################################################################
RUN_NOW ROUTINE
./* Set program specific variables and establish the next chain link
.   if applicable. Then run the program's main logic. */

... Save stack size at program start
    RETCOUNT ORIG_RETURN

...Set applicable traps
    TRAPSAVE TRAP_DATA
    TRAPCLR ALL
    TRAP QUIT NORESET IF F9

...Get run date
    CLOCK TIMESTAMP INTO CURRDATE

    MOVE "A1811261" TO C_PROG        ...Program name
    MOVE "LSU OPT2 DEFAULT" TO C_OPER  ...Program description
    MOVE "MENU" TO C_NEXT
    MOVE "RUN_NOW" TO C_LABEL
    MOVE BLANKS TO C_FILE
    MOVE BLANKS TO C_KEYS

...Check if the program was called from the command line
    IF (C_CMDPAR)
        CALL CMD_START USING C_PROG
    ENDIF

...Init the counters
    CALL X_ZERO_COUNTS
    IF NOT (C_CMDPAR)
        MOVE "LSU OPT2s Found  " TO X_CNTDESC1
        MOVE "Members Updated  " TO X_CNTDESC2
        MOVE "Not Used         " TO X_CNTDESC3
        MOVE "Not Used         " TO X_CNTDESC4
        MOVE "Not Used         " TO X_CNTDESC5
        MOVE "Not Used         " TO X_CNTDESC6
        CALL X_BLANK_SCREEN
        CALL X_DISPLAY_COUNTS
    ENDIF

    CALL SCHED_PICK

    SWITCH RUN_MODE
        CASE "N" | "B"
            CALL MAIN_PROCESS
        DEFAULT
    ENDSWITCH

    CALL NORMAL_EOJ

    RETURN
    ENDROUTINE


.#######################################################################
MAIN_PROCESS ROUTINE
./* Do all of the work
. */
    debug ...start of process
    CALL INIT_RUN
    CALL PROCESS_MEMES
    CALL FINISH_UP

    RETURN
    ENDROUTINE


.#######################################################################
INIT_RUN LROUTINE
./* Set up files and runtime variables
. */

    IF NOT (ENABLE_TEST)
        CLEAR TEST_CONFIG
    ENDIF

    CALL PREP_OGBFILE USING UPDATE_FILE,UPDATE_NAME

    PACK SCRATCH WITH "meme.dbf ",WORK_NAME," ":
                      "-p112-116eq#"LSUSH#" ":  ...MEME_NET
                      "-p126-129eq#"OPT2#" ":   ...MEME_PLAN
                      "-t"

    IF NOT (TEST_SKIP_REFORMAT)
        CALL UTIL_XMESSAGE USING "Reformatting MEME for LSU OPT2 plans",28
        REFORMAT SCRATCH
        IF (OVER)
            CALL UTIL_ERRORMSG USING "Reformat for LSU OPT2 recs failed","INIT_RUN"
        ENDIF
        MESSAGEERASE
    ENDIF

    CALL OPEN_OGBFILE USING WORK_FILE,WORK_NAME

    PACK END_OF_YEAR WITH "20181231"
    PACK FIRST_OF_YEAR WITH "20190101"

    RETURN
    ENDROUTINE


.#######################################################################
PROCESS_MEMES LROUTINE
./* For each OPT2, filter bad ones, and split into OPT2/OPT1 for 1/1/19
. */
SAVE_MEME       CHAR    X_MEME_SIZE
...
    debug
    MOVE OGB_SEQ1 TO OGB_SEQ
    CALL UTIL_XMESSAGE USING "Processing LSU OPT2 plans",28
    LOOP
        debug
        CLEAR SAVE_MEME:
              MEM_LIST:
              MEME_LIST

        CALL READ_OGBFILE USING WORK_FILE,WORK_NAME,MEME_LIST
        WHILE (RESULT = 1)

        CALL UTIL_XCOUNTER USING X_CNT1,1

...Catch bad MEME's to update
        CONTINUE IF NOT (SQUEEZE MEME_NET = "LSUSH" & SQUEEZE MEME_PLAN = "OPT2")
        IF (SQUEEZE MEME_TRM = "")
            PACK MEME_TRM WITH LAST_DATE
        ENDIF
        CONTINUE IF (MEME_TRM <= END_OF_YEAR | MEME_TRM < MEME_EFF)

        CALL UTIL_MEM_FIND USING MEME_ID1
        CONTINUE IF NOT (EQUAL)

        CALL UTIL_XCOUNTER USING X_CNT2,1

...Common fields
        PACK MEME_CHGBY WITH PROG_ID,BLANKS
        PACK MEME_CHGDT WITH CURRDATE,BLANKS
        IF (MEME_TRM = LAST_DATE)
            FILL " " IN MEME_TRM
        ENDIF

...Snapshot of the current record
        PACK SAVE_MEME WITH MEME_LIST

...Build the new record
        FILL " " IN MEME_ID2
        PACK MEME_PLAN WITH "OPT1",BLANKS
        PACK MEME_EFF WITH FIRST_OF_YEAR,BLANKS
        PACK MEME_NEWBY WITH PROG_ID,BLANKS
        PACK MEME_NEWDT WITH CURRDATE,BLANKS

        PACKLEN INM_REC WITH MEM_LIST,MEME_LIST
        UNPACK INM_REC INTO INM_LIST
        CALL WRITE_OGBREC USING UPDATE_FILE,UPDATE_NAME,INM_LIST

./* Term the current coverage, removing any term-fields since they should
. * now carry over to the OPT1 plan
. */
        UNPACK SAVE_MEME INTO MEME_LIST
        PACK MEME_TRM WITH END_OF_YEAR
        FILL "~" IN MEME_TLET:
                    MEME_TRUN

        PACKLEN INM_REC WITH MEM_LIST,MEME_LIST
        UNPACK INM_REC INTO INM_LIST
        CALL WRITE_OGBREC USING UPDATE_FILE,UPDATE_NAME,INM_LIST
    REPEAT

    RETURN
    ENDROUTINE


.#######################################################################
FINISH_UP LROUTINE
./* Close files and cleanup work data
. */
    IF NOT (C_CMDPAR)
        CALL X_DISPLAY_COUNTS
    ENDIF

    CALL CLOSE_OGBFILE USING WORK_FILE,WORK_NAME

    IF NOT (TEST_SKIP_REFORMAT)
        ERASE WORK_NAME
    ENDIF

    CALL CLOSE_OGBFILE USING UPDATE_FILE,UPDATE_NAME

    RETURN
    ENDROUTINE


.# =================================================================== #
.# Routine Includes
.# =================================================================== #

    INC VALID.IO  ...MCSI's valid code table IO routines
    INC UTIL_COMMON.SR  ...OGB Compilation of Standard UTIL routines
    INC UTIL_MEM.SR  ...MEM read routines

.# =================================================================== #
.# End Program
.# =================================================================== #
