++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
+    File name      :  a1102142.ps   
+    Description    :  Term PEC for Active Members under 19                    
+    Author         :  Loria Patterson
+    Project #      :  A1102142
+    Date created   :  03/22/2011
++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
+                             Revisions
+    apazw 06/12/2017 - mod - increase file size
++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

              INC       COMMON.CB
              INC       X_COMMON.CB
              INC       X_OGBFILE.CB
              INC       VALID.RL

.----------------------------------------------------------------------------
.Program Specific Includes
.----------------------------------------------------------------------------
              INC       MEM.RL                Member Demographics File
              INC       MEME.RL               Member Enrollment         
              INC       D_MEMMEM.CB         

.----------------------------------------------------------------------------
.Input File Declaration   
.----------------------------------------------------------------------------

ACTIVE_FILE     FILE      VAR=12,TEXT      
ACTIVE_NAME     INIT      "/w/a1102142.act"
 	    	 
ACTIVE_LIST      LIST 	    	 
ACTIVE_ID1       DIM  8
ACTIVE_ID2       DIM  4
                 LISTEND
.----------------------------------------------------------------------------
.Output File Lists                                 
.----------------------------------------------------------------------------

ACTOUT_FILE     FILE     VAR=X_INM_SIZE,TEXT
ACTOUT_NAME     INIT     "a1102142.txt"

ACTOUT_SRTKEY     INIT    "1-8  -u -t"

.----------------------------------------------------------------------------
.Program Specific Variables & Working Storage
.----------------------------------------------------------------------------
FIRST_MEME_READ DIM       1
WS_AGE          DIM       3
EXT_DATE        INIT      "20110701"
EXP_DATE        INIT      "20110630"
DISP3           DIM       3
DISP4           DIM       4
LPTR            FORM      2
DATE1           DIM       8
TIMESTAMP       DIM       14
CURRDATE        DIM       8
SPACES          DIM       8
TRAP_DATA       DIM       500
CAL_CLOCK       DIM       21
ORIG_RETURN     FORM      5
CURR_RETURN     FORM      5
TODAY           DIM       8
VALID_DATE      DIM       1          
YN_ANS          DIM       1          
BLANK8          INIT      "        "
SPACE1          INIT      " "
SPACE30         DIM       30 

. ...Report Fields

. ...Printing Controls

PARAM_LIST    LIST
PARAM_XXX     DIM      10
PARAM_YYY     DIM       5
PARAM_DATE    DIM       8
              LISTEND

. ----------------------------
. Standard report headings
. ----------------------------

. ----------------------------
. Begin Program
. ----------------------------

. ---------------------------------
. Start here if selected from menu   
. ---------------------------------

         CALL      RUN_NOW

         CHAIN     C_NEXT

. --------------------------------------
. Start here if called by the Scheduler    
. --------------------------------------

         INC       SCHED_NP.SR              Scheduler routines (W/O printer)

RUN_NOW

. ...Check return stack
         RETCOUNT  ORIG_RETURN

. ----------------------------
. Fill top of screen
. ----------------------------

. ...Screen Display
.        MOVE      "MENU"              TO  C_NEXT
         MOVE      "TERM PEC"          TO  C_OPER
         MOVE      "a1102142"          TO  C_PROG
         MOVE      BLANKS              TO  C_LABEL
         MOVE      BLANKS              TO  C_FILE
         MOVE      BLANKS              TO  C_KEYS
         CLOCK     CALENDAR            INTO  CAL_CLOCK
         
         CALL X_BLANK_SCREEN

. ----------------------------
. Set Applicable Traps
. ----------------------------
         
. ...Set Applicable Traps
         TRAPSAVE  TRAP_DATA
         TRAPCLR   ALL
         TRAP      QUIT   NORESET  IF F9
	
. ----------------------------
. Get scheduler information
. ----------------------------

         CALL     SCHED_PICK

         IF (RUN_MODE = "B" OR RUN_MODE = "N")
            CALL      MAIN_PROCESS
         ENDIF
         RETURN

+++++++++++++++++<BEGIN MAIN PROCESS>++++++++++++++++++++++++++++++++++++++++

. ----------------------------
. Main Process
. ----------------------------

MAIN_PROCESS
   Debug
      CALL  INITIALIZE
      CALL  REFORMAT_MEME
      CALL  OPEN_FILES 
      CALL  EXT_ACTIVE      
      CALL  CLOSE_FILES

      MOVE ACTOUT_NAME TO C_FILE
      CALL OPEN_OGBFILE USING ACTOUT_FILE,ACTOUT_NAME 
      CALL SORT_OGBFILE USING ACTOUT_NAME,ACTOUT_NAME,ACTOUT_SRTKEY
      IF OVER
          ERROR HORZ="15",VERT="19":
                BGCOL=C_ERRB,FGCOL=C_ERRF:
                LNCOL=C_ERRL,SHADE="Y":
                TEXT="SORT FAILED(a1102142.txt)"
          CHAIN "MENU"
      ENDIF


      CALL  CLOSE_FILES

. ...Normal End-Of-Job
      CALL  NORMAL_EOJ
	 
. ...Return Control Back to Chain - End of Program	 
      TRAPREST  TRAP_DATA
      MOVE  SCH_RTSUCC  TO  SCH@RTCODE
      RETURN                                   
                         
+++++++++++++++++++<END OF MAIN PROCESS>+++++++++++++++++++++++++++++++++++++

. ----------------------------
. Initialize
. ----------------------------
INITIALIZE
      CLOCK     TIMESTAMP   INTO  TIMESTAMP
      CLOCK     TIMESTAMP   INTO  CURRDATE
      CLOCK     TIMESTAMP     TO  TODAY

. ...Display Counters
      MOVE      "Records Read:    " TO X_CNTDESC1
      MOVE      "Records Found:   " TO X_CNTDESC2
      MOVE      "                 " TO X_CNTDESC3
      MOVE      "                 " TO X_CNTDESC4
      MOVE      "                 " TO X_CNTDESC5
      MOVE      "                 " TO X_CNTDESC6
	 
      CALL      X_DISPLAY_COUNTS

      RETURN


.-------------------------------
. Reformat MEME file for Members
. currently active         
.-------------------------------

REFORMAT_MEME
    MESSAGE HORZ="22",VERT="20":
    BGCOL=C_DLGB,FGCOL=C_DLGF:
    LNCOL=C_DLGL,SHADE="y": 
    TEXT="REFORMATTING FILE ... PLEASE WAIT";
    
.--------------------------------------------
...Get only Records with Blank PEC dates 
.--------------------------------------------

    MOVE BLANKS TO SCRATCH
    PACK SCRATCH WITH "meme.dbf /w/a1102142.ref":
                      " -p424-437ge#"",20110701,"#" -t -j"
    REFORMAT SCRATCH

    IF OVER
         ERROR  HORZ="15",VERT="19":
                BGCOL=C_ERRB,FGCOL=C_ERRF:
                LNCOL=C_ERRL,SHADE="Y":
                TEXT="REFORMAT FAILED(meme.dbf)"
         CHAIN  "MENU"
    ENDIF

.-------------------------------------------------
...Get only ACTIVE Records without term dates
.-------------------------------------------------
    MOVE BLANKS TO SCRATCH
    PACK SCRATCH WITH "/w/a1102142.ref /w/a1102142.act":
                      " -p104-111eq#"",BLANK8,"#" 1-12 -t -j"
    REFORMAT SCRATCH
    IF OVER
        ERROR HORZ="15",VERT="19":
              BGCOL=C_ERRB,FGCOL=C_ERRF:
              LNCOL=C_ERRL,SHADE="Y":
              TEXT="REFORMAT FAILED(a1102142.ref)"
        CHAIN "MENU"
    ENDIF

.----------------------------------------------------
...Get only ACTIVE Records with future term dates
.----------------------------------------------------
    MOVE BLANKS TO SCRATCH
    PACK SCRATCH WITH "/w/a1102142.ref /w/a1102142.act":
                      " -p104-111gt#"",20110701,"#" 1-12 -t -j -a"
    REFORMAT SCRATCH
    IF OVER
        ERROR HORZ="15",VERT="19":
              BGCOL=C_ERRB,FGCOL=C_ERRF:
              LNCOL=C_ERRL,SHADE="Y":
              TEXT="REFORMAT FAILED(a1102142.act append)"
        CHAIN "MENU"
    ENDIF

        MESSAGE HORZ="22",VERT="20":
                BGCOL=C_DLGB,FGCOL=C_DLGF:
                LNCOL=C_DLGL,SHADE="y": 
                TEXT="******* REFORMAT COMPLETE *******";
 
      RETURN   

. ----------------------------
. Open Files
. ----------------------------
OPEN_FILES

      MOVE   ACTIVE_NAME TO C_FILE
      CALL   OPEN_OGBFILE USING ACTIVE_FILE,ACTIVE_NAME

      MOVE   ACTOUT_NAME TO C_FILE
      CALL   PREP_OGBFILE USING ACTOUT_FILE,ACTOUT_NAME

    RETURN


. ----------------------------
... Extract Active Members   
. ----------------------------
EXT_ACTIVE     

   LOOP
     MOVE OGB_SEQ1 TO OGB_SEQ
     CALL READ_OGBFILE USING ACTIVE_FILE,ACTIVE_NAME,ACTIVE_LIST
     IF (RESULT <> 1)
         BREAK   
     ENDIF

         ADD 1 TO X_CNT1
         UNPACK X_CNT1 INTO DISP4,DISP3
         IF (DISP3 = "000")
            CALL X_DISPLAY_COUNTS
         ENDIF

...Checking for age of dependents under 19
     CLEAR  INM_REC

     PACK    MEM_FK11 WITH ACTIVE_ID1 
     CALL    FILE_READ USING MEM_FD:    ...FILE DECLARATION LIST
                            MEM_FN:          ...FILE NAME LIST
                            MEM_FC:          ...(I)NDEX OR (A)IMDEX
                            MEM_FV:          ...FILE KEY VARLISTS
                            MEM_FK:          ...FILE KEY SIZES
                            MEM_LIST:        ...FILE RECORD (FIELD LIST)
                            MEM_REC:         ...FILE RECORD (PACKED)
                            MEM_OLD:         ...FILE SAVED WHEN READ
                            MEM_QTY:         ...NUMBER OF FILES
                            1                 ...FILE NUMBER FOR THIS READ

        IF (MEM_ID1 <> ACTIVE_ID1)
           CONTINUE 
        ENDIF

        CALL CALC_AGE USING MEM_DOB,WS_AGE,EXT_DATE  
        IF (WS_AGE >= " 19") 
           CONTINUE
        ENDIF

...Read MEME for dependents with health coverage

        PACK MEME_FK11 WITH ACTIVE_ID1,ACTIVE_ID2
        CALL FILE_READ USING MEME_FD:             ...FILE DECLARATION LIST
                             MEME_FN:             ...FILE NAME LIST
                             MEME_FC:             ...TYPE OF INDEX
.                                                 ...(I)NDEX or (A)IMDEX
                             MEME_FV:             ...FILE KEY VARLIST
                             MEME_FK:             ...FILE KEY SIZES
                             MEME_LIST:           ...FILE RECORD (FIELD LIST)
                             MEME_REC:            ...FILE RECORD (PACKED)
                             MEME_OLD:            ...FILE SAVED WHEN READ
                             MEME_QTY:            ...NUMBER OF FILES
                             1                    ...FILE NUMBER FOR THIS READ

            IF (RESULT <> 1)
               CONTINUE 
            ENDIF

            IF ((SQUEEZE MEME_TRM <> "") AND (SQUEEZE MEME_EFF > MEME_TRM))
                CONTINUE
            ENDIF

               PACKLEN INM_REC WITH MEM_REC,MEME_REC
               UNPACK INM_REC INTO INM_LIST
               MOVE "20110630" TO INME_PREX
               CLEAR INM_CHGBY
               CLEAR INM_CHGDT
               MOVE OGB_SEQ1 TO OGB_SEQ
               CALL WRITE_OGBREC USING ACTOUT_FILE,ACTOUT_NAME,INM_LIST    

               ADD 1 TO X_CNT2
               CLEAR INM_LIST

   REPEAT
   RETURN


.............................................................................

. ----------------------------
. Operator pressed F9
. ----------------------------


QUIT
         IF (C_RMODE = "I")
            error     horz="23",vert="19":
                      bgcol=C_ERRB,fgcol=C_ERRF:
                      lncol=C_ERRL,shade="Y":
                      text=" Process Aborted by Operator ":
                      text="-----------------------------"
            messageerase
         ENDIF

         CLEAR     C_NOHIST

         TRAPREST  TRAP_DATA

         IF (RUN_MODE = "B")
            LOOP
               RETCOUNT  CURR_RETURN
               WHILE     (CURR_RETURN > ORIG_RETURN)
               NORETURN
            REPEAT
            MOVE      SCH_RTABRT  TO  SCH@RTCODE
            RETURN
         ENDIF
         MOVE  "MENU"      TO  C_NEXT
         CHAIN     C_NEXT


. ----------------------------
. Close the file.
. ----------------------------
CLOSE_FILES
        MOVE  ACTIVE_NAME TO C_FILE
        CALL  CLOSE_OGBFILE USING ACTIVE_FILE,ACTIVE_NAME

        MOVE  ACTOUT_NAME TO C_FILE
        CALL  CLOSE_OGBFILE USING ACTOUT_FILE,ACTOUT_NAME

    RETURN


. ----------------------------
. Normal End-Of-Job Routine
. ----------------------------

NORMAL_EOJ

       	 MOVE      "        NEOJ        " TO X_FINISH_STATUS
	        MOVE      " A1102142 Completed   " TO X_PROG_FINISH
         CALL      X_END_OF_JOB
         
    RETURN
.----------------------------------------------------------------------------
.Subroutines and other includes
.----------------------------------------------------------------------------
              INC       VALID.IO
              INC       FILE.IO
              INC       X_OGBFILE.IO
              INC       X_STANDARD.SR
              INC       UTIL.SR 

.----------------------------------------------------------------------------
.End of A1102142.ps   
.----------------------------------------------------------------------------
