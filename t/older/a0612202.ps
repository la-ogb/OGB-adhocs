+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
+    Program name   :  A0612202.PS 
+    Description    :  CUS - FIXES CLIENT ID IN MEME                       
+    Author         :  Stephen J. McDonald
+    APSJM A0612202 12/27/2006  INITIAL VERSION                                 
+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

. ----------------------------
. Required Program Includes
. ----------------------------

              INC       COMMON.CB            ...Common work area
              INC       X_COMMON.CB          ...OGB Common Work Area
              INC       VALID.RL             ...Valid codes
              INC       X_OGBFILE.CB         ...OGB Custom File Handler
              INC       PRT.RL               ...Printer Definition File
              
. ----------------------------
. Program Specific Includes
. ----------------------------
              INC       MEM.RL           ...Member Demographics
              INC       MEME.RL          ...Member Enrollment  
              INC       MEMO.RL          ...Member Other Cov     
              INC       MEMS.RL          ...Member Salary        
              INC       CLI.RL           ...Client Demographics
              INC       D_MEMMEM.CB      ...IMPACT UPDATE

. ----------------------------
. File Declarations
. ----------------------------
.
MEME_WRK_FD     FILE     VAR=12,TEXT
MEME_WRK_NME    INIT    "/w/a0612202.meme.wrk"    
MEME_WRK_LIST   LIST
MEME_WRK_ID1    DIM     8
MEME_WRK_ID2    DIM     4
                LISTEND 
.
U_CLI_FD      FILE     VAR=2300,TEXT
U_CLI_NAME    INIT    "a0612202.txt"      
.
. ----------------------------
. Program Specific Variables & Working Storage
. ----------------------------
BLANK8        INIT "        "
CNT_4         DIM  4
CNT_3         DIM  3
FIRST_PAGE    DIM       1
FIRST_TIME    DIM       1
WORK_COUNT    DIM       8
FRONT         DIM       5
BACK          DIM       3
TIMESTAMP     DIM       14
CURRDATE      DIM       8
TRAP_DATA     DIM       500
CAL_CLOCK     DIM       21
ORIG_RETURN   FORM      5
CURR_RETURN   FORM      5
TODAY         DIM       8
. ...Report Fields

PAGE_NO       FORM      5
LINE_NO       FORM      3
LINE_MAX      FORM     "54"

R_PAGE_NO     DIM       5
R_DATE        DIM      10
R_TIME        DIM       8
YES           INIT     "Y"
NO            INIT     "N"

. ...Printing Controls

NORM_10       DIM      29
BOLD_10       DIM      29
NORM_12       DIM      26
BOLD_12       DIM      26
NORM_16       DIM      33
BOLD_16       DIM      33

ESC_CHAR      EQU      27
XCAPE         INIT     ESC_CHAR

PARAM_LIST    LIST
PARAM_XXX     DIM      10
PARAM_YYY     DIM       5
PARAM_DATE    DIM       8
              LISTEND

. ----------------------------
. Begin Program
. ----------------------------

. ----------------------------
. Start here if selected from menu   
. ----------------------------


         CALL      RUN_NOW

         CHAIN     C_NEXT

. ----------------------------
. Start here if called by the Scheduler    
. ----------------------------

         INC       SCHED_NP.SR              Scheduler routines (w/o printer)

RUN_NOW
. ...Check return stack
    RETCOUNT  ORIG_RETURN
. ----------------------------
. Fill top of screen
. ----------------------------

. ...Screen Display
         DISPLAY   *ES
         MOVE      "MENU"              TO  C_NEXT
         MOVE      "FIX CLI ID" TO  C_OPER
         MOVE      "A0612202"           TO  C_PROG
         MOVE      BLANKS              TO  C_LABEL
         MOVE      BLANKS              TO  C_FILE
         MOVE      BLANKS              TO  C_KEYS
         CLOCK     CALENDAR            INTO  CAL_CLOCK
         
         CALL X_BLANK_SCREEN

. ----------------------------
. Set Applicable Traps
. ----------------------------
         
. ...Set Applicable Traps
         TRAPSAVE  TRAP_DATA
         TRAPCLR   ALL
         TRAP      QUIT   NORESET  IF F9
	
. ----------------------------
. Get scheduler information
. ----------------------------

         CALL     SCHED_PICK

         IF (RUN_MODE = "B" OR RUN_MODE = "N")
            CALL      MAIN_PROCESS
         ENDIF
         RETURN

. ----------------------------
. Main Process
. ----------------------------

MAIN_PROCESS
    DEBUG
    CALL INITIALIZE
    CALL REFORMAT_FILES
    CALL OPEN_FILES
    CALL PROCESS_WRK_FILE
....Close Files
    CALL      CLOSE_FILES 
    CALL      X_DISPLAY_COUNTS
    TRAPREST  TRAP_DATA
    MOVE      SCH_RTSUCC  TO  SCH@RTCODE
    RETURN                                                   
. ----------------------------
. End of Main Process
. ----------------------------

PROCESS_WRK_FILE
 LOOP      
    MOVE OGB_SEQ1 TO OGB_SEQ
    CALL READ_OGBFILE USING MEME_WRK_FD,MEME_WRK_NME,MEME_WRK_LIST
      IF (RESULT <> 1)
         BREAK 
      ENDIF
    ADD 1 TO X_CNT1
    UNPACK X_CNT1 INTO CNT_4,CNT_3
    IF (CNT_3 = "000")
       CALL X_DISPLAY_COUNTS
    ENDIF
    PACK MEM_FK11 WITH MEME_WRK_ID1,BLANKS
    CALL FILE_READ USING MEM_FD:             ...FILE DECLARATION LIST
                         MEM_FN:             ...FILE NAME LIST
                         MEM_FC:             ...TYPE OF INDEX
.                                             ...(I)NDEX or (A)IMDEX
                         MEM_FV:             ...FILE KEY VARLIST
                         MEM_FK:             ...FILE KEY SIZES
                         MEM_LIST:           ...FILE RECORD (FIELD LIST)
                         MEM_REC:            ...FILE RECORD (PACKED)
                         MEM_OLD:            ...FILE SAVED WHEN READ
                         MEM_QTY:            ...NUMBER OF FILES
                         1                    ...FILE NUMBER FOR THIS READ
    IF (RESULT <> 1)
       CONTINUE
    ENDIF
    PACK MEME_FK11 WITH MEME_WRK_ID1,MEME_WRK_ID2,BLANKS
    CALL FILE_READ USING MEME_FD:             ...FILE DECLARATION LIST
                         MEME_FN:             ...FILE NAME LIST
                         MEME_FC:             ...TYPE OF INDEX
.                                             ...(I)NDEX or (A)IMDEX
                         MEME_FV:             ...FILE KEY VARLIST
                         MEME_FK:             ...FILE KEY SIZES
                         MEME_LIST:           ...FILE RECORD (FIELD LIST)
                         MEME_REC:            ...FILE RECORD (PACKED)
                         MEME_OLD:            ...FILE SAVED WHEN READ
                         MEME_QTY:            ...NUMBER OF FILES
                         1                    ...FILE NUMBER FOR THIS READ
    IF (RESULT <> 1)
       CONTINUE
    ENDIF
    CALL GET_CLI_ID
    IF (RESULT <> 1)
       CONTINUE
    ENDIF
    IF (MEME_CLIR = CLI_ID1)
       CONTINUE
    ENDIF
    CLEAR MEMO_REC
    CLEAR MEMS_REC
    PACKLEN INM_REC WITH MEM_REC,MEME_REC,MEMO_REC,MEMS_REC
    UNPACK INM_REC INTO INM_LIST
    MOVE CLI_ID1 TO INME_CLIR
    CLEAR INME_ID1
    CLEAR INME_ID2
    CLEAR INME_CHGBY
    CLEAR INME_NEWBY
    MOVE OGB_SEQ1 TO OGB_SEQ
    CALL WRITE_OGBREC USING U_CLI_FD,U_CLI_NAME,INM_LIST
    CLEAR INM_LIST
 REPEAT
 RETURN

GET_CLI_ID
   CLEAR CLI_AIM
   PACK CLI_AIM1 WITH "01X",MEME_LEV1
   PACK CLI_AIM2 WITH "02X",MEME_LEV2
   CALL     FILE_READ USING CLI_FD:      ...File Declaration List
                             CLI_FN:      ...File Name List
                             CLI_FC:      ...(I)ndex Or (A)imdex
                             CLI_FV:      ...File Key Varlists
                             CLI_AIM:     ...File Key Sizes
                             CLI_LIST:    ...File Record (Field List)
                             CLI_REC:     ...File Record (Packed)
                             CLI_OLD:     ...File Saved When Read
                             CLI_QTY:     ...Number Of Files
                             2            ...File Number For This Read

   RETURN 

REFORMAT_FILES 
   DISPLAY   *P1:21,*EL,*BOLDON, *BGCOLOR=*BLACK, *FGCOLOR=*WHITE:
             *P30:21, "REFORMATTING FILES", *BOLDOFF
   PACK     SCRATCH WITH BLANKS
   PACK     SCRATCH WITH "meme.dbf"," ",MEME_WRK_NME:
            " -p96-103le",TODAY:                  
            " -p104-111eq#"",BLANK8,"#" 1-12 -t -j"
   REFORMAT SCRATCH
    IF OVER
       ERROR     HORZ="22",VERT="20":
                 BGCOL=C_ERRB,FGCOL=C_ERRF:
                 LNCOL=C_ERRL,SHADE="Y":
                 TEXT="REFORMAT FAILED(MEME.DBF)"
      CHAIN     "MENU"
    ENDIF
   PACK     SCRATCH WITH BLANKS
   PACK     SCRATCH WITH "meme.dbf"," ",MEME_WRK_NME:
            " -p96-103le",TODAY:                  
            " -p104-111ge#"",TODAY,"#" 1-12 -a -t -j"
   REFORMAT SCRATCH
    IF OVER
       ERROR     HORZ="22",VERT="20":
                 BGCOL=C_ERRB,FGCOL=C_ERRF:
                 LNCOL=C_ERRL,SHADE="Y":
                 TEXT="REFORMAT FAILED(MEME.DBF)"
      CHAIN     "MENU"
    ENDIF
   PACK     SCRATCH WITH BLANKS
   PACK     SCRATCH WITH "meme.dbf"," ",MEME_WRK_NME:
            " -p96-103ge",TODAY:                  
            " -p104-111eq#"",BLANK8,"#" 1-12 -a -t -j"
   REFORMAT SCRATCH
    IF OVER
       ERROR     HORZ="22",VERT="20":
                 BGCOL=C_ERRB,FGCOL=C_ERRF:
                 LNCOL=C_ERRL,SHADE="Y":
                 TEXT="REFORMAT FAILED(MEME.DBF)"
      CHAIN     "MENU"
    ENDIF
   PACK     SCRATCH WITH BLANKS
   PACK     SCRATCH WITH "meme.dbf"," ",MEME_WRK_NME:
            " -p96-103ge",TODAY:                  
            " -p104-111ge#"",TODAY,"#" 1-12 -a -t -j"
   REFORMAT SCRATCH
    IF OVER
       ERROR     HORZ="22",VERT="20":
                 BGCOL=C_ERRB,FGCOL=C_ERRF:
                 LNCOL=C_ERRL,SHADE="Y":
                 TEXT="REFORMAT FAILED(MEME.DBF)"
      CHAIN     "MENU"
    ENDIF
   PACK     SCRATCH WITH BLANKS
   PACK      SCRATCH WITH MEME_WRK_NME," ",MEME_WRK_NME," 1-12 -t -u"
   SORT      SCRATCH
     IF OVER
        ERROR     HORZ="22",VERT="20":
                  BGCOL=C_ERRB,FGCOL=C_ERRF:
                  LNCOL=C_ERRL,SHADE="Y":
                  TEXT="SORT FAILED(A0612202.MEME.WRK)"
        CHAIN     "MENU"
     ENDIF

   RETURN

. ----------------------------
. Operator pressed F9
. ----------------------------

QUIT
         IF (C_RMODE = "I")
            error     horz="23",vert="19":
                      bgcol=C_ERRB,fgcol=C_ERRF:
                      lncol=C_ERRL,shade="Y":
                      text=" Process Aborted by Operator ":
                      text="-----------------------------"
            messageerase
         ENDIF

         CLEAR     C_NOHIST

         TRAPREST  TRAP_DATA

         IF (RUN_MODE = "B")
            LOOP
               RETCOUNT  CURR_RETURN
               WHILE     (CURR_RETURN > ORIG_RETURN)
               NORETURN
            REPEAT
            MOVE      SCH_RTABRT  TO  SCH@RTCODE
            RETURN
         ENDIF
         CHAIN     "MENU"

INITIALIZE
         DISPLAY *P1:9,*ES
         CALL X_BLANK_SCREEN
         CLOCK     TIMESTAMP   INTO  TIMESTAMP
         CLOCK     TIMESTAMP   INTO  CURRDATE
         CLOCK     TIMESTAMP     TO  TODAY

. ...Display Counters
	 MOVE      "Recs Read:     " TO X_CNTDESC1
	 MOVE      "Recs Written:     " TO X_CNTDESC2

	 CALL      X_DISPLAY_COUNTS
         RETURN
.
OPEN_FILES
         CALL      PREP_OGBFILE USING U_CLI_FD,U_CLI_NAME  
         CALL      OPEN_OGBFILE USING MEME_WRK_FD,MEME_WRK_NME
         RETURN

CLOSE_FILES
         CALL      CLOSE_OGBFILE USING U_CLI_FD,U_CLI_NAME  
         CALL      CLOSE_OGBFILE USING MEME_WRK_FD,MEME_WRK_NME
         RETURN

    INC       PRINT.SR
    INC       UTIL.SR      
    INC       VALID.IO
    INC       X_STANDARD.SR
    INC       FILE.IO

. ----------------------------
. Program Specific Includes
. ----------------------------

              INC       X_OGBFILE.IO

. +++++++++++++++++++++++++++++++++++++
. /End of A0612202.PS
. +++++++++++++++++++++++++++++++++++++
