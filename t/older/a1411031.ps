+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
+    Program name   :  A1411031   
+    APSJM A141103 11/03/2014 - INITIAL VERSION
+    apazw 06/12/2017 - mod - increase file size
+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

. ----------------------------
. Required Program Includes
. ----------------------------

              INC       COMMON.CB            ...Common work area
              INC       X_COMMON.CB          ...OGB Common Work Area
              INC       VALID.RL             ...Valid codes
              INC       X_OGBFILE.CB         ...OGB Custom File Handler
              INC       PRT.RL               ...Printer Definition File
              
. ----------------------------
. Program Specific Includes
. ----------------------------
              INC       MEM.RL           ...Member Demographics
              INC       MEME.RL          ...Member Enrollment  
              INC       CLI.RL           ...Client 
              INC       D_MEMMEM.CB      ...IMPACT UPDATE
              INC       CMDPARAM.CB

. ----------------------------
. File Declarations
. ----------------------------
.Input Files
MEME_REF_FD         FILE       VAR=13,TEXT
MEME_REF_NAME       INIT       "/w/a1411031_ref.txt"    
MEME_REF_LIST       LIST
MEME_REF_ID1        DIM        8
MEME_REF_ID2        DIM        4
                    LISTEND
 
.Output Files
MEME_O_FD     FILE     VAR=X_INM_SIZE,TEXT
MEME_O_NAME   INIT    "a1411031.txt"      
.
. ----------------------------
. Program Specific Variables & Working Storage
. ----------------------------
W_TRM         DIM   8
BLUE          INIT  "BLUE "
BLANK8        INIT  "        "
MEME_SAV3     DIM   600
MEME_SAV4     DIM   600
MEME_SAV5     DIM   600
MEME_SAV6     DIM   600
MAGNO         INIT "MAGNO"
PELCN         INIT "PELCN"
TRM_12        INIT "20141231"
TRM_02        INIT "20150228"
EFF_01        INIT "20150101"
EFF_03        INIT "20150301"
CMP_TRM       DIM  9
CNT_4         DIM  4
CNT_3         DIM  3
FIRST_PAGE    DIM       1
FIRST_TIME    DIM       1
WORK_COUNT    DIM       8
FRONT         DIM       5
BACK          DIM       3
TIMESTAMP     DIM       14
CURRDATE      DIM       8
TRAP_DATA     DIM       500
CAL_CLOCK     DIM       21
ORIG_RETURN   FORM      5
CURR_RETURN   FORM      5
TODAY         DIM       8
. ...Report Fields

PAGE_NO       FORM      5
LINE_NO       FORM      3
LINE_MAX      FORM     "54"

R_PAGE_NO     DIM       5
R_DATE        DIM      10
R_TIME        DIM       8
YES           INIT     "Y"
NO            INIT     "N"

. ...Printing Controls

NORM_10       DIM      29
BOLD_10       DIM      29
NORM_12       DIM      26
BOLD_12       DIM      26
NORM_16       DIM      33
BOLD_16       DIM      33

ESC_CHAR      EQU      27
XCAPE         INIT     ESC_CHAR

PARAM_LIST    LIST
PARAM_XXX     DIM      10
PARAM_YYY     DIM       5
PARAM_DATE    DIM       8
              LISTEND

. ----------------------------
. Begin Program
. ----------------------------

. ----------------------------
. Start here if selected from menu   
. ----------------------------


         CALL      RUN_NOW

         CHAIN     C_NEXT

. ----------------------------
. Start here if called by the Scheduler    
. ----------------------------

         INC       SCHED_NP.SR              Scheduler routines (w/o printer)

RUN_NOW
. ...Check return stack
    RETCOUNT  ORIG_RETURN
. ----------------------------
. Fill top of screen
. ----------------------------

. ...Screen Display
         DISPLAY   *ES
         MOVE      "MENU"              TO  C_NEXT
         MOVE      "FIX AE 2014 Flex" TO  C_OPER
         MOVE      "A1411031"           TO  C_PROG
         MOVE      BLANKS              TO  C_LABEL
         MOVE      BLANKS              TO  C_FILE
         MOVE      BLANKS              TO  C_KEYS
         CLOCK     CALENDAR            INTO  CAL_CLOCK
         
         CALL X_BLANK_SCREEN

. ----------------------------
. Set Applicable Traps
. ----------------------------
         
. ...Set Applicable Traps
         TRAPSAVE  TRAP_DATA
         TRAPCLR   ALL
         TRAP      QUIT   NORESET  IF F9
	
. ----------------------------
. Get scheduler information
. ----------------------------

         CALL     SCHED_PICK

         IF (RUN_MODE = "B" OR RUN_MODE = "N")
            CALL      MAIN_PROCESS
         ENDIF
         RETURN

. ----------------------------
. Main Process
. ----------------------------

MAIN_PROCESS
    DEBUG
    CALL INITIALIZE
    CALL REFORMAT_MEME
    CALL OPEN_FILES
    CALL PROCESS_FILE
....Close Files
    CALL      CLOSE_FILES 
    CALL      X_DISPLAY_COUNTS
    TRAPREST  TRAP_DATA
    MOVE      SCH_RTSUCC  TO  SCH@RTCODE
    RETURN                                                   
. ----------------------------
. End of Main Process
. ----------------------------

PROCESS_FILE
 LOOP
    MOVE OGB_SEQ1 TO OGB_SEQ
    CALL READ_OGBFILE USING MEME_REF_FD,MEME_REF_NAME,MEME_REF_LIST        
      IF (RESULT <> 1)
         BREAK 
      ENDIF
    ADD 1 TO X_CNT1
    UNPACK X_CNT1 INTO CNT_4,CNT_3
    IF (CNT_3 = "000")
       CALL X_DISPLAY_COUNTS
    ENDIF
    PACK MEM_FK11 WITH MEME_REF_ID1,BLANKS
    CALL  FILE_READ USING MEM_FD:  ...FILE DECLARATION LIST
        	           MEM_FN:  ...FILE NAME LIST  
		           MEM_FC:  ...TYPE OF INDEX   
.	  		             ...(I)NDEX OR (A)IMDEX
		           MEM_FV:  ...FILE KEY VARLISTS
		           MEM_FK:  ...FILE KEY SIZES  
		           MEM_LIST:...FILE RECORD (FIELD LIST)
		           MEM_REC: ...FILE RECORD (PACKED)
		           MEM_OLD: ...FILE SAVED WHEN READ
		           MEM_QTY: ...NUMBER OF FILES 
			   1	     ...FILE NUMBER FOR THIS READ

    IF (RESULT <> 1)
       CONTINUE
    ENDIF
    IF (MEM_REL <> "01")
        CONTINUE
    ENDIF
    CALL READ_MEME_HLTH
    IF (MEME_SAV4 <> "")
       CONTINUE
    ENDIF
    IF (SQUEEZE MEME_SAV <> "")
      IF ((SQUEEZE MEME_SAV3 = "") AND (MEME_SAV2 = ""):
            AND (SQUEEZE MEME_SAV6 = ""))
         UNPACK MEME_SAV INTO MEME_REC
         UNPACK MEME_SAV INTO MEME_LIST
         IF ((SQUEEZE MEME_LEV2 = "95") :
                  OR (SQUEEZE MEME_LEV2 = "93") :
                  OR (SQUEEZE MEME_LEV2 = "99") :
                  OR (SQUEEZE MEME_LEV2 = "94"))
                  CONTINUE          T
         ENDIF

         IF ((SQUEEZE MEME_LEV2 = "92") AND (MEME_RETPY <> "Y"))
                  CONTINUE                  
         ENDIF
         CLEAR INM_REC
         PACKLEN INM_REC WITH MEM_REC,MEME_REC
         UNPACK INM_REC INTO INM_LIST
         MOVE "20141231" TO INME_TRM
         CLEAR INME_CHGBY
         CLEAR INM_CHGBY
         MOVE OGB_SEQ1 TO OGB_SEQ
         CALL WRITE_OGBREC USING MEME_O_FD,MEME_O_NAME,INM_LIST
         CLEAR INM_LIST 
         UNPACK MEME_SAV INTO MEME_REC
         CLEAR INM_REC
         PACKLEN INM_REC WITH MEM_REC,MEME_REC
         UNPACK INM_REC INTO INM_LIST
         MOVE "20150101" TO INME_EFF
         MOVE BLANKS TO INME_TRM
         MOVE "A" TO INME_ERSA
         CLEAR INME_CHGBY
         CLEAR INM_CHGBY
         CLEAR INME_ID1
         CLEAR INME_ID2
         CLEAR INM_NEWBY
         CLEAR INME_NEWBY
         MOVE OGB_SEQ1 TO OGB_SEQ
         CALL WRITE_OGBREC USING MEME_O_FD,MEME_O_NAME,INM_LIST
         CLEAR INM_LIST   
         CONTINUE
      ENDIF
    ENDIF
    IF ((SQUEEZE MEME_SAV = "") AND (SQUEEZE MEME_SAV5 <> ""))
      IF ((SQUEEZE MEME_SAV3 = "") AND (MEME_SAV2 <> ""))
         DEBUG
..... GAP in coverage for new plan year
         UNPACK MEME_SAV5 INTO MEME_REC
         UNPACK MEME_SAV5 INTO MEME_LIST
         IF ((SQUEEZE MEME_LEV2 = "95") :
                  OR (SQUEEZE MEME_LEV2 = "93") :
                  OR (SQUEEZE MEME_LEV2 = "99") :
                  OR (SQUEEZE MEME_LEV2 = "94"))
                  CONTINUE          
         ENDIF

         IF ((SQUEEZE MEME_LEV2 = "92") AND (MEME_RETPY <> "Y"))
                  CONTINUE                  
         ENDIF
         CLEAR INM_REC
         PACKLEN INM_REC WITH MEM_REC,MEME_REC
         UNPACK INM_REC INTO INM_LIST
         MOVE "20150101" TO INME_EFF
         MOVE "20150228" TO INME_TRM
         MOVE "A" TO INME_ERSA
         CLEAR INME_CHGBY
         CLEAR INM_CHGBY
         CLEAR INME_ID1
         CLEAR INME_ID2
         CLEAR INM_NEWBY
         CLEAR INME_NEWBY
         MOVE OGB_SEQ1 TO OGB_SEQ
         CALL WRITE_OGBREC USING MEME_O_FD,MEME_O_NAME,INM_LIST
         CLEAR INM_LIST 
      ENDIF
    ENDIF
    IF (MEME_SAV <> "")
      IF ((SQUEEZE MEME_SAV2 <> "") AND (SQUEEZE MEME_SAV6 <> "") AND:
            (SQUEEZE MEME_SAV3 = ""))
         DEBUG
         UNPACK MEME_SAV INTO MEME_REC   
         UNPACK MEME_SAV INTO MEME_LIST
         IF ((SQUEEZE MEME_LEV2 = "95") :
                  OR (SQUEEZE MEME_LEV2 = "93") :
                  OR (SQUEEZE MEME_LEV2 = "99") :
                  OR (SQUEEZE MEME_LEV2 = "94"))
                  CONTINUE
         ENDIF

         IF ((SQUEEZE MEME_LEV2 = "92") AND (MEME_RETPY <> "Y"))
                  CONTINUE
         ENDIF
         CLEAR INM_REC
         PACKLEN INM_REC WITH MEM_REC,MEME_REC
         UNPACK INM_REC INTO INM_LIST
         MOVE "20141231" TO INME_TRM
         CLEAR INME_CHGBY
         CLEAR INM_CHGBY
         MOVE OGB_SEQ1 TO OGB_SEQ
         CALL WRITE_OGBREC USING MEME_O_FD,MEME_O_NAME,INM_LIST
         CLEAR INM_LIST
         UNPACK MEME_SAV6 INTO MEME_REC   
         UNPACK MEME_SAV6 INTO MEME_LIST
         CLEAR INM_REC
         PACKLEN INM_REC WITH MEM_REC,MEME_REC
         UNPACK INM_REC INTO INM_LIST
         MOVE "20150101" TO INME_EFF
         MOVE "20150228" TO INME_TRM
         MOVE "A" TO INME_ERSA
         CLEAR INME_CHGBY
         CLEAR INM_CHGBY
         CLEAR INME_ID1
         CLEAR INME_ID2
         CLEAR INM_NEWBY
         CLEAR INM_CHGDT
         CLEAR INME_CHGDT
         CLEAR INM_NEWDT
         CLEAR INME_NEWDT
         CLEAR INME_NEWBY
         MOVE OGB_SEQ1 TO OGB_SEQ
         CALL WRITE_OGBREC USING MEME_O_FD,MEME_O_NAME,INM_LIST
         CLEAR INM_LIST 
      ENDIF
    ENDIF
 REPEAT
 RETURN
.
READ_MEME_HLTH   
    CLEAR W_TRM
    CLEAR MEME_SAV
    CLEAR MEME_SAV2
    CLEAR MEME_SAV3
    CLEAR MEME_SAV4
    CLEAR MEME_SAV5
    CLEAR MEME_SAV6
    PACK MEME_FK11 WITH MEM_ID1,BLANKS
    CALL  FILE_READ USING MEME_FD:  ...FILE DECLARATION LIST
        	           MEME_FN:  ...FILE NAME LIST  
		           MEME_FC:  ...TYPE OF INDEX   
.	  		             ...(I)NDEX OR (A)IMDEX
		           MEME_FV:  ...FILE KEY VARLISTS
		           MEME_FK:  ...FILE KEY SIZES  
		           MEME_LIST:...FILE RECORD (FIELD LIST)
		           MEME_REC: ...FILE RECORD (PACKED)
		           MEME_OLD: ...FILE SAVED WHEN READ
		           MEME_QTY: ...NUMBER OF FILES 
			   1	     ...FILE NUMBER FOR THIS READ
    LOOP 
          CALL  FILE_NEXT USING MEME_FD:  ...FILE DECLARATION LIST
                                   MEME_FN:  ...FILE NAME LIST
                                   MEME_FC:  ...TYPE OF INDEX
.                                            ...(I)NDEX OR (A)IMDEX
                                   MEME_FV:  ...FILE KEY VARLISTS
                                   MEME_FK:  ...FILE KEY SIZES
                                   MEME_LIST:...FILE RECORD (FIELD LIST)
                                   MEME_REC: ...FILE RECORD (PACKED)
                                   MEME_OLD: ...FILE SAVED WHEN READ
                                   MEME_QTY: ...NUMBER OF FILES
                                   1         ...FILE NUMBER FOR THIS READ
            IF ((MEME_ID1 <> MEM_ID1) OR (RESULT <> 1))
                  BREAK
            ENDIF
            IF (SQUEEZE MEME_NET = "NOCOV")
              CONTINUE
            ENDIF
            IF (SQUEEZE MEME_PRD = "HSA")
               CONTINUE
            ENDIF
            IF (SQUEEZE MEME_PRD = "HSA1T")
               CONTINUE
            ENDIF
            IF (SQUEEZE MEME_NET = "LIFE")
               CONTINUE
            ENDIF
            IF (SQUEEZE MEME_PRD = "HRFEE")
               CONTINUE
            ENDIF
            IF (SQUEEZE MEME_NET = "EGWP"))
               CONTINUE
            ENDIF
            IF (SQUEEZE MEME_TRM <> "")
               IF (MEME_TRM < MEME_EFF)
                 CONTINUE
               ENDIF
            ENDIF         
            PACK CLI_AIM1 WITH "01X",MEME_LEV1
            PACK CLI_AIM2 WITH "02X",MEME_LEV2
            CALL     FILE_READ USING CLI_FD:      ...File Declaration List
                             CLI_FN:      ...File Name List
                             CLI_FC:      ...(I)ndex Or (A)imdex
                             CLI_FV:      ...File Key Varlists
                             CLI_AIM:     ...File Key Sizes
                             CLI_LIST:    ...File Record (Field List)
                             CLI_REC:     ...File Record (Packed)
                             CLI_OLD:     ...File Saved When Read
                             CLI_QTY:     ...Number Of Files
                             2            ...File Number For This Read
            IF (CLI_US02 <> "U")
                  CONTINUE
            ENDIF
            IF (SQUEEZE MEME_TRM = "")
               FILL "9" IN W_TRM
            ELSE
               MOVE MEME_TRM TO W_TRM
            ENDIF
            IF ((MEME_EFF < EFF_01) AND (W_TRM > EFF_01))
             PACKLEN MEME_SAV WITH MEME_LIST
            ENDIF
            IF (MEME_EFF > "20150101")
                   IF ((MEME_EFF = EFF_03) AND ((MEME_PRD = "MAGNO") OR:
                                             (MEME_PRD = "PELCN")))
                            PACKLEN MEME_SAV2 WITH MEME_LIST
                   ELSE
                            PACKLEN MEME_SAV3 WITH MEME_LIST
                   ENDIF
            ENDIF
            IF (MEME_EFF = "20150101")
              PACKLEN MEME_SAV4 WITH MEME_LIST
            ENDIF
            IF ((SQUEEZE MEME_TRM <> "") AND (MEME_TRM = "20141231"))
               PACKLEN MEME_SAV5 WITH MEME_LIST
            ENDIF
            IF (MEME_TRM = TRM_02)
                PACKLEN MEME_SAV6 WITH MEME_LIST
            ENDIF
    REPEAT
    RETURN
.

.
. ----------------------------
. Operator pressed F9
. ----------------------------
REFORMAT_MEME
.--------------------------------------------
...Display message to Operator to Please Wait
.--------------------------------------------
    MESSAGE HORZ="22",VERT="20":
            BGCOL=C_DLGB,FGCOL=C_DLGF:
            LNCOL=C_DLGL,SHADE="y": 
            TEXT="REFORMATTING FILE ... PLEASE WAIT";
    
.----------------------------------
...Get only BLUE CROSS records                
.----------------------------------
    MOVE BLANKS TO SCRATCH
    PACK SCRATCH WITH "meme.dbf /w/a1411031_ref1.txt":  
                      " -p112-116eq#"",BLUE,"#"":
                      " -t -j"
    REFORMAT SCRATCH
    IF OVER
      IF (NOT C_CMDPAR)
        ERROR HORZ="15",VERT="19":
              BGCOL=C_ERRB,FGCOL=C_ERRF:
              LNCOL=C_ERRL,SHADE="Y":
              TEXT="REFORMAT FAILED(x_meme.blue)"
        CHAIN "MENU"
      ELSE
        IF (C_CMDPAR)
            CALL CMD_ERROR USING "x_meme.blue"
            STOP
        ENDIF
      ENDIF
    ENDIF

.-------------------------------------------------------------------------
...Get only BLUE Records From x_meme.blue without term dates
.-------------------------------------------------------------------------
 MOVE BLANKS TO SCRATCH
 PACK SCRATCH WITH "/w/a1411031_ref1.txt /w/a1411031_ref.txt 1-12 ":
                      " -p104-111eq#"",BLANK8,"#" -t -j"
    REFORMAT SCRATCH
    IF OVER
      IF (NOT C_CMDPAR)
         ERROR HORZ="15",VERT="19":
               BGCOL=C_ERRB,FGCOL=C_ERRF:
               LNCOL=C_ERRL,SHADE="Y":
               TEXT="REFORMAT FAILED(x_meme.actblue)"
         CHAIN "MENU"
      ELSE
        IF (C_CMDPAR)
           CALL CMD_ERROR USING "x_meme.actblue"
           STOP
        ENDIF
      ENDIF
    ENDIF
 
.-----------------------------------------------------------------------------
...Get only B C Records From x_meme.blue with term dates > 01/01/2015 
.-----------------------------------------------------------------------------
 MOVE BLANKS TO SCRATCH
 PACK SCRATCH WITH "/w/a1411031_ref1.txt /w/a1411031_ref.txt 1-12 ": 
                      " -p104-111gt#"",TRM_12,"#" -t -j -a"
    REFORMAT SCRATCH
    IF OVER
      IF (NOT C_CMDPAR)
        ERROR HORZ="15",VERT="19":
              BGCOL=C_ERRB,FGCOL=C_ERRF:
              LNCOL=C_ERRL,SHADE="Y":
              TEXT="REFORMAT FAILED"
        CHAIN "MENU"
      ELSE
        IF (C_CMDPAR)
           CALL CMD_ERROR USING "a1411031_ref append"
           STOP
        ENDIF
      ENDIF
    ENDIF
 
.-------------------------------------------------------
...Display message to operator that reformat is complete
.-------------------------------------------------------
    MESSAGE HORZ="22",VERT="20":
            BGCOL=C_DLGB,FGCOL=C_DLGF:
            LNCOL=C_DLGL,SHADE="y":
            TEXT="     !!!!! REFORMAT COMPLETE !!!!!     ";
    RETURN


    RETURN

QUIT
         IF (C_RMODE = "I")
            error     horz="23",vert="19":
                      bgcol=C_ERRB,fgcol=C_ERRF:
                      lncol=C_ERRL,shade="Y":
                      text=" Process Aborted by Operator ":
                      text="-----------------------------"
            messageerase
         ENDIF

         CLEAR     C_NOHIST

         TRAPREST  TRAP_DATA

         IF (RUN_MODE = "B")
            LOOP
               RETCOUNT  CURR_RETURN
               WHILE     (CURR_RETURN > ORIG_RETURN)
               NORETURN
            REPEAT
            MOVE      SCH_RTABRT  TO  SCH@RTCODE
            RETURN
         ENDIF
         CHAIN     "MENU"

INITIALIZE
         DISPLAY *P1:9,*ES
         CALL X_BLANK_SCREEN
         CLOCK     TIMESTAMP   INTO  TIMESTAMP
         CLOCK     TIMESTAMP   INTO  CURRDATE
         CLOCK     TIMESTAMP     TO  TODAY

. ...Display Counters
	 MOVE      "Recs Read:     " TO X_CNTDESC1
	 MOVE      "Recs Written:     " TO X_CNTDESC2

	 CALL      X_DISPLAY_COUNTS
         RETURN
.
OPEN_FILES
         CALL      OPEN_OGBFILE USING MEME_REF_FD,MEME_REF_NAME
         CALL      PREP_OGBFILE USING MEME_O_FD,MEME_O_NAME 
         RETURN

CLOSE_FILES
         RETURN

    INC       PRINT.SR
    INC       UTIL.SR      
    INC       VALID.IO
    INC       X_STANDARD.SR
    INC       FILE.IO

. ----------------------------
. Program Specific Includes
. ----------------------------

              INC       X_OGBFILE.IO
              INC       CMDPARAM.SR
. +++++++++++++++++++++++++++++++++++++
. /End of A1410241.PS
. +++++++++++++++++++++++++++++++++++++
