+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
+    Program name   :  A0908273.PS
+    Description    :  ADH - ADDS THE QUALIFYING REASON & DATE FOR VER  
+                            MEME CONVERSION TO ALL FUTUR EFF AND CURRENT
+                            MEME RECORDS.
+    APSJM A0908273 08/28/2009 - INITIAL VERSION                                
+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

. ----------------------------
. Required Program Includes
. ----------------------------

              INC       COMMON.CB            ...Common work area
              INC       X_COMMON.CB          ...OGB Common Work Area
              INC       VALID.RL             ...Valid codes
              INC       X_OGBFILE.CB         ...OGB Custom File Handler
              INC       PRT.RL               ...Printer Definition File
              
. ----------------------------
. Program Specific Includes
. ----------------------------
              INC       MEM.RL           ...Member Demographics
              INC       MEME.RL          ...Member Enrollment  
              INC       D_MEMMEM.CB      ...IMPACT UPDATE

. ----------------------------
. File Declarations
. ----------------------------
.Input Files
DEP_REF_FD         FILE       VAR=10,TEXT
DEP_REF_NAME       INIT       "/w/conv_ver_dep.txt"

DEP_REF_LIST       LIST
DEP_ID1            DIM         8
DEP_REL            DIM         2
                   LISTEND
.
.Output Files
U_DEPS_FD     FILE     VAR=3100,TEXT
U_DEPS_NAME    INIT    "a0908273.txt"      
.
. ----------------------------
. Program Specific Variables & Working Storage
. ----------------------------
WRITE_SW      DIM  1
W_TRM         DIM  8
DIM_2         DIM  2
DIM_3         DIM  3
Y             INIT "Y"
CMP_TRM       DIM  9
CNT_4         DIM  4
CNT_3         DIM  3
FIRST_PAGE    DIM       1
FIRST_TIME    DIM       1
WORK_COUNT    DIM       8
FRONT         DIM       5
BACK          DIM       3
TIMESTAMP     DIM       14
CURRDATE      DIM       8
TRAP_DATA     DIM       500
CAL_CLOCK     DIM       21
ORIG_RETURN   FORM      5
CURR_RETURN   FORM      5
TODAY         DIM       8
. ...Report Fields

PAGE_NO       FORM      5
LINE_NO       FORM      3
LINE_MAX      FORM     "54"

R_PAGE_NO     DIM       5
R_DATE        DIM      10
R_TIME        DIM       8
YES           INIT     "Y"
NO            INIT     "N"

. ...Printing Controls

NORM_10       DIM      29
BOLD_10       DIM      29
NORM_12       DIM      26
BOLD_12       DIM      26
NORM_16       DIM      33
BOLD_16       DIM      33

ESC_CHAR      EQU      27
XCAPE         INIT     ESC_CHAR

PARAM_LIST    LIST
PARAM_XXX     DIM      10
PARAM_YYY     DIM       5
PARAM_DATE    DIM       8
              LISTEND

. ----------------------------
. Begin Program
. ----------------------------

. ----------------------------
. Start here if selected from menu   
. ----------------------------


         CALL      RUN_NOW

         CHAIN     C_NEXT

. ----------------------------
. Start here if called by the Scheduler    
. ----------------------------

         INC       SCHED_NP.SR              Scheduler routines (w/o printer)

RUN_NOW
. ...Check return stack
    RETCOUNT  ORIG_RETURN
. ----------------------------
. Fill top of screen
. ----------------------------

. ...Screen Display
         DISPLAY   *ES
         MOVE      "MENU"              TO  C_NEXT
         MOVE      "CONVERT VER DEPS" TO  C_OPER
         MOVE      "A0908273"           TO  C_PROG
         MOVE      BLANKS              TO  C_LABEL
         MOVE      BLANKS              TO  C_FILE
         MOVE      BLANKS              TO  C_KEYS
         CLOCK     CALENDAR            INTO  CAL_CLOCK
         
         CALL X_BLANK_SCREEN

. ----------------------------
. Set Applicable Traps
. ----------------------------
         
. ...Set Applicable Traps
         TRAPSAVE  TRAP_DATA
         TRAPCLR   ALL
         TRAP      QUIT   NORESET  IF F9
	
. ----------------------------
. Get scheduler information
. ----------------------------

         CALL     SCHED_PICK

         IF (RUN_MODE = "B" OR RUN_MODE = "N")
            CALL      MAIN_PROCESS
         ENDIF
         RETURN

. ----------------------------
. Main Process
. ----------------------------

MAIN_PROCESS
    DEBUG
    CALL INITIALIZE
    CALL REFORMAT_MEM
    CALL OPEN_FILES
    CALL PROCESS_REF_FILE
....Close Files
    CALL      CLOSE_FILES 
    CALL      X_DISPLAY_COUNTS
    TRAPREST  TRAP_DATA
    MOVE      SCH_RTSUCC  TO  SCH@RTCODE
    RETURN                                                   
. ----------------------------
. End of Main Process
. ----------------------------

PROCESS_REF_FILE
 LOOP
    MOVE OGB_SEQ1 TO OGB_SEQ
    CALL READ_OGBFILE USING DEP_REF_FD,DEP_REF_NAME,DEP_REF_LIST        
      IF (RESULT <> 1)
         BREAK 
      ENDIF
    ADD 1 TO X_CNT1
    UNPACK X_CNT1 INTO CNT_4,CNT_3
    IF (CNT_3 = "000")
       CALL X_DISPLAY_COUNTS
    ENDIF

    PACK MEM_FK11 WITH DEP_ID1,BLANKS
    CALL  FILE_READ USING MEM_FD:  ...FILE DECLARATION LIST
        	           MEM_FN:  ...FILE NAME LIST  
		           MEM_FC:  ...TYPE OF INDEX   
.	  		             ...(I)NDEX OR (A)IMDEX
		           MEM_FV:  ...FILE KEY VARLISTS
		           MEM_FK:  ...FILE KEY SIZES  
		           MEM_LIST:...FILE RECORD (FIELD LIST)
		           MEM_REC: ...FILE RECORD (PACKED)
		           MEM_OLD: ...FILE SAVED WHEN READ
		           MEM_QTY: ...NUMBER OF FILES 
			   1	     ...FILE NUMBER FOR THIS READ

    IF (RESULT <> 1)
       CONTINUE
    ENDIF
    CLEAR MEME_SAV
    CLEAR WRITE_SW
    CLEAR W_TRM
    PACK MEME_FK11 WITH MEM_ID1,BLANKS
    CALL  FILE_READ USING MEME_FD:  ...FILE DECLARATION LIST
        	           MEME_FN:  ...FILE NAME LIST  
		           MEME_FC:  ...TYPE OF INDEX   
.	  		             ...(I)NDEX OR (A)IMDEX
		           MEME_FV:  ...FILE KEY VARLISTS
		           MEME_FK:  ...FILE KEY SIZES  
		           MEME_LIST:...FILE RECORD (FIELD LIST)
		           MEME_REC: ...FILE RECORD (PACKED)
		           MEME_OLD: ...FILE SAVED WHEN READ
		           MEME_QTY: ...NUMBER OF FILES 
			   1	     ...FILE NUMBER FOR THIS READ
    LOOP 
          CALL  FILE_NEXT USING MEME_FD:  ...FILE DECLARATION LIST
                                   MEME_FN:  ...FILE NAME LIST
                                   MEME_FC:  ...TYPE OF INDEX
.                                            ...(I)NDEX OR (A)IMDEX
                                   MEME_FV:  ...FILE KEY VARLISTS
                                   MEME_FK:  ...FILE KEY SIZES
                                   MEME_LIST:...FILE RECORD (FIELD LIST)
                                   MEME_REC: ...FILE RECORD (PACKED)
                                   MEME_OLD: ...FILE SAVED WHEN READ
                                   MEME_QTY: ...NUMBER OF FILES
                                   1         ...FILE NUMBER FOR THIS READ
            IF ((MEME_ID1 <> MEM_ID1) OR (RESULT <> 1))
                IF (SQUEEZE WRITE_SW = "")
                  IF (SQUEEZE MEME_SAV <> "")
                    UNPACK MEME_SAV INTO MEME_LIST
                    CALL WRITE_MEMMEM
                  ENDIF             
                ENDIF
                BREAK
            ENDIF
            IF (SQUEEZE MEME_TRM <> "")
               IF (MEME_TRM < MEME_EFF)
                    CONTINUE
               ENDIF
            ENDIF
            CLEAR CMP_TRM
            IF (SQUEEZE MEME_TRM = "")
               FILL "9" IN CMP_TRM
            ELSE
               MOVE MEME_TRM TO CMP_TRM
            ENDIF
            IF ((CMP_TRM <> "99999999") AND (CMP_TRM < TODAY))
              IF ((SQUEEZE W_TRM = "") OR (CMP_TRM > W_TRM))
                 MOVE CMP_TRM TO W_TRM  
                 PACKLEN MEME_SAV WITH MEME_LIST
              ENDIF
            ENDIF
            IF (DEP_REL <> "01")
              IF (MEME_EFF > TODAY)
                 CALL WRITE_MEMMEM
                 CONTINUE
              ENDIF
              IF ((TODAY >= MEME_EFF) AND (TODAY < CMP_TRM))
                 CALL WRITE_MEMMEM
                 MOVE "1" TO WRITE_SW
                 CONTINUE
              ENDIF 
            ELSE
              UNPACK MEME_LEV1 INTO DIM_3,DIM_2
              SWITCH DIM_2
                CASE "SV"
                DEFAULT
                    SWITCH MEME_LEV2
                      CASE "94   " OR "95   "
                      DEFAULT
                         CONTINUE
                    ENDSWITCH 
              ENDSWITCH
              IF (MEME_EFF > TODAY)
                CALL WRITE_MEMMEM
                CONTINUE
              ENDIF
              IF ((TODAY >= MEME_EFF) AND (TODAY < CMP_TRM))
                 CALL WRITE_MEMMEM
                 MOVE "1" TO WRITE_SW
                 CONTINUE
              ENDIF 
            ENDIF
    REPEAT
 REPEAT
 RETURN

WRITE_MEMMEM
    IF (SQUEEZE MEME_LCE = "20081231")
      IF (SQUEEZE MEME_LCT = "20081231")
        IF (SQUEEZE MEME_US02 = "CV")
           RETURN
        ENDIF
      ENDIF
    ENDIF
    CLEAR INM_REC
    PACKLEN MEME_REC WITH MEME_LIST
    PACKLEN INM_REC WITH MEM_REC,MEME_REC
    UNPACK INM_REC INTO INM_LIST
    MOVE "CV" TO INME_US02
    MOVE "20100131" TO INME_LCE
    MOVE "20100131" TO INME_LCT
    CLEAR INME_CHGBY
    CLEAR INME_NEWBY
    CLEAR INM_CHGBY
    MOVE OGB_SEQ1 TO OGB_SEQ
    CALL WRITE_OGBREC USING U_DEPS_FD,U_DEPS_NAME,INM_LIST
    CLEAR INM_LIST
 RETURN
.
REFORMAT_MEM
    MOVE BLANKS TO SCRATCH
    PACK SCRATCH WITH "mem.dbf /w/conv_ver_dep.txt":
                      " -p712-712eq#"",Y,"#"":
                      " 1-8 27-28 -t -j"

    REFORMAT SCRATCH
    IF OVER
        ERROR HORZ="15",VERT="19":
              BGCOL=C_ERRB,FGCOL=C_ERRF:
              LNCOL=C_ERRL,SHADE="Y":
              TEXT="REFORMAT FAILED(mem.dbf)"
        CHAIN "MENU"
    ENDIF

      MESSAGE HORZ="22",VERT="20":
              BGCOL=C_DLGB,FGCOL=C_DLGF:
              LNCOL=C_DLGL,SHADE="y":
              TEXT="******* REFORMAT COMPLETE *******";

    RETURN


 RETURN
. ----------------------------
. Operator pressed F9
. ----------------------------

QUIT
         IF (C_RMODE = "I")
            error     horz="23",vert="19":
                      bgcol=C_ERRB,fgcol=C_ERRF:
                      lncol=C_ERRL,shade="Y":
                      text=" Process Aborted by Operator ":
                      text="-----------------------------"
            messageerase
         ENDIF

         CLEAR     C_NOHIST

         TRAPREST  TRAP_DATA

         IF (RUN_MODE = "B")
            LOOP
               RETCOUNT  CURR_RETURN
               WHILE     (CURR_RETURN > ORIG_RETURN)
               NORETURN
            REPEAT
            MOVE      SCH_RTABRT  TO  SCH@RTCODE
            RETURN
         ENDIF
         CHAIN     "MENU"

INITIALIZE
         DISPLAY *P1:9,*ES
         CALL X_BLANK_SCREEN
         CLOCK     TIMESTAMP   INTO  TIMESTAMP
         CLOCK     TIMESTAMP   INTO  CURRDATE
         CLOCK     TIMESTAMP     TO  TODAY

. ...Display Counters
	 MOVE      "Recs Read:     " TO X_CNTDESC1
	 MOVE      "Recs Written:     " TO X_CNTDESC2

	 CALL      X_DISPLAY_COUNTS
         RETURN
.
OPEN_FILES
         CALL      OPEN_OGBFILE USING DEP_REF_FD,DEP_REF_NAME
         CALL      PREP_OGBFILE USING U_DEPS_FD,U_DEPS_NAME   
         RETURN

CLOSE_FILES
         RETURN

    INC       PRINT.SR
    INC       UTIL.SR      
    INC       VALID.IO
    INC       X_STANDARD.SR
    INC       FILE.IO

. ----------------------------
. Program Specific Includes
. ----------------------------

              INC       X_OGBFILE.IO

. +++++++++++++++++++++++++++++++++++++
. /End of A0901201.PS
. +++++++++++++++++++++++++++++++++++++
