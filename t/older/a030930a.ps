+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
+    Program name   :  A030930A.PS
+    apsjm 09/30/2003 A0309301 ADH - MONTHLY CLAIMS DATA BY PLAN REPORT   
+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

. ----------------------------
. Required Program Includes
. ----------------------------

              INC       COMMON.CB            ...Common work area
              INC       X_COMMON.CB          ...OGB Common Work Area
              INC       PRT.RL               ...Printer Definition File
              INC       VALID.RL             ...Valid codes
              INC       X_OGBFILE.CB         ...OGB Custom File Handler
. ----------------------------
. Program Specific Includes
. ----------------------------
              INC       MONPLAN.RL           ... PLAN BY MONTH             

. ----------------------------
. File Declarations
. ----------------------------
MONPLAN_FD       IFILE    FIX=213,UNCOMP,KEYL=11,TEXT
MONPLAN_NAME     INIT     "/w/monplan.ix1"
.
MONPLAN_KEY      DIM      11 
.
. ----------------------------
. Program Specific Variables & Working Storage
. ----------------------------
SLASH         INIT      "/"
DASH          INIT      "-"
MONPLAN_RESULT FORM      1
DISP3         DIM       3
DISP4         DIM       4
DT_CCYY       DIM       4
DT_MM         DIM       2
WS_DD         DIM       2
FIRST_PAGE    DIM       3
FIRST_TIME    DIM       1
SAVE_PLAN     DIM       5
FRONT         DIM       5
BACK          DIM       3
TIMESTAMP     DIM       14
CURRDATE      DIM       8
WRITE_COUNT   FORM      8
TRAP_DATA     DIM       500
CAL_CLOCK     DIM       21
ORIG_RETURN   FORM      5
CURR_RETURN   FORM      5
TODAY         DIM       8
PDF_NAME      DIM       25
. ...Report Fields

PAGE_NO       FORM      5
LINE_NO       FORM      3
LINE_MAX      FORM     "54"

R_PAGE_NO     DIM       5
R_DATE        DIM      10
R_TIME        DIM       8
YES           INIT     "Y"
NO            INIT     "N"

. ...Printing Controls

NORM_10       DIM      29
BOLD_10       DIM      29
NORM_12       DIM      26
BOLD_12       DIM      26
NORM_16       DIM      33
BOLD_16       DIM      33

ESC_CHAR      EQU      27
XCAPE         INIT     ESC_CHAR

PARAM_LIST    LIST
PARAM_XXX     DIM      10
PARAM_YYY     DIM       5
PARAM_DATE    DIM       8
              LISTEND
.
MASK_102      INIT      "Z,ZZZ,ZZZ,ZZ9.99-"
MASK_7        INIT      "Z,ZZZ,ZZ9" 
.
. ----------------------------
. Header Lines Definition

HDR1_REC      LIST
HDR1_FIL1     DIM   73 
HDR1_TLE      INIT  "Monthly Impact Data By Plan"             
              LISTEND
.
HDR2_REC      LIST
HDR2_FIL1     DIM   1 
HDR2_LIT1     INIT  "Date"                 
HDR2_FIL      DIM   19
HDR2_LIT2     INIT  "Amount"
HDR2_FIL3     DIM   12
HDR2_LIT3     INIT  "Amount"
HDR2_FIL4     DIM   13
HDR2_LIT4     INIT  "Amount"
HDR2_FIL5     DIM   12
HDR2_LIT5     INIT  "Amount"
HDR2_FIL6     DIM   11
HDR2_LIT6     INIT  "Amount"
HDR2_FIL7     DIM   11
HDR2_LIT7     INIT  "Amount"
HDR2_FIL8     DIM   13
HDR2_LIT8     INIT  "Amount"
HDR2_FIL9     DIM   10
HDR2_LIT9     INIT  "Amount"
              LISTEND
. 
HDR3_REC      LIST
HDR3_LIT1     INIT  "MM/CCYY"              
HDR3_FIL2     DIM   1 
HDR3_LIT2     INIT  "Plan"  
HDR3_FIL3     DIM   12 
HDR3_LIT3     INIT  "Billed"
HDR3_FIL4     DIM   12
HDR3_LIT4     INIT  "Allowed"
HDR3_FIL5     DIM   13
HDR3_LIT5     INIT  "Paid"  
HDR3_FIL6     DIM   11
HDR3_LIT6     INIT  "Allowed COB"
HDR3_FIL7     DIM   7 
HDR3_LIT7     INIT  "Paid COB"
HDR3_FIL8     DIM   8
HDR3_LIT8     INIT  "Deductible"
HDR3_FIL9     DIM   11
HDR3_LIT9     INIT  "Copay"
HDR3_FILA     DIM   9
HDR3_LITA     INIT  "Coinsurance"
HDR3_FILB     DIM   5
HDR3_LITB     INIT  "Members"
              LISTEND
.
. ----------------------------
. Detail Lines Definition
. ----------------------------
.
DTL1_REC      LIST
DTL1_DATE     DIM    7
DTL1_FIL2     DIM    2
DTL1_PLAN     DIM    5
DTL1_FIL3     DIM    2
DTL1_BILL     DIM    17
DTL1_FIL5     DIM    1
DTL1_ALLOW    DIM    17
DTL1_FIL6     DIM    1
DTL1_PAID     DIM    17
DTL1_FIL7     DIM    1
DTL1_A_COB    DIM    17
DTL1_FIL8     DIM    1
DTL1_P_COB    DIM    17
DTL1_FIL9     DIM    1
DTL1_DED      DIM    17
DTL1_FILA     DIM    1
DTL1_COPAY    DIM    17
DTL1_FILB     DIM    1
DTL1_COINS    DIM    17
DTL1_FILC     DIM    1
DTL1_MEM      DIM    9
              LISTEND
.
. ----------------------------
.
. ----------------------------
. Standard report headings
. ----------------------------

SRH_LEFT1     INIT      "Program name: "
SRH_LEFT2     INIT      "Date Printed: "
SRH_TITLE1    INIT      "   STATE OF LOUISIANA   "
SRH_TITLE2    INIT      "OFFICE OF GROUP BENEFITS"
SRH_RIGHT1    INIT      "Page Number: "
SRH_RIGHT2    INIT      "Report Time: "
SRH_DASH      INIT      "-"
                                                   
. ----------------------------
. Begin Program
. ----------------------------

. ----------------------------
. Start here if selected from menu   
. ----------------------------


         CALL      RUN_NOW

         CHAIN     C_NEXT

. ----------------------------
. Start here if called by the Scheduler    
. ----------------------------

         INC       SCHED_WP.SR              Scheduler routines (with printer)

RUN_NOW

. ...Check return stack
         RETCOUNT  ORIG_RETURN

. ----------------------------
. Fill top of screen
. ----------------------------

         DISPLAY   *ES
         MOVE      "MENU"              TO  C_NEXT
         MOVE      "MONTH BY PLAN"      TO  C_OPER
         MOVE      "A030930A"          TO  C_PROG
         MOVE      BLANKS              TO  C_LABEL
         MOVE      BLANKS              TO  C_FILE
         MOVE      BLANKS              TO  C_KEYS
         CLOCK     CALENDAR          INTO  CAL_CLOCK
         
         CALL X_BLANK_SCREEN

. ----------------------------
. Set Applicable Traps
. ----------------------------
         
. ...Set Applicable Traps
         TRAPSAVE  TRAP_DATA
         TRAPCLR   ALL
         TRAP      QUIT   NORESET  IF F9
	
. ---------------------------
. Get printer information
. ---------------------------
.        MOVE      "00000025" TO C_PRTUSR
         CALL      PRINT_PICK      .this routine gives the user a printer list
         RETURN    IF (RESULT = 2)

. ----------------------------
. Get scheduler information
. ----------------------------

         CALL     SCHED_PICK

         IF (RUN_MODE = "B" OR RUN_MODE = "N")
            CALL      MAIN_PROCESS
         ENDIF
         RETURN

. ----------------------------
. Main Process
. ----------------------------

MAIN_PROCESS
         DEBUG
         CALL      INITIALIZE
         
. ...Prepare Report         
         CALL      REPORT_PREP
         
. ...Open Input File
         CALL      OPEN_FILES

. ...READ the Input File    
         CALL      READ_INPUT
         MOVE      MONPLAN_PLAN TO SAVE_PLAN

. ...Print Heading         
         CALL PRINT_HEADING

. ...Print Report
         CALL      PRINT_REPORT

. ...Print Done
         CALL      PRINT_DONE
         
. ...Close Files
         CALL      CLOSE_FILES 

         CALL      NORMAL_EOJ

         RETURN
. ----------------------------
. End of Main Process
. ----------------------------

. ----------------------------
. Prepare The Report
. ----------------------------

REPORT_PREP

. ----------------------------
. Initialization
. ----------------------------

         UNPACK    TIMESTAMP   INTO  W_YEAR,W_MM,W_DD,W_HH,W_MN,W_SS
         PACK      R_DATE      WITH  W_MM,"/",W_DD,"/",W_YEAR,BLANKS
         PACK      R_TIME      WITH  W_HH,":",W_MN,":",W_SS,BLANKS
         MOVE      0             TO  WRITE_COUNT
         MOVE      YES           TO  FIRST_PAGE

. ----------------------------
. Set up font variables
. ----------------------------
        PACK      NORM_10 WITH  XCAPE,"&l2a1O",XCAPE,"(s p10.00h12.0v0s0b4099T"
        PACK      BOLD_10 WITH  XCAPE,"&l2a1O",XCAPE,"(s p10.00h12.0v0s3b4099T"
        PACK      NORM_12 WITH  XCAPE,"(10U",XCAPE,"(s0p12.00h12.0v0s0b3T"
        PACK      BOLD_12 WITH  XCAPE,"(10U",XCAPE,"(s0p12.00h12.0v0s3b3T"
        PACK      NORM_16 WITH  XCAPE,"&l2a1o5.45C",XCAPE,"(s0p16.67h8.5v0s0b0T"
        PACK      BOLD_16 WITH  XCAPE,"&l2a1o5.45C",XCAPE,"(s0p16.67h8.5v0s0b0T"
 
.       PACK      NORM_10 WITH  XCAPE,"(10U",XCAPE,"(s0p10.00h12.0v0s0b4099T"
.       PACK      BOLD_10 WITH  XCAPE,"(10U",XCAPE,"(s0p10.00h12.0v0s3b4099T"
.       PACK      NORM_12 WITH  XCAPE,"(10U",XCAPE,"(s0p12.00h12.0v0s0b3T"
.       PACK      BOLD_12 WITH  XCAPE,"(10U",XCAPE,"(s0p12.00h12.0v0s3b3T"
.       PACK      NORM_16 WITH  XCAPE,"(11U",XCAPE,"(s0p16.67h8.5v0s0b0T"
.       PACK      BOLD_16 WITH  XCAPE,"(11U",XCAPE,"(s0p16.67h8.5v0s3b0T"
        IF (SQUEEZE PRT_DESC = "PDF")
            PACK PDF_NAME WITH OGB_PATH,"A0309301.PDF"
            PACK C_PRTFILE WITH PDF_NAME
            MOVE      "L" TO C_ORIENT
            SET C_PCL
        ELSE   
            PACK      OGB_NAME WITH OGB_PATH,"A0309301.PRT"
            MOVE      OGB_NAME TO C_PRTFILE
        ENDIF
        CALL      PRINT_PREP
        RETURN        
. ----------------------------
. Print the report heading
. ----------------------------

PRINT_HEADING
         ADD       1           TO  PAGE_NO
         MOVE      PAGE_NO     TO  R_PAGE_NO
 
.        IF (FIRST_PAGE = YES)
.           MOVE      NO          TO  FIRST_PAGE
.           PRINT     XCAPE,"E",XCAPE,"&l0O"         Set to Potrait  
.        ELSE
.           PRINT  	   *F;
.        ENDIF
 
.        PRINT     BOLD_16,*C

         IF        (SQUEEZE PRT_DESC = "PDF")
          IF (FIRST_PAGE = YES) 
               PRINT *FONT="COURIER(07,BOLD)"
               MOVE      NO          TO  FIRST_PAGE
          ELSE
               PRINT *FONT="COURIER(07,BOLD)",*F
          ENDIF
         ELSE
                    PRINT     BOLD_16,*C
         ENDIF

 
         PRINT     *3,SRH_LEFT1,C_PROG:
                   *74,SRH_TITLE1:
                   *156,SRH_RIGHT1,(SQUEEZE R_PAGE_NO):
                   *N:
                   *3,SRH_LEFT2,R_DATE:
                   *74,SRH_TITLE2:
                   *149,SRH_RIGHT2,R_TIME
         MOVE      2 TO LINE_NO
         CALL      PRINT_SUBHDGS
         RETURN

. ----------------------------
. Print Sub Headings
. ----------------------------

PRINT_SUBHDGS
         PRINT    HDR1_REC,*N:
                  HDR2_REC,*N:
                  HDR3_REC,*N
         ADD 4 TO LINE_NO
         RETURN

PRINT_REPORT
         LOOP
             IF (MONPLAN_RESULT <> 1)
                 BREAK
             ENDIF

             IF (MONPLAN_PLAN <> SAVE_PLAN)
               CLEAR DTL1_REC
               PRINT DTL1_REC,*N
               ADD 2 TO LINE_NO
               MOVE MONPLAN_PLAN TO SAVE_PLAN
             ENDIF
              
             CALL PRINT_DET_LINE

             CALL READ_INPUT

         REPEAT

         RETURN
         
PRINT_DET_LINE
       IF (LINE_NO > LINE_MAX)
            CALL PRINT_HEADING
       ENDIF
       UNPACK  MONPLAN_DATE INTO DT_CCYY,DT_MM
       PACK DTL1_DATE WITH DT_MM,SLASH,DT_CCYY
       MOVE MONPLAN_PLAN TO DTL1_PLAN
       EDIT MONPLAN_CHGS INTO MASK_102 GIVING DTL1_BILL     
       EDIT MONPLAN_ALLOW INTO MASK_102 GIVING DTL1_ALLOW    
       EDIT MONPLAN_PAID INTO MASK_102 GIVING DTL1_PAID     
       EDIT MONPLAN_A_COB INTO MASK_102 GIVING DTL1_A_COB    
       EDIT MONPLAN_P_COB INTO MASK_102 GIVING DTL1_P_COB    
       EDIT MONPLAN_DED INTO MASK_102 GIVING DTL1_DED      
       EDIT MONPLAN_COPAY INTO MASK_102 GIVING DTL1_COPAY    
       EDIT MONPLAN_COINS INTO MASK_102 GIVING DTL1_COINS    
       EDIT MONPLAN_PAT_CT INTO MASK_7 GIVING DTL1_MEM
       PRINT DTL1_REC
       ADD 1 TO LINE_NO
       RETURN

INITIALIZE
         CLOCK     TIMESTAMP   INTO  TIMESTAMP
         CLOCK     TIMESTAMP   INTO  CURRDATE
         CLOCK     TIMESTAMP     TO  TODAY

. ...Display Counters
	 MOVE      "Recs Read   : " TO X_CNTDESC1
	 MOVE      "Recs Written: " TO X_CNTDESC2
	 MOVE      "Not Used: " TO X_CNTDESC3
	 MOVE      "Not Used: " TO X_CNTDESC4
	 MOVE      "Not Used: " TO X_CNTDESC5
	 MOVE      "Not Used: " TO X_CNTDESC6
	 CALL      X_DISPLAY_COUNTS
         RETURN
. ----------------------------
. Read Input File 
. ----------------------------
READ_INPUT
      CALL      READKS_OGBFILE_IX USING MONPLAN_FD,MONPLAN_NAME:           
                                               MONPLAN_LIST 
      MOVE      RESULT TO MONPLAN_RESULT
      IF        (MONPLAN_RESULT = 1)
          ADD       1 TO X_CNT1
          UNPACK    X_CNT1 INTO DISP4,DISP3
          IF (DISP3 = "000")
              CALL      X_DISPLAY_COUNTS
          ENDIF
      ENDIF 
      RETURN

OPEN_FILES
         CALL      OPEN_OGBFILE_IX USING MONPLAN_FD,MONPLAN_NAME     
         RETURN

CLOSE_FILES
         CALL      CLOSE_OGBFILE_IX USING MONPLAN_FD,MONPLAN_NAME  
         RETURN

QUIT
         IF (C_RMODE = "I")
            error     horz="23",vert="19":
                      bgcol=C_ERRB,fgcol=C_ERRF:
                      lncol=C_ERRL,shade="Y":
                      text=" Process Aborted by Operator ":
                      text="-----------------------------"
            messageerase
         ENDIF

         CLEAR     C_NOHIST

         TRAPREST  TRAP_DATA

         IF (RUN_MODE = "B")
            LOOP
               RETCOUNT  CURR_RETURN
               WHILE     (CURR_RETURN > ORIG_RETURN)
               NORETURN
            REPEAT
            MOVE      SCH_RTABRT  TO  SCH@RTCODE
            RETURN
         ENDIF
         CHAIN     C_NEXT

NORMAL_EOJ
	 MOVE      "        NEOJ        " TO X_FINISH_STATUS
	 MOVE      "MONTHLY PLAN PRINT COMPLETE" TO X_PROG_FINISH
	 CALL      X_END_OF_JOB
         TRAPREST  TRAP_DATA
         MOVE      SCH_RTSUCC  TO  SCH@RTCODE
         RETURN                                                   
. ----------------------------
              INC       UTIL.SR      
              INC       VALID.IO
              INC       PRINT.SR
              INC       X_STANDARD.SR
              INC       FILE.IO

. ----------------------------
. Program Specific Includes
. ----------------------------

              INC       X_OGBFILE.IO

. +++++++++++++++++++++++++++++++++++++
. /End of A030930A.PS
. +++++++++++++++++++++++++++++++++++++
