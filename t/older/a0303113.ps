+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
+    Program name   :  A0303113.PS
+    Description    :  Program to extract paid claims for HCA Providers
+
+    apeks 03/12/2003 a0303113 DEV - Initial Version. 
+..............................REVISIONS.......................................
+    apiii mm/dd/ccyy ayymmdd# MOD - Description        
+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

. ----------------------------
. Required Program Includes
. ----------------------------

              INC       COMMON.CB            ...Common work area
              INC       X_COMMON.CB          ...OGB Common Work Area
              INC       VALID.RL             ...Valid codes
              INC       X_OGBFILE.CB         ...OGB Custom File Handler
              
. ----------------------------
. Program Specific Includes
. ----------------------------

              INC       MEM.RL               ...Member Demographics
              INC       MEME.RL              ...Member Enrollment
              INC       MEMA.RL              ...Member Accum File
              INC       CLMA.RL              ...Claim Accum File
              INC       SCH.RL               ...Scheduler File  

. ----------------------------
. File Declarations
. ----------------------------

. Reformat of CLM File
TMP_FILE        FILE    VAR=100,TEXT
TMP_NAME        INIT    "/w/a0303113.tmp"
TMP_LIST        LIST
TMP_CLM         DIM     8
TMP_PRV         DIM     8
TMP_CHG         DIM     10
TMP_PDPRV       DIM     10
TMP_PDMEM       DIM     10
TMP_COPAY       DIM     10
TMP_DED         DIM     10
                LISTEND

. Output Files
OUT_FILE        IFILE   VAR=150,KEYLEN=8,TEXT
OUT_NAME        INIT    "/w/a0303113.ix1"
OUT_LIST        LIST
OUT_PRV         DIM     8
OUT_1           DIM     1
OUT_PRVNM       DIM     20
OUT_2           DIM     1
OUT_CHG         DIM     12
OUT_3           DIM     1
OUT_PDPRV       DIM     12
OUT_4           DIM     1
OUT_PDMEM       DIM     12
OUT_5           DIM     1
OUT_COPAY       DIM     12
OUT_6           DIM     1
OUT_DED         DIM     12
                LISTEND

CSV_NAME        INIT    "/w/a0303113.csv"

. ---------------------------------------------
. Program Specific Variables & Working Storage
. ---------------------------------------------
CAL_CLOCK       DIM       21 
CYCLE_DATE      DIM       8
HOLD_CYCLE_DATE DIM       8
HOLD_FILE_NME   DIM       31
ORIG_RETURN     FORM      5       
PARAM_LIST      LIST
PARAM_XXX       DIM       10
PARAM_YYY       DIM       5
PARAM_DATE      DIM       8
       	        LISTEND
TRAP_DATA       DIM       500

DIM2            DIM       2
DIM8            DIM       8
DIM10           DIM       10
DIM24           DIM       24
MCS@DIM1        DIM       @
MCS@DIM2        DIM       @
MCS@FORM1       FORM      @

W_YR1           FORM      4
W_YR2           FORM      4

WS_CCYY         DIM       4
WS_CC           DIM       2
WS_YY           DIM       2
WS_MM           DIM       2
WS_DD           DIM       2

CLM_RD_CNT      FORM      10 
CLM_WT_CNT      FORM      10 

W_FROM          INIT      "19990301"
W_THRU          INIT      "20010720"
W_CLMDATE       DIM       8
W_CURRDT        DIM       8

W_HCA           DIM       1
W_HCA_HOSP      DIM       20

AMT1            FORM      9.2
AMT2            FORM      9.2
AMT3            FORM      9.2

W_PDPRV         DIM       12 
W_PDMEM         DIM       12 
W_COPAY         DIM       12 
W_DED           DIM       12  
W_CHG           DIM       12 

. ----------------------------
. Begin Program
. ----------------------------

. ---------------------------------
. Start here if selected from menu   
. ---------------------------------

         CALL      RUN_NOW

         CHAIN     C_NEXT

. --------------------------------------
. Start here if called by the Scheduler    
. --------------------------------------

         INC       SCHED_NP.SR              Scheduler routines (with printer)

RUN_NOW

. ...Check return stack
         RETCOUNT  ORIG_RETURN

. ----------------------------
. Fill top of screen
. ----------------------------

. ...Screen Display
         MOVE      "MENU"              TO  C_NEXT
         MOVE      "HCA CLAIMS" TO  C_OPER
         MOVE      "A0303113"           TO  C_PROG
         MOVE      BLANKS              TO  C_LABEL
         MOVE      BLANKS              TO  C_FILE
         MOVE      BLANKS              TO  C_KEYS
         CLOCK     CALENDAR          INTO  CAL_CLOCK
         
         CALL X_BLANK_SCREEN

. ----------
.   9-Quit
. ----------

         DISPLAY   *RESETSW,*BGCOLOR=C_MANB,*FGCOLOR=C_MANF,*ES:
                   *BGCOLOR=C_DATB,*COLOR=C_DATF:
                   *P1:2,*RPTCHAR *HLN:80,*+:
                   *P3:2,"<",CAL_CLOCK,">":
                   *P29:2,"<",C_OPER,">":
                   *P54:2,"<",C_PROG,">":
                   *P66:2,"<REPORT>":
                   *P01:1,*BGCOLOR=C_F01B,*COLOR=C_F01F,"        ":
                   *P09:1,*BGCOLOR=C_F02B,*COLOR=C_F02F,"        ":
                   *P17:1,*BGCOLOR=C_F03B,*COLOR=C_F03F,"        ":
                   *P25:1,*BGCOLOR=C_F04B,*COLOR=C_F04F,"        ":
                   *P33:1,*BGCOLOR=C_F05B,*COLOR=C_F05F,"        ":
                   *P41:1,*BGCOLOR=C_F06B,*COLOR=C_F06F,"        ":
                   *P49:1,*BGCOLOR=C_F07B,*COLOR=C_F07F,"        ":
                   *P57:1,*BGCOLOR=C_F08B,*COLOR=C_F08F,"        ":
                   *P65:1,*BGCOLOR=C_F09B,*COLOR=C_F09F," 9-Quit ":
                   *P73:1,*BGCOLOR=C_F10B,*COLOR=C_F10F,"        ":
                   *BGCOLOR=C_DLGB,*COLOR=C_DLGF


. ----------------------------
. Set Applicable Traps
. ----------------------------
         
. ...No Traps should be Set
         TRAPSAVE  TRAP_DATA
         TRAPCLR   ALL
	
. ----------------------------
. Get scheduler information
. ----------------------------

         CALL     SCHED_PICK

         IF (RUN_MODE = "B" OR RUN_MODE = "N")
            CALL      MAIN_PROCESS
         ENDIF
         RETURN

. ----------------------------
. Main Process
. ----------------------------

MAIN_PROCESS

         CALL INIT_PROG
         CALL FIND_CLAIMS
         CALL COPY_TO_CSV    

         RETURN

. ----------------------------
. Initialize Program       
. ----------------------------

INIT_PROG

         CALL MAKE_INPUT_FILE
         CALL SCR_DISPLAY
         CALL OPEN_FILES

         RETURN

. ----------------------------
. Reformat files for Input  
. ----------------------------

MAKE_INPUT_FILE

         IF (C_RMODE = "I")
            message   horz="26",vert="10":
                      bgcol=C_DLGB,fgcol=C_DLGF:
                      lncol=C_DLGL,shade="Y":
                      text=" Reformatting CLM File":
                      text=" Please wait...      "
         ENDIF

. Reformat claims with a deductible amount output since last clmdate
         CLEAR SCRATCH
         PACK SCRATCH WITH "clm.dbf ",TMP_NAME," 1-8 46-53":
                           " 2359-2368 2449-2468":
                           " 2234-2253 -p2491neB":
                           " -p326-333ge",W_FROM:
                           " -p326-333le",W_THRU," -t -j"
        
         REFORMAT SCRATCH

         RETURN

. ----------------------------
. Screen display
. ----------------------------

SCR_DISPLAY

         IF (C_RMODE = "I")
            message   horz="26",vert="10":
                      bgcol=C_DLGB,fgcol=C_DLGF:
                      lncol=C_DLGL,shade="Y":
                      text="       -Records Processed-        ":
                      text=" Claims Read                      ":
                      text=" Claims Written                   "
         ENDIF

         MOVE 0 TO CLM_RD_CNT
         MOVE 0 TO CLM_WT_CNT

         CALL DISPLAY_CNTS 

         RETURN

. ----------------------------
. Open Program Files        
. ----------------------------

OPEN_FILES

         CALL OPEN_OGBFILE USING TMP_FILE,TMP_NAME

         CALL PREP_OGBFILE_IX USING OUT_FILE,OUT_NAME

         PACK TMP_PRV    WITH "HOSP ID",BLANKS
         PACK W_HCA_HOSP WITH "HOSPITAL NAME",BLANKS
         PACK TMP_CHG    WITH "CHARGES",BLANKS
         PACK TMP_PDPRV  WITH "PAID HCA",BLANKS
         PACK TMP_PDMEM  WITH "PAID MEM",BLANKS
         PACK TMP_COPAY  WITH "COPAY",BLANKS
         PACK TMP_DED    WITH "DEDUCTIBLE",BLANKS

         CALL WRITE_OUT

         RETURN

. ----------------------------
.  Extract Qualified Claims 
. ----------------------------

FIND_CLAIMS

         MOVE OGB_SEQ1 TO OGB_SEQ

. Read re-formatted claim file
         LOOP
           CALL READ_OGBFILE USING TMP_FILE,TMP_NAME,TMP_LIST
           IF (RESULT <> 1)
              GOTO FIND_CLAIMS_END
           ENDIF

         ADD 1 TO CLM_RD_CNT
         
         CALL CHECK_PRV

         IF (W_HCA = "N")
            GOTO FIND_CLAIMS_MORE
         ENDIF

         debug
         CALL ADD_AMT USING TMP_CHG,W_CHG 
         CALL ADD_AMT USING TMP_PDPRV,W_PDPRV 
         CALL ADD_AMT USING TMP_PDMEM,W_PDMEM 
         CALL ADD_AMT USING TMP_COPAY,W_COPAY 
         CALL ADD_AMT USING TMP_DED,W_DED

. Read for this provider and add paid/charge amounts
         CALL READ_OGBFILE_IX USING OUT_FILE,OUT_NAME,TMP_PRV,OUT_LIST
         IF (RESULT <> 1)
            CALL WRITE_OUT      
            GOTO FIND_CLAIMS_MORE
         ENDIF

         CALL ADD_AMT USING TMP_CHG,OUT_CHG 
         CALL ADD_AMT USING TMP_PDPRV,OUT_PDPRV 
         CALL ADD_AMT USING TMP_PDMEM,OUT_PDMEM 
         CALL ADD_AMT USING TMP_COPAY,OUT_COPAY 
         CALL ADD_AMT USING TMP_DED,OUT_DED

         CALL UPDATE_OGBREC_IX USING OUT_FILE,OUT_LIST

FIND_CLAIMS_MORE
         REPEAT

FIND_CLAIMS_END
         RETURN

. ----------------------------
. Check for HCA Providers   
. ----------------------------

CHECK_PRV

         PACK W_HCA WITH "N"
         CLEAR W_HCA_HOSP

         SWITCH TMP_PRV
            CASE "00057117"
                 PACK W_HCA WITH "Y"
                 PACK W_HCA_HOSP WITH "SAVOY MEDICAL CENTER"
            CASE "00072176"
                 PACK W_HCA WITH "Y"
                 PACK W_HCA_HOSP WITH "WINN PARISH MEDICAL "
            CASE "00072174"
                 PACK W_HCA WITH "Y"
                 PACK W_HCA_HOSP WITH "OAKDALE COMMUNITY   "
            CASE "00072175"
                 PACK W_HCA WITH "Y"
                 PACK W_HCA_HOSP WITH "AVOYELLES HOSPITAL  "
            CASE "00063244"
                 PACK W_HCA WITH "Y"
                 PACK W_HCA_HOSP WITH "RAPIDES REGIONAL MED"
            CASE "00072552"
                 PACK W_HCA WITH "Y"
                 PACK W_HCA_HOSP WITH "DAUTERIVE HOSPITAL  "
            CASE "00050349"
                 PACK W_HCA WITH "Y"
                 PACK W_HCA_HOSP WITH "LAKELAND MEDICAL CTR"
            CASE "00072556"
                 PACK W_HCA WITH "Y"
                 PACK W_HCA_HOSP WITH "MEDICAL CENTER OF SW"
            CASE "00062097"
                 PACK W_HCA WITH "Y"
                 PACK W_HCA_HOSP WITH "TULANE UNIV HOSPITAL"
            CASE "00050378"
                 PACK W_HCA WITH "Y"
                 PACK W_HCA_HOSP WITH "LAKEVIEW REGIONAL   "
            CASE "00107891"
                 PACK W_HCA WITH "Y"
                 PACK W_HCA_HOSP WITH "LAKEVIEW REGIONAL   "
            CASE "00107890"
                 PACK W_HCA WITH "Y"
                 PACK W_HCA_HOSP WITH "LAKEVIEW REGIONAL   "
            CASE "00072557"
                 PACK W_HCA WITH "Y"
                 PACK W_HCA_HOSP WITH "WOMEN AND CHILDREN'S"
            CASE "00072075"
                 PACK W_HCA WITH "Y"
                 PACK W_HCA_HOSP WITH "NORTH MONROE MED CTR"
         ENDSWITCH

         RETURN

. ----------------------------
. Write Output File         
. ----------------------------

WRITE_OUT 

         PACK OUT_PRV   WITH TMP_PRV
         PACK OUT_PRVNM WITH W_HCA_HOSP
         PACK OUT_CHG   WITH TMP_CHG
         PACK OUT_PDPRV WITH TMP_PDPRV
         PACK OUT_PDMEM WITH TMP_PDMEM
         PACK OUT_COPAY WITH TMP_COPAY
         PACK OUT_DED   WITH TMP_DED  

         PACK OUT_1  WITH "," 
         PACK OUT_2  WITH "," 
         PACK OUT_3  WITH "," 
         PACK OUT_4  WITH "," 
         PACK OUT_5  WITH "," 
         PACK OUT_6  WITH "," 
         
         MOVE OGB_SEQ1 TO OGB_SEQ
         CALL  WRITE_OGBREC_IX USING OUT_FILE,OUT_NAME,OUT_PRV,OUT_LIST

         RETURN

. ----------------------------
. Add amount fields         
. ----------------------------

ADD_AMT ROUTINE MCS@DIM1,MCS@DIM2
.second field in "using" is destination field

         MOVE MCS@DIM1 TO AMT1
         MOVE MCS@DIM2 TO AMT2
         ADD AMT1 TO AMT2 GIVING AMT3
         MOVE AMT3 TO MCS@DIM2

         RETURN

. ----------------------------
. Copy output file to .csv  
. ----------------------------

COPY_TO_CSV

         PACK TMP_PRV WITH BLANKS
         PACK W_HCA_HOSP WITH "HCA TOTALS"
         PACK TMP_CHG   WITH W_CHG
         PACK TMP_PDPRV WITH W_PDPRV
         PACK TMP_PDMEM WITH W_PDMEM
         PACK TMP_COPAY WITH W_COPAY
         PACK TMP_DED   WITH W_DED  

         CALL WRITE_OUT

         CALL CLOSE_OGBFILE_IX USING OUT_FILE,OUT_NAME

         CLEAR SCRATCH
         PACK SCRATCH WITH "cp /w/a0303113.txt ",CSV_NAME 

         ROLLOUT SCRATCH

         IF OVER
               ERROR     HORZ="15",VERT="19":
                         BGCOL=C_ERRB,FGCOL=C_ERRF:
                         LNCOL=C_ERRL,SHADE="Y":
                         TEXT="File Copy FAILED":
                         TEXT="Contact APPS"
               CHAIN     "MENU"
         ENDIF

         RETURN

. ----------------------------
. Display Delay             
. ----------------------------

DELAY_DSP ROUTINE MCS@FORM1

        MOVE MCS@FORM1 TO DIM10 
        UNPACK DIM10 INTO DIM8,DIM2
        IF (DIM2 = "00")
           CALL DISPLAY_CNTS
        ENDIF

        RETURN

. ----------------------------
. Display Counters          
. ----------------------------

DISPLAY_CNTS 

         IF (C_RMODE = "I")
            DISPLAY   *P51:12,CLM_RD_CNT
            DISPLAY   *P51:13,CLM_WT_CNT
         ENDIF

         RETURN

. ----------------------------
. Program Specific Includes
. ----------------------------
	      INC	FILE.IO
	      INC	VALID.IO
	      INC	X_STANDARD.SR
	      INC	X_OGBFILE.IO

. +++++++++++++++++++++++++++++++++++++
. /End of CLM0200.PS
. +++++++++++++++++++++++++++++++++++++
