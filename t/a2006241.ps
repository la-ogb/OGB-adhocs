./* ====================================================================
+ NAME:
+   a2006241.ps
+
+ DESCRIPTION:
+   Fixes erronous data sent threw section111 file
+
+ CREATED BY:
+   apdrh - 2020-06-24
+
+ LAST MODIFIED BY:
+   apjwh - 2020-06-26
. * ================================================================= */

./* =================
. * Variable Includes
. * ================= */
    INC UTIL_COMMON.CB  ... OGB Common variables for UTIL routine
    INC UTIL_MEMMEM.CB
    INC MSPA.RL
    INC VALID.RL

./* ===================================
. * Scheduler Params
. * =================================== */
PARAM_LIST              LIST WITH NAMES
PARAM_MODE              CHAR    4
                        LISTEND

./* ===================================
. * File & Global Variable Declarations
. * =================================== */

...Input File
OLD_DATA_FILE           FILE    VAR=500,TEXT
OLD_DATA_NAME           INIT    "/w/20150415.txt"
OLD_DATA_LIST               LIST
OLD_DATA_REC            CHAR    500
                        LISTEND

...Output File
FIX_TERM_FILE           FILE    VAR=500,TEXT
FIX_TERM_NAME           INIT    "/w/20200415sct111.txt"
FIX_TERM_LIST           LIST
FIX_TERM_REC            CHAR    500
                        LISTEND

RUNDATE                 CHAR    8
DAY_BEFORE_EFF          CHAR    8

./* =============
. * Begin Program
. * ============= */

...Non-Impact scheduler run - Start here
    CALL RUN_NOW
    CHAIN C_NEXT

...Scheduler begins here
    INC SCHED_NP.SR // for use without the printer

.#######################################################################
RUN_NOW LROUTINE
./* Set runtime variables
. */
    MOVE "a2006211" TO C_PROG
    MOVE "Error Fix" TO C_OPER  ...Change
    MOVE "menu" TO C_NEXT
    MOVE BLANKS TO C_LABEL
    MOVE BLANKS TO C_FILE
    MOVE BLANKS TO C_KEYS

    IF (C_CMDPAR)
        CALL CMD_START USING C_PROG // signal start to Autosys
    ENDIF

...Set applicable traps
    TRAPSAVE TRAP_DATA
    TRAPCLR ALL
    TRAP QUIT NORESET IF F9

...Get scheduler information
    CALL SCHED_PICK

    IF (RUN_MODE = "B" | RUN_MODE = "N")
        CALL MAIN_PROCESS
    ENDIF

    RETURN
    ENDROUTINE


.#######################################################################
MAIN_PROCESS LROUTINE
    debug
    CALL INITIALIZE
    CALL PROCESS_MSP
    CALL CLEAN_UP

    RETURN
    ENDROUTINE


.#######################################################################
INITIALIZE LROUTINE

    CLOCK TIMESTAMP TO RUNDATE

.Setup counters
    PACK X_CNTDESC1 WITH "Records Read            ",BLANKS
    PACK X_CNTDESC2 WITH "Term Date Changed       ",BLANKS
    PACK X_CNTDESC3 WITH "Record to Delete        ",BLANKS
    PACK X_CNTDESC4 WITH "Total Records Changed   ",BLANKS

    CALL X_ZERO_COUNTS
    IF NOT (C_CMDPAR)
        CALL X_DISPLAY_COUNTS
    ENDIF

    CALL OPEN_OGBFILE USING OLD_DATA_FILE,OLD_DATA_NAME
    CALL PREP_OGBFILE USING FIX_TERM_FILE,FIX_TERM_NAME

    RETURN
    ENDROUTINE


.#######################################################################
PROCESS_MSP LROUTINE
+ Process the old MSP file from 4/2015 and generate the correct term
+ date for all members that lost status as an Active Covered Individual
+ since then.
+
+ NOTE: This process will only fix those record we sent erroneously.
+ It will not handle cases such as rehired retirees & members returning
+ to BCBS afeter dropping it for the span that was reported on this old
+ file
FIRST_MEME   NUM   1
...

    MOVE OGB_SEQ1 TO OGB_SEQ
    LOOP
        CALL READ_OGBFILE_SEQ USING OLD_DATA_FILE,OLD_DATA_NAME,MSPX_LIST
        WHILE (RESULT = 1)

        CALL UTIL_XCOUNTER USING X_CNT1,100
        debug
        CALL UTIL_MEM_FIND USING MSPX_MEM
        WHILE EQUAL

        SET FIRST_MEME
        LOOP
            IF (FIRST_MEME)
                CLEAR FIRST_MEME
                CALL UTIL_MEME_FIND USING MEM_ID1
            ELSE
                CALL UTIL_MEME_NEXT
            ENDIF
            WHILE EQUAL

            CLEAR MEME_SAV

.Skip extra billing records and other records we don't care about
            CONTINUE IF (SQUEEZE MEME_NET = "LIFE")
            CONTINUE IF (SQUEEZE MEME_PLAN = "HRA1")
            CONTINUE IF (SQUEEZE MEME_LEV2 = "95" & SQUEEZE MEME_MODE = "")
            CONTINUE IF (SQUEEZE MEME_LEV2 = "90" & SQUEEZE MEME_MODE != "")
            CONTINUE IF (SQUEEZE MEME_TRM != "" & MEME_EFF > MEME_TRM)
            CONTINUE IF (SQUEEZE MEME_PRD = "HSA")
            CONTINUE IF (SQUEEZE MEME_NET = "OGB")


.We only consider the BCBS plans to be the GHP for the purposes of reporting
.an Active Covered Individual on the MSP, excluding the HSA
            CONTINUE IF (SQUEEZE MEME_PRD = "HSA")
            CONTINUE IF (SQUEEZE MEME_TRM != "" & MEME_EFF > MEME_TRM)
            CONTINUE IF (CHOP MEME_NET = "BLUE")

            IF (SQUEEZE MEME_NET = "PPLSH")
                debug
                PACK MEME_SAV WITH MEME_LIST
                CALL TERM_DATE
                CALL RECORD_WRITE
                BREAK
            ENDIF

.Coverge under a retirement group isn't reported as MSP
            CMATCH "R" IN MEME_LEV1
            IF (EQUAL)
                PACK MEME_SAV WITH MEME_LIST
                CALL DELETE_REC
                CALL RECORD_WRITE
                BREAK
            ENDIF

.This is not a health record. It will never actually hit this
            IF (SQUEEZE MEME_NET = "NOCOV")
                PACK MEME_SAV WITH MEME_LIST
                CALL TERM_DATE
                CALL RECORD_WRITE
                BREAK
            ENDIF
        REPEAT
    REPEAT

    RETURN
    ENDROUTINE


.#######################################################################
TERM_DATE LROUTINE
+ Sets Term date to 1 day before effective date

    CALL DATE_CALC USING MEME_EFF, "SUB", "DAYS", 1, DAY_BEFORE_EFF
    MOVE DAY_BEFORE_EFF TO MSPX_TRM
    CALL UTIL_XCOUNTER USING X_CNT2,100

    RETURN
    ENDROUTINE


.#######################################################################
DELETE_REC LROUTINE
+ Delete retired recs

    MOVE "1" TO MSPX_TRXT
    CALL UTIL_XCOUNTER USING X_CNT3,100

    RETURN
    ENDROUTINE


.#######################################################################
RECORD_WRITE LROUTINE

    CALL WRITE_OGBREC USING FIX_TERM_FILE,FIX_TERM_NAME,MSPX_LIST
    CALL UTIL_XCOUNTER USING X_CNT4,100

    RETURN
    ENDROUTINE


.#######################################################################
CLEAN_UP

    CALL CLOSE_OGBFILE USING OLD_DATA_FILE,OLD_DATA_NAME
    CALL CLOSE_OGBFILE USING FIX_TERM_FILE,FIX_TERM_NAME

    RETURN
    ENDROUTINE


./* ==============================
. * Subroutines and Other Includes
. * ============================== */
... MCSI includes found in /nfsgb2/mcsprd/i
... B includes found in /mcsapl/i
    INC UTIL_COMMON.SR
    INC UTIL_MEM.SR
    INC UTIL_MEME.SR

./* ==============
. * End of program
. * ============== */
