++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
+ NAME:
+   a2405012.ps
+
+ DESCRIPTION:
+   Term all DHH enrollments for end of contract - 6/30/24
+
+ CREATED BY:
+   apjwh - 2024-05-01
++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

.# =================================================================== #
.# Variable Includes
.# =================================================================== #
    INC VALID.RL
    INC UTIL_COMMON.CB
    INC UTIL_MEMMEM.CB
    INC MEMU.RL
    INC CLI.RL
    INC XALID.RL

.# =================================================================== #
.# Scheduler Parameters
.# =================================================================== #

PARAM_LIST          LIST WITH NAMES
PARAM_TEST          CHAR    4
                    LISTEND

.# =================================================================== #
.# Equates for code clarity
.# =================================================================== #

V_FLAG              EQU     1   ...0 or 1
V_DATE              EQU     8   ...YYYYMMDD
MSG_HEIGHT          EQU     24
REFRESH_RATE        EQU     200

.# =================================================================== #
.# Configuration
.# =================================================================== #

PROG_ID             INIT    "TRMDHH"
EMAIL_TO_NOERROR    INIT    "$ITAPPS"
EMAIL_TO_ERROR      INIT    "$ITAPPS"
CONTRACT_END        INIT    "20240630"

.# =================================================================== #
.# Debugging Configuration
.# =================================================================== #

ENABLE_TEST         NUM     V_FLAG
TEST_CONFIG         LIST
TEST_EMAIL_TO       INIT    "$CURRENT_USER_EMAIL"
                    LISTEND

.# =================================================================== #
.# File Declarations
.# =================================================================== #

WORK_FD             FILE    FIXED=X_MEME_SIZE,TEXT
WORK_FILE           INIT    "/w/tmp/a2405012.id.wrk"

UPDATE_FD           FILE    FIXED=X_INM_SIZE,TEXT
UPDATE_FILE         INIT    "a2405012.upd"

REPORT_FD           FILE    VAR=200,TEXT
REPORT_FILE         INIT    "/w/tmp/a2405012.csv"

.# =================================================================== #
.# File lists
.# =================================================================== #

WORK_LIST           LIST
WORK_ID             CHAR    8
                    LISTEND

REPORT_LIST         LIST
REPORT_REC          CHAR    200
                    LISTEND

REPORT_HEADER       VARLIST "MEMBER ID,":
                            "LOG MESSAGE,":
                            "FIRST NAME,":
                            "LAST NAME,":
                            "RELATION,":
                            "LEV1,":
                            "LEV2,":
                            "INDIVIDUALLY BILLED,":
                            "MEME EFF,":
                            "MEME TRM,":
                            "NETWORK,":
                            "PRODUCT,":
                            "PLAN,":
                            "ZIP TYPE,":
                            "ZIP CODE,":
                            "STATE"

.# =================================================================== #
.# Program Variables
.# =================================================================== #

CURRDATE            CHAR    V_DATE
START_DATETIME      CHAR    24
END_DATETIME        CHAR    24

SOMETHING_IN_LOG    NUM     1
ORIG_EFF            CHAR    8
ORIG_TRM            CHAR    8
ORIG_TLET           CHAR    1
ORIG_TRUN           CHAR    8
ZIP_FAIL            NUM     1
ZIP_TYPE            CHAR    20
ZIPCODE             CHAR    5
ZIPSTATE            CHAR    2

.# =================================================================== #
.# Begin Program
.# =================================================================== #
.Manual and Autosys
    CALL RUN_NOW
    CHAIN C_NEXT    ...go to next program (usually the Menu)

.Impact Scheduler Hook
    INC SCHED_NP.SR     ...for jobs that don't use the print system


.#######################################################################
RUN_NOW ROUTINE
+ Configure runtime settings

.Save stack size at program start
    RETCOUNT ORIG_RETURN

.Set applicable traps
    CALL UTIL_SET_NORMAL_TRAPS

.Get run date
    CLOCK TIMESTAMP INTO CURRDATE

    MOVE "A2405012" TO C_PROG
    MOVE "TERM DHH MEME" TO C_OPER
    MOVE "MENU" TO C_NEXT
    MOVE "RUN_NOW" TO C_LABEL
    MOVE BLANKS TO C_FILE
    MOVE BLANKS TO C_KEYS

.Check if the program was called from the command line
    IF (C_CMDPAR)
        CALL CMD_START USING C_PROG
    ELSE
        CALL X_BLANK_SCREEN
    ENDIF

.Configure test options based on file mode. Default is Production
    CALL UTIL_SET_TESTMODE USING PARAM_TEST:  ...scheduler param
                                 ENABLE_TEST:  ...test toggle
                                 TEST_CONFIG  ...test settings list

.Set up run mode or build SCH record
    CALL SCHED_PICK

    SWITCH RUN_MODE
        CASE "N" | "B"
            CALL MAIN_PROCESS
        DEFAULT
    ENDSWITCH

    CALL NORMAL_EOJ

    RETURN
    ENDROUTINE


.#######################################################################
MAIN_PROCESS LROUTINE
    debug
    CALL INITIALIZE
    CALL MEME_REFORMAT
    CALL TERM_ENROLLMENTS
    CALL CLEAN_UP

    RETURN
    ENDROUTINE


.#######################################################################
INITIALIZE LROUTINE
+ Setup runtime variables
    CLOCK TIMESTAMP TO CURRDATE
    CLOCK CALENDAR TO START_DATETIME

.Get the files and EDI locations from valid 101
    CALL UTIL_GET_SYS_DIRS_COMMON

    CALL UTIL_INIT_COUNTERS USING "Enrollments Proc'd":
                                  "Errors":
                                  "Updates":
                                  "":
                                  "":
                                  ""

    RETURN
    ENDROUTINE


.#######################################################################
MEME_REFORMAT LROUTINE
+ Sort MEME records for VANTA plans other then MHHP

    CALL UTIL_XMESSAGE USING "Building work files...",MSG_HEIGHT

.Find future termed coverage
    PACK SCRATCH USING MEME_FNS," ",WORK_FILE," -t":
                        " -p0112-0116eq#"DHH  #"":
                        " -p0104-0111gt#"",CONTRACT_END,"#""

    CALL UTIL_DO_REFORMAT USING SCRATCH,"Finding MEME Data 1"

.Find open coverage
    PACK DATE_CCYYMMDD WITH BLANKS
    PACK SCRATCH USING MEME_FNS," ",WORK_FILE," -t -a":
                        " -p0112-0116eq#"DHH  #"":
                        " -p0104-0111eq#"",DATE_CCYYMMDD,"#""

    CALL UTIL_DO_REFORMAT USING SCRATCH,"Finding MEME Data 2"

.Sort file
    PACK SCRATCH WITH WORK_FILE, " ", WORK_FILE," 1-12 -t"
    CALL UTIL_DO_SORT USING SCRATCH, "Sorting work file"

    MESSAGEERASE

    RETURN
    ENDROUTINE


.#######################################################################
TERM_ENROLLMENTS LROUTINE
    CALL OPEN_OGBFILE USING WORK_FD,WORK_FILE
    CALL PREP_OGBFILE USING UPDATE_FD,UPDATE_FILE

    CALL PREP_OGBFILE USING REPORT_FD,REPORT_FILE
    CALL WRITE_OGBREC_SEQ USING REPORT_FD,REPORT_FILE,REPORT_HEADER

    LOOP
        CALL READ_OGBFILE_SEQ USING WORK_FD,WORK_FILE,MEME_LIST
        WHILE (RESULT = 1)

        CALL UTIL_MEM_FIND USING MEME_ID1
        CONTINUE IF NOT EQUAL

.skip voids
        IF (CHOP MEME_TRM > "")
            CONTINUE IF (CHOP MEME_TRM < MEME_EFF)
            CONTINUE IF (CHOP MEME_TRM <= CONTRACT_END)
        ENDIF

        CALL UTIL_XCOUNTER USING X_CNT1,REFRESH_RATE

        CALL TERM_PLAN
    REPEAT

    CALL CLOSE_OGBFILE USING UPDATE_FD,UPDATE_FILE
    CALL CLOSE_OGBFILE USING WORK_FD,WORK_FILE
    CALL UTIL_DELETE_FILE USING WORK_FILE

    RETURN
    ENDROUTINE


.#######################################################################
LOG_UPDATES LROUTINE MSG@
MSG@        CHAR    @
...
    SET SOMETHING_IN_LOG
    PACK REPORT_REC WITH    (CHOP MEME_ID1),PIPE:
                            (CHOP MSG@),PIPE:
                            (CHOP MEM_FNAME),PIPE:
                            (CHOP MEM_LNAME),PIPE:
                            (CHOP MEM_REL),PIPE:
                            (CHOP MEME_LEV1),PIPE:
                            (CHOP MEME_LEV2),PIPE:
                            (CHOP MEME_MODE),PIPE:
                            (CHOP MEME_EFF),PIPE:
                            (CHOP MEME_TRM),PIPE:
                            (CHOP MEME_NET),PIPE:
                            (CHOP MEME_PRD),PIPE:
                            (CHOP MEME_PLAN)

    REPLACE ",;" IN REPORT_REC
    REPLACE "|," IN REPORT_REC

    CALL WRITE_OGBREC_SEQ USING REPORT_FD,REPORT_FILE,REPORT_LIST

    RETURN
    ENDROUTINE


.#######################################################################
LOG_ERROR LROUTINE MSG@
MSG@        CHAR    @
...
    CALL LOG_UPDATES USING MSG@,""
    CALL UTIL_XCOUNTER USING X_CNT2,REFRESH_RATE

    RETURN
    ENDROUTINE


.#######################################################################
TERM_PLAN LROUTINE

.For future plans, void & replace
    IF (MEME_EFF > CONTRACT_END)
        CALL UTIL_CALC_DAY_BEFORE USING MEME_EFF,MEME_TRM
        PACK MEME_TLET WITH "V"

        CALL WRITE_UPDATE
        CALL LOG_UPDATES USING "VOIDED FUTURE"

.For active plans, default to replacement for 1/1
    ELSE
        PACK MEME_TRM WITH CONTRACT_END
        PACK MEME_TRUN WITH MEME_TRM
        PACK MEME_TLET WITH "T"

        CALL WRITE_UPDATE
        CALL LOG_UPDATES USING "TERMED"
    ENDIF

    RETURN
    ENDROUTINE


.#######################################################################
WRITE_UPDATE LROUTINE
+ Write update record
HOLD_MEM    CHAR    X_MEM_SIZE
...
    PACK HOLD_MEM WITH MEM_LIST
    FILL " " IN MEM_LIST
    PACK MEM_ID1 WITH MEME_ID1,BLANKS

    FILL " " IN INM_LIST
    PACKLEN INM_REC WITH MEM_LIST,MEME_LIST
    UNPACK INM_REC INTO INM_LIST

    CALL UTIL_FORMAT_INME USING PROG_ID

    CALL WRITE_OGBREC_SEQ USING UPDATE_FD,UPDATE_FILE,INM_LIST
    CALL UTIL_XCOUNTER USING X_CNT3,REFRESH_RATE

    UNPACK HOLD_MEM INTO MEM_LIST

    RETURN
    ENDROUTINE


.#######################################################################
CLEAN_UP LROUTINE
+ Cleanup work files and handle output files
    CLOCK CALENDAR INTO END_DATETIME

    debug
    CALL UTIL_TRIM_TRAILING_SPACE USING REPORT_FILE,REPORT_FILE
    CALL UTIL_PENDING_BACKUP USING "/w/tmp/",REPORT_FILE,""
    CALL UTIL_PENDING_BACKUP USING UTIL_FILES_DIR,UPDATE_FILE,""
    CALL SEND_EMAIL
    CALL UTIL_DELETE_FILE USING REPORT_FILE

    RETURN
    ENDROUTINE


.#######################################################################
SEND_EMAIL LROUTINE
+ Email the report
REPORT@         CHAR    @
RECIPIENTS      CHAR    200
SUBJECT         CHAR    100
BODY            CHAR    1000
BODY_BASE       CHAR    1000
...

    PACK SCRATCH WITH "LDH/DHH MEMEs Termed Due To Contract End"
    IF (SOMETHING_IN_LOG)
        PACK SCRATCH WITH SCRATCH," *UPDATED*"
    ELSE
        PACK SCRATCH WITH SCRATCH," *NO UPDATES REQUIRED*"
    ENDIF

    CALL UTIL_SET_SUBJECT USING SCRATCH,ENABLE_TEST,SUBJECT

    PACK BODY_BASE WITH "":
        "Checking for members with DHH network to term (contract ends ":
        CONTRACT_END,")."

    CALL UTIL_PACK_BODY_AND_COUNTERS USING BODY,BODY_BASE

    IF (SOMETHING_IN_LOG)
        CALL UTIL_SET_RECIPIENTS USING RECIPIENTS,EMAIL_TO_ERROR,TEST_EMAIL_TO
        CALL UTIL_EMAIL_NEOJ USING  C_PROG:
                                    RECIPIENTS:
                                    SUBJECT:
                                    BODY:
                                    START_DATETIME:
                                    END_DATETIME:
                                    REPORT_FILE
    ELSE
        CALL UTIL_SET_RECIPIENTS USING RECIPIENTS,EMAIL_TO_NOERROR,TEST_EMAIL_TO
        CALL UTIL_EMAIL_NEOJ USING  C_PROG:
                                    RECIPIENTS:
                                    SUBJECT:
                                    BODY:
                                    START_DATETIME:
                                    END_DATETIME
    ENDIF

    RETURN
    ENDROUTINE


.# =================================================================== #
.# Subroutines Includes
.# =================================================================== #
    INC UTIL_COMMON.SR
    INC UTIL_TABLE_IO.SR
    INC UTIL_TEST.SR
    INC UTIL_SYSTEM.SR
    INC UTIL_DATA.SR
    INC UTIL_EMAIL.SR
    INC UTIL_FORMAT_UPDATES.SR
    INC UTIL_DATES.SR
    INC UTIL_MEMBERS.SR     .MEM* io routines
    INC UTIL_BILLING.SR     .BIL* io routines
    INC UTIL_CLIENTS.SR     .CLI* io routines

.# =================================================================== #
.# End of Program
.# =================================================================== #
