++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
+ NAME:
+   a2404242.ps
+
+ DESCRIPTION:
+   Create new ENBA Life rates from BASI rates
+
+ CREATED BY:
+   apjwh - 2023-08-16
++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

.# =================================================================== #
.# Variable Includes
.# =================================================================== #
    INC VALID.RL
    INC UTIL_COMMON.CB
    INC ART.RL

.# =================================================================== #
.# Scheduler Parameters
.# =================================================================== #

PARAM_LIST          LIST WITH NAMES
PARAM_TEST          CHAR    4
                    LISTEND

.# =================================================================== #
.# Equates for code clarity
.# =================================================================== #

V_FLAG              EQU     1   ...0 or 1
V_DATE              EQU     8   ...YYYYMMDD
MSG_HEIGHT          EQU     24
REFRESH_RATE        EQU     10

.# =================================================================== #
.# Configuration
.# =================================================================== #

EMAIL_TO            INIT    "$ITAPPS"
NEW_SPEC_RULE       INIT    "OGBENB"
NEW_DEFAULT_VOLUME  NUM     "     15000"
NEW_EFF             INIT    "20250101"
RATE_TO_COPY_EFF    INIT    "20240101"
NEW_PLAN            INIT    "ENBA"
PROG_ID             INIT    "NEWLEB"

CFG_ALLOW_UPDATE    NUM     "1"

.# =================================================================== #
.# Debugging Configuration
.# =================================================================== #

ENABLE_TEST         NUM     V_FLAG
TEST_CONFIG         LIST
TEST_SKIP_RF        NUM     "0"
TEST_EMAIL          INIT    "$CURRENT_USER_EMAIL"
TEST_SKIP_UPDATE    NUM     "0"
                    LISTEND

.# =================================================================== #
.# File Declarations
.# =================================================================== #

IN_FD               FILE    FIXED=8,TEXT
IN_FILE             INIT    "/w/tmp/a2404242.in"

LOG_FD              FILE    VAR=1000,TEXT
LOG_FILE            INIT    "/w/tmp/a2404242.csv"

.# =================================================================== #
.# File lists
.# =================================================================== #

IN_LIST             LIST
IN_ID               CHAR    8
                    LISTEND

LOG_LIST            LIST
LOG_REC             CHAR    1000
                    LISTEND

LOG_HEADER          VARLIST "BASE_RATE_ID,":
                            "LOG_TYPE,":
                            "LOG_MESSAGE,":
                            "UPDATE_ID,":
                            "EFF,":
                            "TRM,":
                            "NET,":
                            "PRD,":
                            "PLAN,":
                            "RATE,":
                            "COVLVL,":
                            "AGE_FROM,":
                            "AGE_TO,":
                            "SPECIAL_RATE_RULE,":
                            "DEFAULT_VOLUME,":
                            "RD1-AMT,RD1-TYPE,RD1-WHO,":
                            "RD2-AMT,RD2-TYPE,RD2-WHO,":
                            "RD3-AMT,RD3-TYPE,RD3-WHO,":
                            "RD4-AMT,RD4-TYPE,RD4-WHO,":
                            "RD5-AMT,RD5-TYPE,RD5-WHO,":
                            "RD6-AMT,RD6-TYPE,RD6-WHO,":
                            "RD7-AMT,RD7-TYPE,RD7-WHO,":
                            "RD8-AMT,RD8-TYPE,RD8-WHO,":
                            "RD9-AMT,RD9-TYPE,RD9-WHO,":
                            "RD10-AMT,RD10-TYPE,RD10-WHO,":
                            "RD11-AMT,RD11-TYPE,RD11-WHO,":
                            "RD12-AMT,RD12-TYPE,RD12-WHO,":
                            "RD13-AMT,RD13-TYPE,RD13-WHO,":
                            "RD14-AMT,RD14-TYPE,RD14-WHO,":
                            "RD15-AMT,RD15-TYPE,RD15-WHO,":
                            "RD16-AMT,RD16-TYPE,RD16-WHO,":
                            "RD17-AMT,RD17-TYPE,RD17-WHO,":
                            "RD18-AMT,RD18-TYPE,RD18-WHO,":
                            "RD19-AMT,RD19-TYPE,RD19-WHO,":
                            "RD20-AMT,RD20-TYPE,RD20-WHO"

.# =================================================================== #
.# Program Variables
.# =================================================================== #

CURRDATE            CHAR    V_DATE
START_DATETIME      CHAR    24
END_DATETIME        CHAR    24

RATE_TO_COPY_EOY    CHAR    8

.# =================================================================== #
.# Begin Program
.# =================================================================== #
.Manual and Autosys
    CALL RUN_NOW
    CHAIN C_NEXT    ...go to next program (usually the Menu)

.Impact Scheduler Hook
    INC SCHED_NP.SR     ...for jobs that don't use the print system


.#######################################################################
RUN_NOW ROUTINE
+ Configure runtime settings

.Save stack size at program start
    RETCOUNT ORIG_RETURN

.Set applicable traps
    CALL UTIL_SET_NORMAL_TRAPS

.Get run date
    CLOCK TIMESTAMP INTO CURRDATE

    MOVE "a2404242" TO C_PROG        ...Program name
    MOVE "CREATE ENBA ART" TO C_OPER  ...Program description
    MOVE "MENU" TO C_NEXT
    MOVE "RUN_NOW" TO C_LABEL
    MOVE BLANKS TO C_FILE
    MOVE BLANKS TO C_KEYS

.Check if the program was called from the command line
    IF (C_CMDPAR)
        CALL CMD_START USING C_PROG
    ELSE
        CALL X_BLANK_SCREEN
    ENDIF

.Configure test options based on file mode. Default is Production
    CALL UTIL_SET_TESTMODE USING PARAM_TEST:  ...scheduler param
                                 ENABLE_TEST:  ...test toggle
                                 TEST_CONFIG  ...test settings list

.Set up run mode or build SCH record
    CALL SCHED_PICK

    SWITCH RUN_MODE
        CASE "N" | "B"
            CALL MAIN_PROCESS
        DEFAULT
    ENDSWITCH

    CALL NORMAL_EOJ

    RETURN
    ENDROUTINE


.#######################################################################
MAIN_PROCESS LROUTINE
+ Core logic
    debug
    CALL INITIALIZE
    CALL CREATE_RATES
    CALL CLEAN_UP

    RETURN
    ENDROUTINE


.#######################################################################
INITIALIZE LROUTINE
+ Setup runtime variables
    CLOCK TIMESTAMP TO CURRDATE
    CLOCK CALENDAR TO START_DATETIME

.Get the files and EDI locations from valid 101
    CALL UTIL_GET_SYS_DIRS_COMMON

    CALL UTIL_INIT_COUNTERS USING "BASI Rates Proc'd":
                                  "Creates":
                                  "Skipped":
                                  "":
                                  "":
                                  ""

    CALL SET_RUN_CONFIG

    RETURN
    ENDROUTINE


.#######################################################################
SET_RUN_CONFIG LROUTINE
+ Configure any runtime settings here

    IF (TEST_SKIP_RF)
        CALL UTIL_EXISTS USING IN_FILE,"FILE"
        IF NOT EQUAL
            CLEAR TEST_SKIP_RF
        ENDIF
    ENDIF

    UNPACK RATE_TO_COPY_EFF INTO DATE_CCYY
    PACK RATE_TO_COPY_EOY WITH DATE_CCYY,"1231"

    RETURN
    ENDROUTINE


.#######################################################################
CREATE_RATES LROUTINE

.Find all active LIFE ART as of 1/1/24
    CALL FIND_ACTIVE_ART

    CALL OPEN_OGBFILE USING IN_FD,IN_FILE
    CALL PREP_OGBFILE USING LOG_FD,LOG_FILE
    CALL WRITE_OGBREC_SEQ USING LOG_FD,LOG_FILE,LOG_HEADER

    CALL UTIL_XMESSAGE USING "Processing rates...",MSG_HEIGHT

    LOOP
        CALL READ_OGBFILE_SEQ USING IN_FD,IN_FILE,IN_LIST
        WHILE (RESULT = 1)

        CALL UTIL_XCOUNTER USING X_CNT1,REFRESH_RATE

        CALL UTIL_RATE_FIND USING IN_ID
        IF NOT EQUAL
            debug
            CONTINUE
        ENDIF

        CONTINUE IF (CHOP ART_TRM > "" & ART_TRM <= ART_EFF) .voids
        CONTINUE IF (CHOP ART_PLAN != "BASI") .non-basic
        CONTINUE IF (CHOP ART_EFF != RATE_TO_COPY_EFF) .not eff at the start of 2024
        IF (CHOP ART_TRM > "" & ART_TRM < RATE_TO_COPY_EOY) .terms prior to end of year
            debug .shouldn't happen
            CONTINUE
        ENDIF

        CALL CREATE_RATE
    REPEAT

    MESSAGEERASE

    CALL CLOSE_OGBFILE USING IN_FD,IN_FILE
    CALL CLOSE_OGBFILE USING LOG_FD,LOG_FILE

    RETURN
    ENDROUTINE


.#######################################################################
FIND_ACTIVE_ART LROUTINE

    RETURN IF (TEST_SKIP_RF)

    CALL UTIL_XMESSAGE USING "Searching for active rates...",MSG_HEIGHT

    PACK SCRATCH WITH "art.dbf ",IN_FILE," 1-8 -t ":
        " -p9-13eq#"LIFE #" -p23-26eq#"BASI#" -p38-45eq",RATE_TO_COPY_EFF," -p46-53eq#"        #" ":
        " or ":
        " -p9-13eq#"LIFE #" -p23-26eq#"BASI#" -p38-45eq",RATE_TO_COPY_EFF," -p46-53ge",RATE_TO_COPY_EFF

    CALL UTIL_DO_REFORMAT USING SCRATCH,"Searching for active rates"

    MESSAGEERASE

    RETURN
    ENDROUTINE


.#######################################################################
CREATE_RATE LROUTINE
HOLD_ART            CHAR    X_ART_SIZE
VOLUME_NUM_FIELD    NUM     10
...
    PACK ART_PLAN WITH NEW_PLAN,BLANKS
    PACK ART_EFF WITH NEW_EFF
    FILL " " IN ART_ID1:
                ART_TRM

    PACK ART_RULE WITH NEW_SPEC_RULE,BLANKS
    MOVE NEW_DEFAULT_VOLUME TO ART_DEFVOL

    PACK HOLD_ART WITH ART_LIST

    CALL CHECK_IF_NEW_RATE_EXISTS
    IF EQUAL
        CALL WRITE_LOG USING "SKIP","New rate already exists"
        RETURN
    ENDIF

    UNPACK HOLD_ART INTO ART_LIST

    CALL WRITE_TO_ART USING "CREATE","New ENBA rate created"

    RETURN
    ENDROUTINE


.#######################################################################
CHECK_IF_NEW_RATE_EXISTS LROUTINE
+ See if a rate exists that matches
    CLEAR ART_AIM

    PACK ART_AIM1 WITH "01X",ART_NET,BLANKS
    PACK ART_AIM2 WITH "02X",ART_PRD,BLANKS
    PACK ART_AIM4 WITH "04X",ART_PLAN,BLANKS
    PACK ART_AIM5 WITH "05X",ART_RT,BLANKS
    PACK ART_AIM6 WITH "06X",ART_DEPC,BLANKS
    PACK ART_AIM8 WITH "08X",ART_LAGE,BLANKS
    PACK ART_AIM9 WITH "09X",ART_CLIR,BLANKS

    CLEAR ART_LIST
    LOOP
        IF (CHOP ART_ID1 = "")
            CALL UTIL_RATE_AIMFIND
        ELSE
            CALL UTIL_RATE_AIMNEXT
        ENDIF
        WHILE EQUAL
        BREAK IF (ART_EFF = NEW_EFF)
    REPEAT

    RETURN
    ENDROUTINE


.#######################################################################
WRITE_TO_ART LROUTINE LOGTYPE@,LOGMSG@
LOGTYPE@        CHAR    @
LOGMSG@         CHAR    @
...
    PACK ART_CHGBY WITH PROG_ID,BLANKS
    PACK ART_CHGDT WITH CURRDATE
    PACK ART_NEWBY WITH ART_CHGBY
    PACK ART_NEWDT WITH ART_CHGDT

    CALL FORMAT_RATE_AMOUNTS

    CALL WRITE_LOG USING LOGTYPE@,LOGMSG@

.No spouse rates - skip ARTS copy
.No fees - ignore ARTF

    RETURN IF NOT (CFG_ALLOW_UPDATE)
    RETURN IF (TEST_SKIP_UPDATE)

    debug .last stop before write

    CALL FILE_WRITE USING   ART_FD:     ...File declaration list
                            ART_FN:     ...File name list
                            ART_FC:     ...(I)ndex or (A)imdex
                            ART_FV:     ...File key varlists
                            ART_FK:     ...File key sizes
                            ART_LIST:   ...File record (field list)
                            ART_REC:    ...File record (packed)
                            ART_OLD:    ...File saved when read
                            ART_QTY:    ...Number of files
                            1           ...File number for this read
    RETURN
    ENDROUTINE


.#######################################################################
FORMAT_RATE_AMOUNTS LROUTINE
    CALL FORMAT_AMT USING ART_AMT1
    CALL FORMAT_AMT USING ART_AMT2
    CALL FORMAT_AMT USING ART_AMT3
    CALL FORMAT_AMT USING ART_AMT4
    CALL FORMAT_AMT USING ART_AMT5
    CALL FORMAT_AMT USING ART_AMT6
    CALL FORMAT_AMT USING ART_AMT7
    CALL FORMAT_AMT USING ART_AMT8
    CALL FORMAT_AMT USING ART_AMT9
    CALL FORMAT_AMT USING ART_AMT10
    CALL FORMAT_AMT USING ART_AMT11
    CALL FORMAT_AMT USING ART_AMT12
    CALL FORMAT_AMT USING ART_AMT13
    CALL FORMAT_AMT USING ART_AMT14
    CALL FORMAT_AMT USING ART_AMT15
    CALL FORMAT_AMT USING ART_AMT16
    CALL FORMAT_AMT USING ART_AMT17
    CALL FORMAT_AMT USING ART_AMT18
    CALL FORMAT_AMT USING ART_AMT19
    CALL FORMAT_AMT USING ART_AMT20
    RETURN
    ENDROUTINE


.#######################################################################
FORMAT_AMT LROUTINE AMT@
AMT@        CHAR    @
FORM7.2     NUM     7.2
...
    CLEAR FORM7.2
    MOVE (SQUEEZE AMT@) TO FORM7.2
    MOVE FORM7.2 TO AMT@
    RETURN
    ENDROUTINE


.#######################################################################
WRITE_LOG LROUTINE LOGTYPE@,LOGMSG@
LOGTYPE@        CHAR    @
LOGMSG@         CHAR    @
...
    SWITCH LOGTYPE@
        CASE "CREATE"
            CALL UTIL_XCOUNTER USING X_CNT2,REFRESH_RATE
        CASE "SKIP"
            CALL UTIL_XCOUNTER USING X_CNT3,REFRESH_RATE
        DEFAULT
            debug
            CALL UTIL_ERRORMSG USING "Unknown log type"
    ENDSWITCH

    PACK LOG_REC WITH   IN_ID,COMMA:
                        LOGTYPE@,COMMA:
                        LOGMSG@,COMMA:
                        (CHOP ART_ID1),COMMA:
                        (CHOP ART_EFF),COMMA:
                        (CHOP ART_TRM),COMMA:
                        (CHOP ART_NET),COMMA:
                        (CHOP ART_PRD),COMMA:
                        (CHOP ART_PLAN),COMMA:
                        (CHOP ART_RT),COMMA:
                        (CHOP ART_DEPC),COMMA:
                        (SQUEEZE ART_LAGE),COMMA:
                        (SQUEEZE ART_HAGE),COMMA:
                        (SQUEEZE ART_RULE),COMMA:
                        (SQUEEZE ART_DEFVOL),COMMA:
                        (SQUEEZE ART_AMT1),COMMA,(SQUEEZE ART_TYP1),COMMA,(SQUEEZE ART_WHO1),COMMA:
                        (SQUEEZE ART_AMT2),COMMA,(SQUEEZE ART_TYP2),COMMA,(SQUEEZE ART_WHO2),COMMA:
                        (SQUEEZE ART_AMT3),COMMA,(SQUEEZE ART_TYP3),COMMA,(SQUEEZE ART_WHO3),COMMA:
                        (SQUEEZE ART_AMT4),COMMA,(SQUEEZE ART_TYP4),COMMA,(SQUEEZE ART_WHO4),COMMA:
                        (SQUEEZE ART_AMT5),COMMA,(SQUEEZE ART_TYP5),COMMA,(SQUEEZE ART_WHO5),COMMA:
                        (SQUEEZE ART_AMT6),COMMA,(SQUEEZE ART_TYP6),COMMA,(SQUEEZE ART_WHO6),COMMA:
                        (SQUEEZE ART_AMT7),COMMA,(SQUEEZE ART_TYP7),COMMA,(SQUEEZE ART_WHO7),COMMA:
                        (SQUEEZE ART_AMT8),COMMA,(SQUEEZE ART_TYP8),COMMA,(SQUEEZE ART_WHO8),COMMA:
                        (SQUEEZE ART_AMT9),COMMA,(SQUEEZE ART_TYP9),COMMA,(SQUEEZE ART_WHO9),COMMA:
                        (SQUEEZE ART_AMT10),COMMA,(SQUEEZE ART_TYP10),COMMA,(SQUEEZE ART_WHO10),COMMA:
                        (SQUEEZE ART_AMT11),COMMA,(SQUEEZE ART_TYP11),COMMA,(SQUEEZE ART_WHO11),COMMA:
                        (SQUEEZE ART_AMT12),COMMA,(SQUEEZE ART_TYP12),COMMA,(SQUEEZE ART_WHO12),COMMA:
                        (SQUEEZE ART_AMT13),COMMA,(SQUEEZE ART_TYP13),COMMA,(SQUEEZE ART_WHO13),COMMA:
                        (SQUEEZE ART_AMT14),COMMA,(SQUEEZE ART_TYP14),COMMA,(SQUEEZE ART_WHO14),COMMA:
                        (SQUEEZE ART_AMT15),COMMA,(SQUEEZE ART_TYP15),COMMA,(SQUEEZE ART_WHO15),COMMA:
                        (SQUEEZE ART_AMT16),COMMA,(SQUEEZE ART_TYP16),COMMA,(SQUEEZE ART_WHO16),COMMA:
                        (SQUEEZE ART_AMT17),COMMA,(SQUEEZE ART_TYP17),COMMA,(SQUEEZE ART_WHO17),COMMA:
                        (SQUEEZE ART_AMT18),COMMA,(SQUEEZE ART_TYP18),COMMA,(SQUEEZE ART_WHO18),COMMA:
                        (SQUEEZE ART_AMT19),COMMA,(SQUEEZE ART_TYP19),COMMA,(SQUEEZE ART_WHO19),COMMA:
                        (SQUEEZE ART_AMT20),COMMA,(SQUEEZE ART_TYP20),COMMA,(SQUEEZE ART_WHO20)

    CALL WRITE_OGBREC_SEQ USING LOG_FD,LOG_FILE,LOG_LIST

    RETURN
    ENDROUTINE


.#######################################################################
CLEAN_UP LROUTINE
+ Cleanup work files and handle output files
    debug
    CLOCK CALENDAR INTO END_DATETIME

    CALL UTIL_TRIM_TRAILING_SPACE USING LOG_FILE,LOG_FILE

    CALL EMAIL_NEOJ

    CALL UTIL_PENDING_BACKUP USING "/w/tmp",LOG_FILE,""
    CALL UTIL_DELETE_FILE USING LOG_FILE

    RETURN
    ENDROUTINE


.#######################################################################
EMAIL_NEOJ LROUTINE
REPORT@         CHAR    @
RECIPIENTS      CHAR    200
SUBJECT         CHAR    100
BODY            CHAR    1000
BODY_BASE       CHAR    1000
...
    PACK SCRATCH WITH "ADHOC: Enhanced Basic Life Rate Creator"

    CALL UTIL_SET_SUBJECT USING SCRATCH,ENABLE_TEST,SUBJECT

    PACK BODY_BASE WITH "":
        "Attached is a report of new ENBA rates created for ",NEW_EFF

    CALL UTIL_PACK_BODY_AND_COUNTERS USING BODY,BODY_BASE

    CALL UTIL_SET_RECIPIENTS USING RECIPIENTS,EMAIL_TO,TEST_EMAIL
    CALL UTIL_EMAIL_NEOJ USING  C_PROG:
                                RECIPIENTS:
                                SUBJECT:
                                BODY:
                                START_DATETIME:
                                END_DATETIME:
                                LOG_FILE
    RETURN
    ENDROUTINE


.# =================================================================== #
.# Subroutines Includes
.# =================================================================== #
    INC UTIL_COMMON.SR
    INC UTIL_TABLE_IO.SR
    INC UTIL_TEST.SR
    INC UTIL_SYSTEM.SR
    INC UTIL_DATA.SR
    INC UTIL_EMAIL.SR
    INC UTIL_MEMBERS.SR     .MEM* io routines
    INC UTIL_BILLING.SR     .BIL* io routines
    INC UTIL_CLIENTS.SR     .CLI* io routines
    INC UTIL_ART.SR

.# =================================================================== #
.# End of Program
.# =================================================================== #
